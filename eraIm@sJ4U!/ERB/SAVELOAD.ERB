;[ライセンス]パブリックドメイン
;本ファイルに係る著作権を放棄する。
;本ファイルに係る著作人格権は行使しない。
;2009/3/28 MinorShift(Emuera作者)

;独自のセーブ・ロードシステムの例（Emuera  ver 1.50以降対象）
;LOADGAMEの代わりにCALL LOADGAME_EX、SAVEGAMEの代わりにCALL SAVEGAME_EXを実行してください。
;実際に使用するには旧来の@SAVEINFOに合わせて@SAVEINFO_EXを編集する必要があります。
;使用している命令の詳細についてはEmueraの拡張文法.txtを参照してください。
;

@SAVEGAME_EX
CALL SET_COLOR(25)
PRINTFORML ∩（＞ヮ＜）q＜セーブしよー！
RESETCOLOR
CALL PRINT_SAVEDATA
PRINTL [100] 戻る
INPUT
IF (RESULT == 100)
	RETURN
ELSEIF ((RESULT < 0) || (RESULT >= 20))
	RESTART
ELSE
	LOCAL = RESULT
	CHKDATA LOCAL
	;RESULT == 1は"ファイルが存在しない"
	IF RESULT != 1
		PRINTL 既にデータが存在します。上書きしますか？
		PRINTL [0] はい
		PRINTL [1] いいえ
$INPUT_LOOP
		INPUT
		IF RESULT == 1
			RESTART
		ELSEIF RESULT != 0
			GOTO INPUT_LOOP
		ENDIF
	ENDIF
	CALL SAVEINFO_EX
	SAVEDATA LOCAL, RESULTS
ENDIF
CALL SET_COLOR(25)
PRINTFORMW セーブしたよー！＞∩（＞ヮ＜）q
RESETCOLOR

@LOADGAME_EX
CALL SET_COLOR(25)
PRINTFORML ∩（＞ヮ＜）q＜ロードしよー！
RESETCOLOR
CALL PRINT_SAVEDATA

CHKDATA 99
PRINTFORML [99] %RESULTS:0%
PRINTL [100] 戻る
INPUT
IF (RESULT == 100)
	RETURN
	;CALL SYSTEM_TITLE
ELSEIF ((RESULT < 0) || (RESULT >= 20)) && (RESULT != 99)
	RESTART
ELSE
	LOCAL = RESULT
	CHKDATA LOCAL
	;RESULT == 0ならロード可能
	IF RESULT != 0
		RESTART
	ENDIF
	CALL SET_COLOR(25)
	PRINTFORMW ロードしたよー！＞∩（＞ヮ＜）q
	GLOBAL:801 = 0
	SAVEGLOBAL
	RESETCOLOR
	LOADDATA LOCAL
ENDIF

@PRINT_SAVEDATA
;呼び出し元でCOUNTを使っているかも知れないのでCOUNTを保護
LOCAL:0 = COUNT
REPEAT 20
	CHKDATA COUNT
	IF COUNT < 10
		PRINTFORML [ {COUNT}] %RESULTS:0%
	ELSE
		PRINTFORML [{COUNT}] %RESULTS:0%
	ENDIF
REND
COUNT = LOCAL:0
RETURN

@SAVEINFO_EX
;SAVEDATA命令は@SAVEINFO関数を呼ばないのでPUTFORMの代わりの文字列を用意する必要がある
;GETTIME命令でRESULTSに現在時刻を代入
GETTIME
IF !TIME && !TIME:12
	RESULTS:0 = %RESULTS:0% {DAY+1}日目朝
ELSEIF !TIME
	RESULTS:0 = %RESULTS:0% {DAY+1}日目昼
ELSE
	RESULTS:0 = %RESULTS:0% {DAY+1}日目夜
ENDIF
IF STRLENS(SAVESTR:11)
	RESULTS:0 = %RESULTS:0% %SAVESTR:11%
ELSE
	IF TARGET >= 0
		RESULTS:0 = %RESULTS:0% パートナー：%NAME:TARGET%
		SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
			RESULTS:0 = %RESULTS:0% (好感度：{CFLAG:2}) 
	ENDIF
	SIF ASSI >= 0
		RESULTS:0 = %RESULTS:0% 助手：%NAME:ASSI%
ENDIF

IF FLAG:2 == 1
	RESULTS:0 = %RESULTS:0%  I-
ELSE
	RESULTS:0 = %RESULTS:0%  
ENDIF

SIF FLAG:5 == 0
	RESULTS:0 = %RESULTS:0% E@SY
SIF FLAG:5 == 1
	RESULTS:0 = %RESULTS:0% NORM@L
SIF FLAG:5 == 2
	RESULTS:0 = %RESULTS:0% H@RD
SIF FLAG:5 == 3
	RESULTS:0 = %RESULTS:0% M@STER
SIF FLAG:5 == 4
	RESULTS:0 = %RESULTS:0% S-Lunatic
SIF FLAG:5 == 9
	RESULTS:0 = %RESULTS:0% EXTRA
SIF FLAG:5 == 10
	RESULTS:0 = %RESULTS:0% P@RANOIA

SIF FLAG:26 > 1
	RESULTS:0 = %RESULTS:0%  {FLAG:26}周目

SIF FLAG:28
	RESULTS:0 = %RESULTS:0%  ☆

RETURN

@TITLE_LOADGAME
;タイトル画面のロードでも独自のロード画面を使う場合、@TITLE_LOADGAMEを定義する
;@TITLE_LOADGAMEが定義されていると、標準のロード画面の代わりにTITLE_LOADGAMEが呼ばれる。
CALL LOADGAME_EX
RETURN
;RETURNするとタイトルへ戻る。

@SYSTEM_AUTOSAVE
;オートセーブの処理を独自に行う場合、@SYSTEM_AUTOSAVEを定義する
;@SYSTEM_AUTOSAVEが定義されていると、標準のオートセーブの代わりにSYSTEM_AUTOSAVEが呼ばれる。
CALL SAVEINFO_EX
SAVEDATA 99, RESULTS


