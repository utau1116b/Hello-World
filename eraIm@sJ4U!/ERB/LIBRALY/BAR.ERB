;--------------------------------------------------
;テンション表示関数
;アイドルの機嫌の良さ
;高いとコマンド実行しやすい
;--------------------------------------------------
;eratohoJの酒気表示を変えただけ
@TENSION_SHOW
GETFONT
CHKFONT "Times New Roman"
SIF RESULT
	SETFONT "Times New Roman"
CALL PRINT_BAR, TFLAG:47, 100, 10, UNICODE(0x2665), UNICODE(0x2665), BARCOLORSET("紫"), RESULT:1

SETFONT RESULTS

RETURN 0

;-------------------------------------------------
;各種ゲージのバー表示汎用処理
;-------------------------------------------------
;ARG　……キャラ位置を指定
;ARG:1……基礎の番号を指定
;ARG:2……表示形式(0=PRINTFORM, 1=PRINTFORML, 2=表示しない, 3=PRINTFORM(後スペースなし))
;ARG:3……バーサイズ表示（0 ノーマル,　1 スリム）
;ARG:4……低気力時カラー (0 あり, 1 1以下で赤, 2 なし)
@VITAL_BAR, ARG, ARG:1, ARG:2, ARG:3
SIF STRLENS(BASENAME:(ARG:1)) <= 0
	RETURN 0
	
PRINTFORM %BASENAME:(ARG:1)%%UNICODE(0x208D)%
BASE:ARG:(ARG:1) = LIMIT(BASE:ARG:(ARG:1), 0, MAXBASE:ARG:(ARG:1))
IF ARG:3 == 1

	CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 10, UNICODE(0x2583), UNICODE(0x2583), 0x367193, 0x501010, -256*9

ELSE

	CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 32, UNICODE(0x2585), UNICODE(0x2585), 0x367193, 0x501010, -256*4

ENDIF
	PRINTFORM %UNICODE(0x208E)%
IF ARG:2 == 1
	PRINTFORML 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSEIF ARG:2 == 3
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSE
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}　
ENDIF


@LIFE_BAR, ARG, ARG:1, ARG:2, ARG:3, ARG:4
SIF STRLENS(BASENAME:(ARG:1)) <= 0
	RETURN 0
	
PRINTFORM %BASENAME:(ARG:1)%%UNICODE(0x208D)%
BASE:ARG:(ARG:1) = LIMIT(BASE:ARG:(ARG:1), 0, MAXBASE:ARG:(ARG:1))
IF ARG:3 == 1

			CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 10, UNICODE(0x2583), UNICODE(0x2583), 0xe45c5e, 0x152030, -256*9

ELSE

			CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 32, UNICODE(0x2585), UNICODE(0x2585), 0xe45c5e, 0x504010, -256*4

ENDIF
	PRINTFORM %UNICODE(0x208E)%
IF ARG:2 == 1
	PRINTFORML 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSEIF ARG:2 == 3
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSE
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}　
ENDIF

;パースエイダ―
;口説きや説得
@PERSUADE_BAR, ARG, ARG:1, ARG:2, ARG:3, ARG:4
SIF STRLENS(BASENAME:(ARG:1)) <= 0
	RETURN 0
	
PRINTFORM 口説
PRINTFORM %UNICODE(0x208D)%
BASE:ARG:(ARG:1) = LIMIT(BASE:ARG:(ARG:1), 0, MAXBASE:ARG:(ARG:1))
IF ARG:3 == 1
	CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 10, UNICODE(0x2583), UNICODE(0x2583), 0xCCFFFF, 0x205050, 327680
ELSE
	CALL PRINT_COLORBARQ, BASE:ARG:(ARG:1), MAXBASE:ARG:(ARG:1), 32, UNICODE(0x2585), UNICODE(0x2585), 0xCCFFCC, 0x205020, 327685
ENDIF
	PRINTFORM %UNICODE(0x208E)%
IF ARG:2 == 1
	PRINTFORML 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSEIF ARG:2 == 3
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}
ELSE
	PRINTFORM 　{BASE:ARG:(ARG:1), 4}/{MAXBASE:ARG:(ARG:1), 4}　
ENDIF


;ARG:0 ボーカル
;春香色
@PRINT_BAR_VO(ARG:0)
CALL PRINT_BAR, ARG:0, 1000, 32, UNICODE(0x2585), UNICODE(0x2585), 0xe03032, 0x333333

;ARG:0 ヴィジュアル
;真美色
@PRINT_BAR_VI(ARG:0)
CALL PRINT_BAR, ARG:0, 1000, 32, UNICODE(0x2585), UNICODE(0x2585), 0Xfde240, 0x333333

;ARG:0 ダンス
;千早色
@PRINT_BAR_DA(ARG:0)
CALL PRINT_BAR, ARG:0, 1000, 32, UNICODE(0x2585), UNICODE(0x2585), 0x2947ce, 0x333333

;ARG:0 キズナ
;伊織色
@PRINT_BAR_UNI(ARG:0)
CALL PRINT_BAR, ARG:0, 99, 32, UNICODE(0x2585), UNICODE(0x2585), 0xfd9be0, 0x333333

;ARG:0	現在経験値
;ARG:1	次のレベルまで
;美希色
@PRINT_BAR_NX_LV(ARG:0, ARG:1)
CALL PRINT_COLORBARQ, ARG:0, ARG:1, 16, UNICODE(0x2585), UNICODE(0x2585), 0x5d9057, 0x152030, -256*3

;ARG:0	現在地
;ARG:1	マックスステージ
;雪歩色
@PRINT_BAR_STAGE(ARG:0, ARG:1)
CALL PRINT_BAR, ARG:0, ARG:1, 28, UNICODE(0x2585), UNICODE(0x2585), 0Xd6dfeb, 0x333333

;VLは400まで
;ARG:3	長さ
@PRINT_BAR_VL, ARG, ARG:1, ARG:2, ARG:3
VARSET LOCAL
IF MAXBASE:ARG:ボルテージ > 300
	IF BASE:ARG:ボルテージ <= 100
		A = BASE:ARG:ボルテージ
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 200
		A = BASE:ARG:ボルテージ - 100
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 300
		A = BASE:ARG:ボルテージ - 200
		B = 100
	ELSE
		A = BASE:ARG:ボルテージ - 300
		B = 100
	ENDIF
ELSEIF MAXBASE:ARG:ボルテージ > 200
	IF BASE:ARG:ボルテージ <= 100
		A = BASE:ARG:ボルテージ
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 200
		A = BASE:ARG:ボルテージ - 100
		B = 100
	ELSE
		A = BASE:ARG:ボルテージ
		B = 100
	ENDIF
ELSEIF MAXBASE:ARG:ボルテージ > 100
	IF BASE:ARG:ボルテージ <= 100
		A = BASE:ARG:ボルテージ
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 200
		A = BASE:ARG:ボルテージ - 100
		B = 100
	ELSE
		A = BASE:ARG:ボルテージ
		B = 100
	ENDIF
ELSE
	IF BASE:ARG:ボルテージ <= 100
		A = BASE:ARG:ボルテージ
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 200
		A = BASE:ARG:ボルテージ - 100
		B = 100
	ELSEIF BASE:ARG:ボルテージ <= 300
		A = BASE:ARG:ボルテージ - 200
		B = 100
	ELSE
		A = BASE:ARG:ボルテージ - 300
		B = 100
	ENDIF
ENDIF

IF ARG:3 > 0
	LOCAL = 15
ELSE
	LOCAL = 10
ENDIF

;赤
IF BASE:ARG:ボルテージ > 300
	CALL PRINT_COLORBARQ, A, B, LOCAL, UNICODE(0x2583), UNICODE(0x2583), 0xF70F1F, 0x152030, -256*9
;オレンジ
ELSEIF BASE:ARG:ボルテージ > 200
	CALL PRINT_COLORBARQ, A, B, LOCAL, UNICODE(0x2583), UNICODE(0x2583), 0xF09000, 0x152030, -256*9
;緑
ELSEIF BASE:ARG:ボルテージ > 100
	CALL PRINT_COLORBARQ, A, B, LOCAL, UNICODE(0x2583), UNICODE(0x2583), 0xA9D053, 0x152030, -256*3
;青
ELSE
	CALL PRINT_COLORBARQ, A, B, LOCAL, UNICODE(0x2583), UNICODE(0x2583), 0x0082FF, 0x152030, -256*9
ENDIF

@PRINT_BAR_S(ARG, ARG:1, ARG:2)
IF (ARG * 100 / ARG:1) >= 50
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xff6666, 0x642828
ELSEIF (ARG * 100 / ARG:1) >= 25
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xff6666, 0x642828
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xff6666, 0x642828
ENDIF

@PRINT_BAR_T(ARG, ARG:1, ARG:2)
IF (ARG * 100 / ARG:1) >= 50
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xb4de4a, 0x47571d
ELSEIF (ARG * 100 / ARG:1) >= 25
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xb4de4a, 0x47571d
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xb4de4a, 0x47571d
ENDIF

@PRINT_BAR_V(ARG, ARG:1, ARG:2)
IF (ARG * 100 / ARG:1) >= 50
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0f70f1, 0x062c5f
ELSEIF (ARG * 100 / ARG:1) >= 25
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0f70f1, 0x062c5f
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0f70f1, 0x062c5f
ENDIF

@PRINT_BAR_VIT(ARG, ARG:1, ARG:2)
CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xffe43f, 0x645919

;ARG:0 表示するキャラ
;ARG:1 表示したい技能
@NEW_PRINT_ABL_BAR(ARG:0, ARG:1)
	PRINTFORM %ABLNAME:(ARG:1)%({ABL:(ARG:0):(ARG:1),3,RIGHT})　
	;PRINT [
	CALL PRINT_COLORBAR, ABL:(ARG:0):(ARG:1), 10, 10, UNICODE(0x258A), UNICODE(0x258A), 0XFFFFFF, 0X000000
	;PRINT ]

;ARG:0 表示するキャラ
;ARG:1 表示したいフラグ番号
@NEW_PRINT_CFLAG_BAR(ARG:0, ARG:1)
PRINTFORM ({CFLAG:(ARG:0):(ARG:1),4,RIGHT})　
CALL PRINT_COLORBAR, CFLAG:(ARG:0):(ARG:1), 500, 32, UNICODE(0x258A), UNICODE(0x258A), 0XFFFFFF, 0X000000

;仕事資質表示に使う
;ARG:0 表示するキャラ
;ARG:1 表示したい数値
@NEW_PRINT_ABL_BAR2(ARG:0, ARG:1)

LOCALS =  %UNICODE(0X2585)%%UNICODE(0X2585)%%UNICODE(0X2585)%

CALL PRINT_COLORBARQ, ARG:1, 8, 8, LOCALS, LOCALS, 0Xff7b2e, 0X000000, -256*5

;歌唱資質表示に使う
;ARG:0 表示するキャラ
;ARG:1 表示したい数値
@NEW_PRINT_ABL_BARVO(ARG:0, ARG:1)

LOCALS =  %UNICODE(0X2585)%%UNICODE(0X2585)%%UNICODE(0X2585)%

CALL PRINT_COLORBARQ, ARG:1, 8, 8, LOCALS, LOCALS, 0Xff6bb7, 0X000000, -256*5

;被写資質表示に使う
;ARG:0 表示するキャラ
;ARG:1 表示したい数値
@NEW_PRINT_ABL_BARVI(ARG:0, ARG:1)

LOCALS =  %UNICODE(0X2585)%%UNICODE(0X2585)%%UNICODE(0X2585)%

CALL PRINT_COLORBARQ, ARG:1, 8, 8, LOCALS, LOCALS, 0Xf9c752, 0X000000, -256*5

;舞踊資質表示に使う
;ARG:0 表示するキャラ
;ARG:1 表示したい数値
@NEW_PRINT_ABL_BARDA(ARG:0, ARG:1)

LOCALS =  %UNICODE(0X2585)%%UNICODE(0X2585)%%UNICODE(0X2585)%

CALL PRINT_COLORBARQ, ARG:1, 8, 8, LOCALS, LOCALS, 0X9494ef, 0X000000, -256*5

;装備補正と素のステータスを分けて表示
;ARG	対象
;ARG:1	表示したい能力
;ARG:2	ステータスの最高値
;ver0.700より仕様を変えた為、CFLAGは使ってない
@NEWPRINT_CFLAGBAR2(ARG, ARG:1, ARG:2)
VARSET LOCAL
PRINTFORM ({BASE:(ARG:0):(ARG:1),4,RIGHT})　
FOR LOCAL, 0, 100
	IF BASE:ARG:(ARG:1 - 40) * 100 * 100 > LOCAL * ARG:2 * 100
		SETCOLOR 0xe0e0e0
	ELSEIF BASE:ARG:(ARG:1) * 100 * 100 > LOCAL * ARG:2 * 100
		SETCOLOR 0x9090E8
	ELSE
		
	ENDIF
	PRINTFORM %UNICODE(0x258A)%
	SETCOLOR GETBGCOLOR()
NEXT
RESETCOLOR
;ARG	対象
;ARG:1	表示したいステータス
;ARG:2	ステータスの最高値
@NEWPRINT_VOVIDABAR(ARG, ARG:1, ARG:2)
VARSET LOCAL

;VO
IF ARG:1 == 81
	;VO元色
	LOCAL:100 = 0Xf50172
	;VO補正色 
	LOCAL:101 = 0Xbf093a
	;減色率 
	LOCAL:102 = -256*3
;VI
ELSEIF ARG:1 == 82
	;VI元色
	LOCAL:100 = 0Xfde53d
	;VI補正色 
	LOCAL:101 = 0Xfda800
	;減色率 
	LOCAL:102 = -256*3
;DA
ELSE
	;DA元色
	LOCAL:100 = 0X0cadfb
	;DA補正色 
	LOCAL:101 = 0X0061ec
	;減色率 
	LOCAL:102 = -256*3
ENDIF

;ＵＮＩＴ
IF ARG == 999
	FOR LOCAL, 1080, 1085
		IF FLAG:LOCAL > 0
			LOCAL:105 += BASE:(FLAG:LOCAL):(ARG:1 - 40)
			LOCAL:106 += BASE:(FLAG:LOCAL):(ARG:1)
			LOCAL:107 ++
		ENDIF
	NEXT
	LOCAL:105 = LOCAL:105 * 10000 / LOCAL:107 / 10000
	LOCAL:106 = LOCAL:106 * 10000 / LOCAL:107 / 10000
	FOR LOCAL, 0, 50
		IF LOCAL:105 * 50 * 100 > ARG:2  * LOCAL * 100
			SETCOLOR LOCAL:100 - (LOCAL:102 * LOCAL)
		ELSEIF LOCAL:106 * 50 * 100 > LOCAL * ARG:2 *100
			SETCOLOR LOCAL:101 - (LOCAL:102 * LOCAL)
		ELSE
			SETCOLOR 0X333333
		ENDIF
		PRINTFORM %UNICODE(0x2585)%
	NEXT
ELSE
	FOR LOCAL, 0, 32
		IF BASE:ARG:(ARG:1 - 40) * 32 * 100 > ARG:2  * LOCAL * 100
			SETCOLOR LOCAL:100 - (LOCAL:102 * LOCAL)
		ELSEIF BASE:ARG:(ARG:1) * 32 * 100 > LOCAL * ARG:2 *100
			SETCOLOR LOCAL:101 - (LOCAL:102 * LOCAL)
		ELSE
			SETCOLOR 0X333333
		ENDIF
		PRINTFORM %UNICODE(0x2585)%
	NEXT
ENDIF
RESETCOLOR

;ARG 衣装番号
@装備用VOVIDABAR(ARG)
SIF ARG == 0
	RETURN
VARSET LOCAL
TRYCALLFORM VO_{ARG}
LOCAL:1 = RESULT / 100
TRYCALLFORM VI_{ARG}
LOCAL:2 = RESULT / 100
TRYCALLFORM DA_{ARG}
LOCAL:3 = RESULT / 100
CALL PRINT_COLORBARQ, LOCAL:1, 50, 20, UNICODE(0x2585), UNICODE(0x2585), 0Xbf093a, 0X333333, -256*5
PRINTL
CALL PRINT_COLORBARQ, LOCAL:2, 50, 20, UNICODE(0x2585), UNICODE(0x2585), 0Xfda800, 0X333333, -256*5
PRINTL
CALL PRINT_COLORBARQ, LOCAL:3, 50, 20, UNICODE(0x2585), UNICODE(0x2585), 0X0061ec, 0X333333, -256*5
PRINTL

;オーディション・ライブ用、バー表示関数
@BAR_LAF_TIME()
;24
CALL PRINT_COLORBARQ, TFLAG:アピールタイム, TFLAG:アピールタイム記憶, 16, UNICODE(0x2585), UNICODE(0x2585), 0x213e9c, 0x152030, -256*5

@BAR_ボルテージ(ARG)
IF BASE:ARG:ボルテージ < 10000
;0x83ea7d
	TSTR = %" "%
	SIF ARG == 1
		PRINTFORM %TSTR, 4%
	CALL PRINT_COLORBARQ, BASE:ARG:ボルテージ, 10000, 32, UNICODE(0x2585), UNICODE(0x2585), 0x98cc8e, 0x909181, -256*1
	SIF ARG == 0
		PRINTFORM %TSTR, 4%
	;MAX時は輝きが増す
ELSE
;0x99f6b0
	SIF ARG == 1
		PRINTFORM  MAX
		
	CALL PRINT_COLORBARQ, BASE:ARG:ボルテージ, 10000, 32, UNICODE(0x2585), UNICODE(0x2585), 0x99f6b0, 0x909181, 0
	SIF ARG == 0
		PRINTFORM  MAX
ENDIF
@BAR_待機時間(ARG)
CALL PRINT_COLORBARQ, 0, 500, 18, UNICODE(0x2585), UNICODE(0x2585), 0x213e9c, 0x909181, -256*5


;=============================================================================
;システムに関わる処理
;=============================================================================
;-------------------------------------------------
;体力・気力バー表示
;ARG:1 (1:理性表示有り)
;ARG:2 (1:ダミー表示)
;-------------------------------------------------
;ARG　……キャラ位置を指定
@COMMON_SHOW_LIFEBAR1, ARG, ARG:1, ARG:2

IF ARG:2
	CALL PRINT_COLORBARQ, 0, 100, 32, UNICODE(0x2585), UNICODE(0x2585), 0xe45c5e, 0x152030, -256*3
ELSE
	CALL PRINT_COLORBARQ, BASE:ARG:0, MAXBASE:ARG:0, 32, UNICODE(0x2585), UNICODE(0x2585), 0xe45c5e, 0x152030, -256*3
ENDIF

@COMMON_SHOW_LIFEBAR2, ARG, ARG:1, ARG:2

IF ARG:2
	CALL PRINT_COLORBARQ, 0, 100, 32, UNICODE(0x2585), UNICODE(0x2585), 0x367193, 0x504010, -256*3
ELSE
	CALL PRINT_COLORBARQ, BASE:ARG:1, MAXBASE:ARG:1, 32, UNICODE(0x2585), UNICODE(0x2585), 0x367193, 0x501010, -256*3
ENDIF

@COMMON_SHOW_LIFEBAR1S, ARG, ARG:1, ARG:2

CALL PRINT_COLORBARQ, BASE:ARG:0, MAXBASE:ARG:0, 16, UNICODE(0x2585), UNICODE(0x2585), 0xe45c5e, 0x152030, -256*3


;-------------------------------------------------------------------------------
;    BARカラー化 (eramegatenより一部参照)
;-------------------------------------------------------------------------------
;    例  PRINTFORM %PRINT_COLORBAR(40,100,8,%UNICODE(0x2584)%,%UNICODE(0x2584)%,,0x404040,)% 
;        [===     ]
;	引数はBARとほぼ一緒 + ARGSで描写文字の変更が可能(指定しない場合はBARと同じ仕様)
;	ARG:6はバー色(0xFFFFFF形式で指定),
;	ARG:7はバー背景色(0xFFFFFF形式で指定),
;	ARG:8はバーグラデーション幅(バー色数値に10進で四則演算)
;===============================================================================
@PRINT_COLORBARQ(ARG, ARG:1, ARG:2, ARGS = "*", ARGS:1 = ".", ARG:3, ARG:4, ARG:5 = 0)
LOCAL:1 = ARG:1 / (ARG:2+ARG:2/32)
LOCAL:2 = GETCOLOR()
LOCAL:3 = COUNT

REPEAT ARG:2
ARG = ARG - LOCAL:1
IF ARG >= 0
	SETCOLOR ARG:3 - (ARG:5 * COUNT)
	PRINTFORM %ARGS%
ELSE
	SETCOLOR ARG:4
	PRINTFORM %ARGS:1%
ENDIF
REND
COUNT = LOCAL:3
SETCOLOR LOCAL:2



;-------------------------------------------------------------------------------
;BAR表示用関数
;カラーバー表示をOFFにしている場合ARG、ARG:1、ARG:2を利用して従来のBARで表示する
;-------------------------------------------------------------------------------
@PRINT_BAR(ARG, ARG:1, ARG:2, ARGS, ARGS:1, ARG:3, ARG:4)
IF FLAG:23 & 256
	PRINT  
	CALL PRINT_COLORBAR, ARG, ARG:1, ARG:2, ARGS, ARGS:1, ARG:3, ARG:4
	PRINT  
ELSE
	BAR ARG, ARG:1, ARG:2
ENDIF

;-------------------------------------------------------------------------------
;BAR表示用関数・体力用
;3段階で色が変わる体力用カラーバー
;-------------------------------------------------------------------------------
@PRINT_BAR_0(ARG, ARG:1, ARG:2)
IF (ARG * 100 / ARG:1) > 50 && ARG > 650
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0082FF, 0x002050
ELSEIF (ARG * 100 / ARG:1) > 25 && ARG > 500
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFFF00A, 0x505000
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFF0000, 0x500000
ENDIF

;-------------------------------------------------------------------------------
;BAR表示用関数・耐力用
;3段階で色が変わる耐力用カラーバー
;-------------------------------------------------------------------------------
@PRINT_BAR_4(ARG, ARG:1, ARG:2)
IF (ARG * 100 / ARG:1) >= 50
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0082FF, 0x002050
ELSEIF (ARG * 100 / ARG:1) >= 25
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFFF00A, 0x505000
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFF0000, 0x500000
ENDIF

;-------------------------------------------------
;カスタムバー用色セット関数@BARCOLORSET
;PRINT_COLORBARのARG:3に利用し、ARG;4にRESULTを使う
;-------------------------------------------------
@BARCOLORSET, ARGS
#FUNCTION
;LOCALに色を代入
SELECTCASE ARGS
	CASE "濃赤"
		RESULT:1 = 0x701000
		RETURNF 0xF06050
	CASE "赤"
		RESULT:1 = 0x502020
		RETURNF 0xC07070
	CASE "青"
		RESULT:1 = 0x202050
		RETURNF 0x7070C0
	CASE "薄青"
		RESULT:1 = 0x002060
		RETURNF 0x50B0FF
	CASE "紺"
		RESULT:1 = 0x100060
		RETURNF 0x6666FF
	CASE "緑"
		RESULT:1 = 0x205020
		RETURNF 0x66DD66
	CASE "紫"
		RESULT:1 = 0x502050
		RETURNF 0xC070C0
	CASE "黄"
		RESULT:1 = 0x505020
		RETURNF 0xC0B050
	CASE "桃"
		RESULT:1 = 0x300020
		RETURNF 0xFFCCFF
	CASE "青緑"
		RESULT:1 = 0x205050
		RETURNF 0x70C0C0
	CASE "灰"
		RESULT:1 = 0x333333
		RETURNF 0x666666
	;白
	CASEELSE
		RESULT:1 = 0x202020
		RETURNF 0xC0C0C0
ENDSELECT

