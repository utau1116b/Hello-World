;FileName_SOURCE_CALLCOM.ERB ----------------------- Rev1.01
;@COM呼出統括
;CALL		USER
;ARG		キャラNO
;RETURN		VOID
;COMMENT	@COMを呼び出す。コンボ発生判定もここ
;-----------------------------------------------------------
@CALL_COM(ARG)
DEBUGPRINTFORML SELECTCOM = {SELECTCOM}　PREVCOM = {PREVCOM}
;↓もしかしたら派生コマンドは上手く動作してないかも？

LOCALS = 0

;=== ウフフ系派生コマンド ===
IF TFLAG:うふふ
	;シックスナイン
	IF (GET_INSERT_CHARA(ARG,"M","P") == PLAYER || GET_INSERT_CHARA(ARG,"M","C") == PLAYER) && (GET_INSERT_CHARA(ARG,"P","M") == PLAYER || GET_INSERT_CHARA(ARG,"C","M") == PLAYER) && (SELECTCOM == [[調教:クンニ]] || SELECTCOM == [[調教:フェラチオ]])
		TFLAG:特殊COM = 1
		TFLAG:派生COM番号 = 69
	;岩清水
	ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == [[調教:クンニ]]
		TFLAG:特殊COM = 2
		TFLAG:派生COM番号 = 87
	;Gスポット刺激
	ELSEIF SELECTCOM == [[調教:指挿入れ]] && PREVCOM == SELECTCOM
	; && TFLAG:103 == ARG
		TFLAG:特殊COM = 3
		TFLAG:派生COM番号 = 84
	;乱れ牡丹
	ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == [[調教:背面座位]]
		TFLAG:特殊COM = 4
		TFLAG:派生COM番号 = 88
	;手淫フェラ
	ELSEIF (GET_INSERT_CHARA(ARG,"F","P") == PLAYER && GET_INSERT_CHARA(ARG,"M","P",1) == PLAYER) || (GET_INSERT_CHARA(ARG,"M","P") == PLAYER && GET_INSERT_CHARA(ARG,"F","P",1) == PLAYER)
		;フェラ-手淫
		IF GET_INSERT_CHARA(ARG,"F","P") == PLAYER
			TFLAG:SELECTCOM分岐2 = 0
		;手淫-フェラ
		ELSE
			TFLAG:SELECTCOM分岐2 = 1
		ENDIF
		TFLAG:特殊COM = 5
	;挿入Ｇスポット責め、子宮口責め、交互挿入
	ELSEIF (SELECTCOM == [[調教:正常位]] || SELECTCOM == [[調教:後背位]] || SELECTCOM == [[調教:騎乗位]] || SELECTCOM == [[調教:対面座位]] || SELECTCOM == [[調教:背面座位]]) 
		IF TEQUIP:ARG:体位 == TEQUIP:ARG:体位２ && GET_INSERT_CHARA(ARG,"V","P",1) == PLAYER
			IF SELECTCOM == [[調教:騎乗位]] || RAND:2
				TFLAG:特殊COM = 7
				TFLAG:派生COM番号 = 255
			ELSE
				TFLAG:特殊COM = 6
				TFLAG:派生COM番号 = 256
			ENDIF
		ENDIF
		;交互挿入
		IF GET_INSERT_CHARA(ARG,"V","P") == PLAYER && (MASTER_POSE(6,1,1) && MASTER_POSE(6,1,1) != ARG)
			TFLAG:特殊COM = 13
			TFLAG:派生COM番号 = 64
		ENDIF
	;シックスナインパイズリ
	ELSEIF GET_INSERT_CHARA(ARG,"B","P") == PLAYER && (GET_INSERT_CHARA(ARG,"P","M") == PLAYER || GET_INSERT_CHARA(ARG,"C","M") == PLAYER) && (SELECTCOM == [[調教:クンニ]] || SELECTCOM == [[調教:パイズリ]])
		TFLAG:特殊COM = 8
	;ダブルフェラ
	ELSEIF GET_INSERT_CHARA(ARG,"M","P") == PLAYER && (MASTER_POSE(4,1,1) && MASTER_POSE(4,1,1) != ARG )
		TFLAG:特殊COM = 9
		TFLAG:派生COM番号 = 68
	;ダブル素股
	ELSEIF GET_INSERT_CHARA(ARG,"C","P") == PLAYER && (MASTER_POSE(2,1,1) && MASTER_POSE(2,1,1) != ARG )
		TFLAG:特殊COM = 10
	;ダブルパイズリ
	ELSEIF GET_INSERT_CHARA(ARG,"B","P") == PLAYER && (MASTER_POSE(5,1,1) && MASTER_POSE(5,1,1) != ARG )
		TFLAG:特殊COM = 11
	;パイズリフェラ
	ELSEIF (GET_INSERT_CHARA(ARG,"B","P") == PLAYER && GET_INSERT_CHARA(ARG,"M","P",1) == PLAYER) || (GET_INSERT_CHARA(ARG,"M","P") == PLAYER && GET_INSERT_CHARA(ARG,"B","P",1) == PLAYER)
		TFLAG:特殊COM = 12
	;強制放尿
	ELSEIF SELECTCOM == [[調教:放尿]]
		;おもらし癖なし、従順４以上欲望３以上、マゾっ気３以上orおもらし癖ありで利尿剤が無い
		IF (!TALENT:57 && ABL:0 >= 4 && ABL:1 >= 3 && ABL:8 >= 3) || (TALENT:57 && !TEQUIP:22)
			TFLAG:特殊COM = 14
			TFLAG:派生COM番号 = 86
		ENDIF
	;Ｗフェラ
	ELSEIF GET_INSERT_CHARA(ARG,"M","P") == PLAYER && GET_INSERT_CHARA(ASSI,"P","M") == PLAYER
		TFLAG:特殊COM = 15
		TFLAG:派生COM番号 = 66
	ENDIF
ENDIF

;=== 日常系派生コマンド ===
IF !TFLAG:うふふ
	;耳かき
	IF SELECTCOM == [[調教:スキンシップ]] && PREVCOM == [[調教:膝枕して貰う]]
		TFLAG:特殊COM = 60
	;デコちゅー
	ELSEIF (SELECTCOM == [[調教:バードキス]] || SELECTCOM == [[調教:ソフトキス]]) && PREVCOM == [[調教:頭を撫でる]]
		TFLAG:特殊COM = 62 
	ENDIF
ENDIF

;=== デート系 ===

DEBUGPRINTFORML 【CALLCOM】TFLAG={TFLAG:特殊COM}, SELECTCOM={SELECTCOM}
RESULT = 0

IF TFLAG:特殊COM
	CALLFORM COM_ABLE{500 + TFLAG:特殊COM}
	IF RESULT
		TRYCALLFORM CAN_SCOM{TFLAG:特殊COM}(1)
		IF RESULT
			TRYCCALLFORM CAN_SCOM{TFLAG:特殊COM}
			CATCH
				PRINTFORML %コマンド名(TFLAG:特殊COM, 1)%
				TFLAG:派生COM番号 = 0
			ENDCATCH
			
			STR:0 = %コマンド名(TFLAG:特殊COM, 1)%
			
			TRYCALLFORM SCOM{TFLAG:特殊COM}
			
		ENDIF
	ELSE
		;実行判定失敗口上呼び出し
		TFLAG:イベント口上 = 99
		CALL SELF_KOJO
		
		;実行判定非表示にしている場合下記の文章が表示されないと実行できない場合が分かり難いため文章表示
		IF ((FLAG:コンフィグ & 1p27) == 0) && FLAG:特殊モード != 1
			;実行判定を表示する場合は文章表示の必要なし
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の合意を得られませんでした
		ENDIF
	ENDIF
ENDIF

SIF RESULT
	RETURN RESULT

TRYCCALLFORM CAN_COM{SELECTCOM}
CATCH
	PRINTFORML %コマンド名(SELECTCOM)%
ENDCATCH
;実行できない場合
IF A < V
	;実行判定失敗口上呼び出し
	TFLAG:イベント口上 = 99
	CALL SELF_KOJO
	;実行判定非表示にしている場合下記の文章が表示されないと実行できない場合が分かり難いため文章表示
	IF ((FLAG:コンフィグ & 1p27) == 0) && FLAG:特殊モード != 1
		;実行判定を表示する場合は文章表示の必要なし
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の合意を得られませんでした
	ENDIF
ENDIF

STR:0 = %コマンド名(SELECTCOM)%
TRYCALLFORM COM{SELECTCOM}

RETURN RESULT

;ARG	コマンド番号or特殊COM番号
;ARG:1	0,通常,1,派生COM
@コマンド名(ARG, ARG:1)
#FUNCTIONS
IF ARG:1
	SELECTCASE ARG
		CASE 1
			RETURNF "シックスナイン"
		CASE 2
			RETURNF "岩清水"
		CASE 3
			RETURNF "Gスポット刺激"
		CASE 4
			RETURNF "乱れ牡丹"
		CASE 5
			RETURNF "手淫フェラ"
		CASE 6
			RETURNF "挿入Ｇスポット責め"
		CASE 7
			RETURNF "挿入子宮口責め"
		CASE 8
			RETURNF "６９パイズリ"
		CASE 9
			RETURNF "ダブルフェラ"
		CASE 10
			RETURNF "ダブル素股"
		CASE 11
			RETURNF "ダブルパイズリ"
		CASE 12
			RETURNF "パイズリフェラ"
		CASE 13
			RETURNF "交互挿入"
		CASE 14
			RETURNF "強制放尿"
		CASE 15
			RETURNF "Ｗフェラ"
		CASE 60
			RETURNF "耳かき"
		CASE 62
			RETURNF "デコちゅー"
	ENDSELECT
ELSE
	SELECTCASE ARG
			CASE 11
				SIF TEQUIP:触手プレイ
					RETURNF "触手挿入"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能バイブ"
			CASE 13
				SIF TEQUIP:触手プレイ
					RETURNF "アナル触手"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能アナルバイブ"
			CASE 14
				SIF TEQUIP:触手プレイ
					RETURNF "触手クリキャップ"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能電動クリキャップ"
				RETURNF "電動クリキャップ"
			CASE 15
				SIF TEQUIP:触手プレイ
					RETURNF "触手乳首責め"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能二プルキャップ"
				RETURNF "二プルキャップ"
			CASE 16
				SIF TEQUIP:触手プレイ
					RETURNF "触手搾乳"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能搾乳機"
			CASE 17
				SIF TEQUIP:触手プレイ
					RETURNF "触手ペニス責め"
				SIF TALENT:PLAYER:淫具知識
					RETURNF "高性能オナホール"
			CASE 44
				SIF TEQUIP:触手プレイ
					RETURNF "触手緊縛"
			CASE 116
				SIF TFLAG:45
					RETURNF "剃毛される"
			CASE 173
				SIF TFLAG:45
					RETURNF "腹パンされる"
			CASE 280 TO 284
				RETURNF CSTR:(ARG - 200)
			CASE 323
				SIF TEQUIP:デート中 && IS_友情(TARGET)
					RETURNF "遠出する(5,000Ｇを消費)"
				SIF TEQUIP:デート中
					RETURNF "デート(5,000Ｇを消費)"
				RETURNF "デート終了"
		CASEELSE
			RETURNF TRAINNAME:ARG
	ENDSELECT
ENDIF

SIF STRLENS(TRAINNAME:ARG) > 0
	RETURNF TRAINNAME:ARG
RETURNF ""
