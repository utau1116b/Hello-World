;-------------------------------------------------
;交互挿入
;セックス、助手系派生コマンド
;-------------------------------------------------
@SCOM13
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:0 射精対象フラグ（ビット0=TARGET 1=PLAYER 2=MASTER 3=ASSI）
;TCVAR:1 予備射精対象フラグ（ビットを使いたくない人用、CFLAG:100に翻訳されます）	（1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI）
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
;二人目
LOCAL = MASTER_POSE(6,1,1)

LOCALS = {TARGET}/{LOCAL}/
CSTR:PLAYER:10 += LOCALS
TCVAR:MASTER:2 = 1
;強度
TCVAR:MASTER:4 = 2500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 50
DOWNBASE:気力 += 110

SOURCE:快Ｖ = 460
SOURCE:情愛 = 210
SOURCE:痛み = 510
SOURCE:露出 = 80
SOURCE:反感 = 350

SOURCE:LOCAL:快Ｖ = 230
SOURCE:LOCAL:情愛 = 100
SOURCE:LOCAL:痛み = 510
SOURCE:LOCAL:露出 = 40
SOURCE:LOCAL:反感 = 350
;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60
SOURCE:LOCAL:不潔 = Y*10 + 60


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
FOR LOCAL:1,0,2
	IF LOCAL:1 == 0
		LOCAL:2 = TARGET
	ELSE
		LOCAL:2 = LOCAL
	ENDIF
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:(LOCAL:2):抑圧 || TALENT:(LOCAL:2):抵抗
		SOURCE:(LOCAL:2):反感 += 310

	;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
	SIF TALENT:(LOCAL:2):貞操観念 && !TALENT:(LOCAL:2):恋慕
		SOURCE:(LOCAL:2):反感 += 1300

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:(LOCAL:2):処女 == 1
		SOURCE:(LOCAL:2):痛み += 2040
		SOURCE:(LOCAL:2):反感 += 3500
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:(LOCAL:2):処女 == 2
		SOURCE:(LOCAL:2):痛み += 510
		SOURCE:(LOCAL:2):反感 += 1100
	ENDIF

	;Vへの苦痛は先に処理しておく
	CALL PAIN_CHECK_V(LOCAL:2)
NEXT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女
	STAIN:膣内 |= 64
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:Ｖ
STAIN:PLAYER:Ｐ |= STAIN:膣内
STAIN:Ｖ |= STAIN:PLAYER:Ｐ
STAIN:膣内 |= STAIN:PLAYER:Ｐ
;助手のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:LOCAL:3
STAIN:PLAYER:Ｐ |= STAIN:LOCAL:6
STAIN:LOCAL:3 |= STAIN:PLAYER:Ｐ
STAIN:LOCAL:6 |= STAIN:PLAYER:Ｐ
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:Ｖ
STAIN:PLAYER:Ｐ |= STAIN:膣内
STAIN:Ｖ |= STAIN:PLAYER:Ｐ
STAIN:膣内 |= STAIN:PLAYER:Ｐ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;EXP:Ｖ性交経験 ++
;EXP:LOCAL:Ｖ性交経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(39 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(39 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,LOCAL)
	EXP:LOCAL:(39 + HETEROSEX(TARGET,LOCAL)) += 3
	EXP:TARGET:(39 + HETEROSEX(TARGET,LOCAL)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(LOCAL,PLAYER)
	EXP:LOCAL:(39 + HETEROSEX(LOCAL,PLAYER)) += 3
	EXP:PLAYER:(39 + HETEROSEX(LOCAL,PLAYER)) += 3
ENDIF

IF !ASSIPLAY
	EXP:愛情経験 += TALENT:恋慕 * 2
	;処女
	SIF TALENT:処女 && TALENT:恋慕
		EXP:愛情経験 += 4
ENDIF
RETURN 1

@CAN_SCOM13(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF TEQUIP:縄 == 0
	SKIPDISP ARG
	PRINTL 交互挿入
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		A += ABL:Ｖ感覚 * 2
		PRINT Ｖ感覚
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;快楽依存
	IF MARK:快楽依存
		SIF S
			PRINT  + 
		A += MARK:快楽依存 * 3
		PRINT 快楽依存
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定 && !TALENT:恋慕
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:男嫌い && !(TALENT:PLAYER:オトコ) && (!TALENT:恋慕 || PLAYER != MASTER)
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 12
		PRINT 恋慕
		PRINTV '(,12,')
		S = 12
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 2
		PRINT 親愛
		PRINTV '(,2,')
		S = 2
	ENDIF
	;淫乱
	IF TALENT:淫乱 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 3
		PRINT 淫乱
		PRINTV '(,3,')
		S = 3
	ENDIF
	;娼婦
	IF TALENT:淫乱 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 3
		PRINT 娼婦
		PRINTV '(,3,')
		S = 1
	ENDIF
	;処女
	LOCAL = MAX(10,40 - EXP:Ｖ経験)
	IF TALENT:処女
		PRINT  - 
		A -= LOCAL
		PRINT 処女
		PRINTV '(,LOCAL,')
		S = 1
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT Ｖ経験
		PRINT 不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:Ｐ & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:Ｐ & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:Ｐ & 8
		Y += 7
	SIF STAIN:PLAYER:Ｐ & 16
		Y += 15
	Y = Y * (2 - TALENT_LV(TARGET, "汚臭耐性")) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭敏感
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(25以上で実行)
	PRINT  = 
	PRINTV A

	V = 25
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT

	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1
