;-------------------------------------------------
;ダブル素股
;奉仕、レズ系派生コマンド
;-------------------------------------------------
@SCOM10
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:0 射精対象フラグ（ビット0=TARGET 1=PLAYER 2=MASTER 3=ASSI）
;TCVAR:1 予備射精対象フラグ（ビットを使いたくない人用、CFLAG:100に翻訳されます）	（1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI）
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
;二人目
LOCAL = MASTER_POSE(2,1,1)

LOCALS = {TARGET}/{LOCAL}/
CSTR:PLAYER:10 += LOCALS
TCVAR:MASTER:2 = 6
;強度
TCVAR:MASTER:4 = 1400

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 40
DOWNBASE:気力 += 160

SOURCE:快Ｃ = 170
SOURCE:情愛 = 100
SOURCE:性行動 = 300
SOURCE:達成 = 100
SOURCE:屈従 = 600
SOURCE:逸脱 = 200
SOURCE:反感 = 200

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;消費体力、気力
DOWNBASE:LOCAL:体力 += 40
DOWNBASE:LOCAL:気力 += 160

SOURCE:LOCAL:快Ｃ = 170
SOURCE:LOCAL:情愛 = 50
SOURCE:LOCAL:性行動 = 1500
SOURCE:LOCAL:達成 = 50
SOURCE:LOCAL:屈従 = 300
SOURCE:LOCAL:逸脱 = 200
SOURCE:LOCAL:反感 = 200

;上のほうで計算した汚れデータ
SOURCE:LOCAL:不潔 = Y*10 + 60
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
;SIF TALENT:抑圧 || TALENT:抵抗
;	SOURCE:反感 += 400
SIF TALENT:LOCAL:抑圧 || TALENT:LOCAL:抵抗
	SOURCE:LOCAL:反感 += 400
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:Ｖ |= STAIN:PLAYER:Ｐ
STAIN:PLAYER:Ｐ |= STAIN:Ｖ
;助手のＶ⇔調教者のＰの汚れが移動
STAIN:ASSI:3 |= STAIN:PLAYER:Ｐ
STAIN:PLAYER:Ｐ |= STAIN:ASSI:3
;奴隷のＶ⇔助手のＶの汚れが移動
STAIN:Ｖ |= STAIN:ASSI:3
STAIN:ASSI:3 |= STAIN:Ｖ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(39 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(39 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,LOCAL)
	EXP:LOCAL:(39 + HETEROSEX(TARGET,LOCAL)) += 3
	EXP:TARGET:(39 + HETEROSEX(TARGET,LOCAL)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(LOCAL,PLAYER)
	EXP:LOCAL:(39 + HETEROSEX(LOCAL,PLAYER)) += 3
	EXP:PLAYER:(39 + HETEROSEX(LOCAL,PLAYER)) += 3
ENDIF


RETURN 1

@CAN_SCOM10(ARG)
SKIPDISP ARG
PRINTL ダブル素股
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 2
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 2,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 1
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
	S = 1
ENDIF

IF MARK:快楽依存
	SIF S
		PRINT  + 
	A += MARK:快楽依存 * 2
	PRINT 快楽依存
	PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 2,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 1
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感の否定 && !TALENT:淫乱
	PRINT  - 
	A -= 3
	PRINT 快感の否定
	PRINTV '(,3,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:男嫌い && !(TALENT:PLAYER:オトコ) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	A -= 7
	PRINT 男嫌い
	PRINTV '(,7,')
	S = 1
ENDIF
;淫乱
IF TALENT:淫乱 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 3
	PRINT 淫乱
	PRINTV '(,3,')
	S = 3
ENDIF
;娼婦
IF TALENT:淫乱 > 1 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 3
	PRINT 娼婦
	PRINTV '(,3,')
	S = 1
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF S
		PRINT  + 
	A += 6
	PRINT 媚薬
	PRINTV '(,6,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:Ｐ & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:Ｐ & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:Ｐ & 8
	Y += 7
SIF STAIN:PLAYER:Ｐ & 16
	Y += 15

;素股では汚れの影響が少なめ
Y /= 3
Y = Y * (2 - TALENT_LV(TARGET, "汚臭耐性")) / 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	PRINT 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 、汚臭鈍感
	ELSEIF TALENT:汚臭敏感
		PRINT 、汚臭敏感
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(25以上で実行)
PRINT  = 
PRINTV A

V = 25
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1

