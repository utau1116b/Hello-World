;調教時キャラ別口上を一括管理

;-----------------------------------------------------------------------------------
;口上用システムファイルの存在判定
;EVENT_K.ERB(つまりこのファイル)が存在しているかを判定
;口上が使用可能で口上機能を能動的にOFFにしていない場合には、自動的にスイッチが入る
;各口上ファイル内でキャラフラグを制御するので、
;EVENT_Kxx.ERB(キャラ別口上ファイル)を途中で削除してもエラーはでない
;ただし、口上をONにした状態でこのファイルを削除するとエラーが発生する
;あっても邪魔にはならないので削除しない方が良いだろう
;-----------------------------------------------------------------------------------
@EVENTSHOP
#PRI
SIF FLAG:7 == 0
	FLAG:7 = 2

;--------------------------------------------------
;@KOJO_MESSAGE_COM_MASTER関係
;EXモードでの調教主の汎用口上を呼び出します。
;@KOJO_MESSAGE_COM、@KOJO_MESSAGE_COM_ASSIとは排他ではありません。
;表示順序は、主人→助手→対象 となります。
;--------------------------------------------------
@KOJO_MESSAGE_COM_MASTER
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_MESSAGE_COM_MASTER_{NO:MASTER}

;--------------------------------------------------
;@KOJO_MESSAGE_COM_ASSI関係
;助手調教中のコマンド実行時に出力されます。
;@KOJO_MESSAGE_COMとは排他ではありません。
;--------------------------------------------------
@KOJO_MESSAGE_COM_ASSI
SIF FLAG:7 <= 0
	RETURN 0

SIF ASSI < 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_MESSAGE_COM_ASSI_{NO:ASSI}

;--------------------------------------------------
;@KOJO_MESSAGE_PLAYERCHANGE関係
;助手切り替え時に出力されます。
;--------------------------------------------------
@KOJO_MESSAGE_PLAYERCHANGE
SIF FLAG:7 <= 0
	RETURN 0

SIF ASSI < 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_MESSAGE_PLAYERCHANGE_{NO:ASSI}

;--------------------------------------------------
;@KOJO_MESSAGE_COM関係
;コマンド実行時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_COM
SIF FLAG:7 <= 0
	RETURN 0

;対応していない口上が多いTEQUIP装着時、別の口上呼び出し
IF TEQUIP:オムツ || TEQUIP:着衣ローター || TEQUIP:MASTER:アイマスク || TEQUIP:MASTER:縄 || TEQUIP:MASTER:ボールギャグ
	;キャラ別
	TRYCCALLFORM KOJO_MESSAGE_COM_EX_{NO:TARGET}
	CATCH
		;個別ないなら共通
		CALL TRAINVOICE
	ENDCATCH
	RETURN 0
ENDIF
;キャラ別
TRYCCALLFORM KOJO_MESSAGE_COM_{NO:TARGET}
CATCH
	;個別ないなら共通
	CALL TRAINVOICE
ENDCATCH

;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_MESSAGE_PALAMCNG_{NO:TARGET}

;-------------------------------------------------
;@KOJO_MESSAGE_MARKCNG関係
;刻印変動をトリガーにした口上
;刻印変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_MARKCNG
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_MESSAGE_MARKCNG_{NO:TARGET}

;--------------------------------------------------
;@KOJO_EVENT_COM関係
;コマンド処理の最後に出力
;口上をOFFにしても実行する
;--------------------------------------------------
@KOJO_EVENT_COM

;キャラ別
TRYCALLFORM KOJO_EVENT_COM_{NO:TARGET}

;-------------------------------------------------
;@SELF_KOJO関係
;イベント時の口上、イベント開始時に出力します
;-------------------------------------------------
@SELF_KOJO
IF FLAG:7 <= 0
	TFLAG:13 = 0
	RETURN 0
ENDIF

SIF TARGET < 0
	RETURN 0

;キャラ別
TRYCALLFORM SELF_KOJO_K{NO:TARGET}

;-------------------------------------------------
;@LOOK_VIDEO_KOJO関係
;ビデオを一緒に鑑賞する時に出力
;-------------------------------------------------
@LOOK_VIDEO
;FLAG:31〜40をP:1〜10に代入(1回目〜10回目だと解りやすくするため、中身が-1ならその回の撮影はされていない)
P:12 = X
REPEAT 11
SIF COUNT == 0
CONTINUE
X = COUNT+30
P:COUNT = FLAG:X
REND
X = P:12

;キャラ別
TRYCALLFORM LOOK_VIDEO_KOJO_K{NO:TARGET}

;--------------------------------------------------
;@DAILY_LIFE_MASSAGE関係
;休憩実行時に出力されます
;選択されるのは必ずしもTARGETとは限らないので、口上存在判定をCFLAG:A:406を使って特殊にしています
;--------------------------------------------------
@DAILY_LIFE_MASSAGE
SIF FLAG:7 <= 0
	RETURN 1

;キャラ別
TRYCALLFORM DAILY_LIFE_MASSAGE_{NO:A}

;-------------------------------------------------
;@SINGLE_ENDING関係
;単体エンドの条件を満たした時に出力します
;-------------------------------------------------
@SINGLE_ENDING
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM SINGLE_ENDING_K{NO:TARGET}

;--------------------------------------------------
;@KOJO_LUNCH関係
;料理実行時に出力されます
;--------------------------------------------------
@KOJO_LUNCH
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_LUNCH_K{NO:TARGET}

;--------------------------------------------------
;@KOJO_LUNCHEXERCISE関係
;食後の運動イベント発生時に出力されます
;--------------------------------------------------
@KOJO_LUNCH_EXERCISE
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_LUNCH_EXERCISE_K{NO:TARGET}

;-------------------------------------------------
;@SLAVE_ENDING関係
;奴隷エンドの条件を満たした時に出力します
;-------------------------------------------------
@SLAVE_ENDING
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM SLAVE_ENDING_K{NO:TARGET}

;--------------------------------------------------
;@KOJO_JUN関係
;純愛イベント関連時に出力されます
;--------------------------------------------------
@KOJO_JUN
SIF FLAG:7 == 0
	FLAG:7 = 2

SIF FLAG:7 < 0
	RETURN 0

IF TARGET < 0 && (TFLAG:600 >= 28 && TFLAG:600 <= 31)
	;攻略ルート選択時、口上表示時だけ難易度変更
	LOCAL = FLAG:5
	FLAG:5 = FLAG:3
	;パートナーがいない場合でもオープニング等は表示
	CALL COMMON_PLANE_TEXT
	;難易度を元に戻す
	FLAG:5 = LOCAL
ENDIF

SIF TARGET < 0
	RETURN 0

;攻略ルート選択時、口上表示時だけ難易度変更
LOCAL = FLAG:5
FLAG:5 = FLAG:3

;SIF TFLAG:600 == 0
;	PRINTFORML ＜%NAME:TARGET%の口上によるエキストラモード設定＞
;キャラ別
TRYCCALLFORM KOJO_JUN_K{NO:TARGET}
CATCH
	;個別ないなら共通
	CALL COMMON_PLANE_TEXT
ENDCATCH

;難易度を元に戻す
FLAG:5 = LOCAL

;--------------------------------------------------
;主人公によって分岐するオープニング口上1
;共通のオープニングの前に呼び出される
;--------------------------------------------------
@KOJO_PRE_OPENING_STORY
SIF FLAG:7 == 0
	FLAG:7 = 2

SIF FLAG:7 < 0
	RETURN 0

SIF TARGET < 0
	RETURN 0
;キャラ別
TRYCALLFORM KOJO_PRE_OPENING_STORY_M{NO:MASTER}
IF RESULT
	RETURN 1
ELSE
	RETURN 0
ENDIF

;--------------------------------------------------
;主人公によって分岐するオープニング口上2
;共通のオープニングの後に呼び出される
;--------------------------------------------------
@KOJO_POST_OPENING_STORY
SIF FLAG:7 == 0
	FLAG:7 = 2

SIF FLAG:7 < 0
	RETURN 0

SIF TARGET < 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_POST_OPENING_STORY_M{NO:MASTER}

;-------------------------------------------------
;待機中行動の口上
;-------------------------------------------------
@KOJO_TAIKI
SIF FLAG:7 == 0
	FLAG:7 = 2

SIF FLAG:7 < 0
	RETURN 0

;キャラ別
TRYCALLFORM KOJO_TAIKI_K{NO:TARGET}

;-------------------------------------------------
;@DATE_DATE関係
;口上デート先の存在判定
;-------------------------------------------------
@DATE_DATE
;キャラ別
TRYCALLFORM DATE_DATE_K{NO:TARGET}

;-------------------------------------------------
;@SELF_MAEKOJO関係
;イベント時の口上、イベント開始前に出力します
;地の文カット用です
;-------------------------------------------------
@SELF_MAEKOJO
IF FLAG:7 <= 0
	TFLAG:13 = 0
	RETURN 0
ENDIF

SIF TARGET < 0
	RETURN 0

;キャラ別
TRYCALLFORM SELF_MAEKOJO_K{NO:TARGET}

;--------------------------------------------------
;@MAEKOJO_LUNCH_EXERCISE関係
;食後の運動イベント発生時に出力されます
;地の文カット用です
;--------------------------------------------------
@MAEKOJO_LUNCH_EXERCISE

;キャラ別
TRYCALLFORM MAEKOJO_LUNCH_EXERCISE_K{NO:TARGET}

;-------------------------------------------------
;@KOJO_TRAIN_MESSAGE関係
;コミュニケーション時の地の文をカット可能にさせる
;-------------------------------------------------
@KOJO_TRAIN_MESSAGE

;キャラ別
TRYCCALLFORM KOJO_TRAIN_MESSAGE_K{NO:TARGET}
CATCH
	;個別ない場合TFLAG:99 = 0
	TFLAG:99 = 0
ENDCATCH

;-------------------------------------------------
;@COSTUME_SHOW関係
;Notウフフ時のコスプレ内容表示
;-------------------------------------------------
@COSTUME_SHOW

;キャラ別
TRYCALLFORM COSTUME_SHOW_K{NO:TARGET}

;-------------------------------------------------
;@COSTUME_PLAY_SHOW関係
;ウフフ時のコスプレ内容表示
;排他制御の為、関数の戻り値をそのまま返す
;-------------------------------------------------
@COSTUME_PLAY_SHOW

;キャラ別
TRYCALLFORM COSTUME_PLAY_SHOW_K{NO:TARGET}

IF RESULT == 1
	RETURN 1
ELSE
	RETURN 0
ENDIF

;-------------------------------------------------
;カウンター口上
;-------------------------------------------------
@COUNTER_KOJO
SIF FLAG:7 <= 0
	RETURN 0

;キャラ別
TRYCALLFORM COUNTER_KOJO_K{NO:TARGET}



;--------------------------------------------------
;自動喘ぎ関数@TRAINVOICE
;及びその子供関数
;好きな口上ファイルの
;@KOJO_MESSAGE_COM_X1
;の下に
;CALL TRAINVOICE
;RETURN 0
;の二行を加えるだけで簡易喘ぎ声をある程度シチュに応じて自動生成
;現在、調整不測のためネタ機能の域を出ないと思います
;口上未実装のキャラに自動で喘ぎ声を付けるようにしてもいいですが
;その場合素質を見て性格分岐させたいところ。要改良
;--------------------------------------------------
@TRAINVOICE
;Aに喘ぎ回数を入れる処理
;Bに行為レベルをとりあえず0〜3で規定

A = 0
B = 0
C = 0

;コマンドごとの喘ぎ数・基本行為レベル（激しさ数値）を設定
;愛撫・性交系は欲情が大きいと喘ぎ数が+3まで増えます
;愛撫・性交系
;最も軽い　愛撫・胸愛撫・アナル愛撫・アナル舐め・素股・200,乳の揉み合い
IF SELECTCOM == 0 || SELECTCOM == 5 || SELECTCOM == 2 || SELECTCOM == 9 || SELECTCOM == 33 || SELECTCOM == 166 || SELECTCOM == 200
	A = 1
	B = 0
	SIF PALAMLV:2 > 1
		A += 1
	SIF PALAMLV:2 > 2
		A += 1
	SIF PALAMLV:2 > 4
		A += 1
;最も軽い　非ウフフ時のスキンシップ系
ELSEIF SELECTCOM == 303 || (TFLAG:45 == 0 && SELECTCOM == 306) || (TFLAG:45 == 0 && SELECTCOM == 307) || SELECTCOM == 312 || SELECTCOM == 360 || SELECTCOM == 361 || SELECTCOM == 370
	A = 1
	B = 0
	;現在の好感度か歓楽ソースが0以下かつ大成功でないなら感じていない口上へ
	IF (TFLAG:30<= 0 || SOURCE:18 <= 0) && TFLAG:18 != 1
		C = 0
	ELSE
		C = 1
	ENDIF
;軽め　クンニ・ローター・指挿入れ・フェラする・泡踊り・岩清水・自慰・
ELSEIF SELECTCOM < 4 || SELECTCOM == 10 || SELECTCOM == 8 || SELECTCOM == 4 || SELECTCOM == 35 || SELECTCOM == 87 || SELECTCOM == 3 || SELECTCOM == 362
	A = 2
	B = 1
	SIF PALAMLV:2 > 1
		A += 1
	SIF PALAMLV:2 > 2
		A += 1
	SIF PALAMLV:2 > 4
		A += 1
;ちょい重め　Ｇスポット刺激・Ｅマッサージャー・191,手淫する-195,騎乗位する・204,アナルに入れさせる
ELSEIF SELECTCOM == 84 || SELECTCOM == 12 || (SELECTCOM >= 191 && SELECTCOM <= 195) || SELECTCOM == 204 || SELECTCOM == 364
	A = 3
	B = 2
	SIF PALAMLV:2 > 1
		A += 1
	SIF PALAMLV:2 > 2
		A += 1
	SIF PALAMLV:2 > 4
		A += 1
;本番　セックス系全部・196,ペニバン挿入・非ウフフ時のセックス系全部
ELSEIF (SELECTCOM > 19 && SELECTCOM < 30) || SELECTCOM == 64 || SELECTCOM == 34 || SELECTCOM == 36 || SELECTCOM == 167 || SELECTCOM == 168 || SELECTCOM == 196 || SELECTCOM == 255 || SELECTCOM == 256 || SELECTCOM == 363 || SELECTCOM == 365 || SELECTCOM == 366
	A = 3
	B = 3
	SIF PALAMLV:2 > 1
		A += 1
	SIF PALAMLV:2 > 2
		A += 1
	SIF PALAMLV:2 > 4
		A += 1
ENDIF

;玩具装備・解除系
;クリップ系　クリキャップ・ニプルキャップ
IF SELECTCOM == 14 || SELECTCOM == 15 || SELECTCOM == 103 || SELECTCOM == 104
	A = 1
	B = 1
;重め系　搾乳機・アナルビーズ・オナホール・アナル電極
ELSEIF SELECTCOM == 16 || SELECTCOM == 17 || SELECTCOM == 19 || SELECTCOM == 49 || SELECTCOM == 105 || SELECTCOM == 109
	A = 2
	B = 3
;挿入系　バイブ・アナルバイブ・クスコ
ELSEIF SELECTCOM == 11 || SELECTCOM == 13 || SELECTCOM == 110 || SELECTCOM == 101 || SELECTCOM == 102
	A = 1
	B = 2
ENDIF

;どれくらい感じてるかチェック
;ソースに対応した感覚が2以上なら1、4以上なら2。高い方を優先
IF A
	SIF SOURCE:0 && ABL:3 > 1
		C = 1
	SIF SOURCE:0 && ABL:3 > 3
		C = 2
	SIF SOURCE:1 && ABL:4 > 1 && C == 0
		C = 1
	SIF SOURCE:1 && ABL:4 > 3
		C = 2
	SIF SOURCE:2 && ABL:5 > 1 && C == 0
		C = 1
	SIF SOURCE:2 && ABL:5 > 3
		C = 2
	SIF SOURCE:17 && ABL:14 > 1 && C == 0
		C = 1
	SIF SOURCE:17 && ABL:14 > 3
		C = 2
	;感じていない口上へ
	IF C == 0
		;キャラ別
		TRYCCALLFORM VOICE_K{NO:TARGET}_B
		CATCH
			;個別ないなら共通
			CALL VOICE_B
		ENDCATCH
	ENDIF
	;感覚4以上だと激しさ+1
	SIF C == 2
		B += 1
	;そして開発済み口上へ
	IF C > 0
		;キャラ別
		TRYCCALLFORM VOICE_K{NO:TARGET}_A
		CATCH
			;個別ないなら共通
			CALL VOICE_A
		ENDCATCH
	ENDIF
ENDIF

;奉仕系
;キスする
IF SELECTCOM == 6
	A = 6
	B = 3
;手淫
ELSEIF SELECTCOM == 30
	A = 3
	B = 0
;パイズリ
ELSEIF SELECTCOM == 32
	A = 4
	B = 1
;フェラ・シックスナイン・Ｗフェラ・指フェラ
ELSEIF SELECTCOM == 31 || SELECTCOM == 69 ||SELECTCOM == 68 ||SELECTCOM == 66 || SELECTCOM == 201 || SELECTCOM == 373
	A = 6
	B = 2
ENDIF
;奉仕系口上へ
IF A
	;キャラ別
	TRYCCALLFORM VOICE_K{NO:TARGET}_C
	CATCH
		;個別ないなら共通
		CALL VOICE_C
	ENDCATCH
ENDIF

;苦痛系
;痛いの
IF SELECTCOM == 40 || SELECTCOM == 41 || SELECTCOM == 42
	A = 3
	B = 1
	SIF SELECTCOM == 42
		B += 1
;苦しいの　;46,浣腸器＋プラグ・80,イラマチオ・292,衣装破り
ELSEIF SELECTCOM == 46 || SELECTCOM == 80 || SELECTCOM == 107 || SELECTCOM == 108 || SELECTCOM == 292
	A = 2
	B = 3
ENDIF
;苦痛系口上へ
IF A
	;キャラ別
	TRYCCALLFORM VOICE_K{NO:TARGET}_D
	CATCH
		;個別ないなら共通
		CALL VOICE_D
	ENDCATCH
ENDIF

RETURN 0

@VOICE_A
;開発されている子

PRINT 「

;お尻判定
IF SOURCE:2 && SELECTCOM != 3
	P = RAND:3
	SIF P == 0
		PRINT 後ろ……？　
	SIF P == 1
		PRINT そ、そっちは、
	SIF P == 2
		PRINT お、お尻っ、
ENDIF

IF B < 2
	REPEAT A
		P = RAND:10
		SIF P == 0
			PRINT あ、
		SIF P == 1
			PRINT あっ、
		SIF P == 2
			PRINT あ……
		SIF P == 3
			PRINT ん、
		SIF P == 4
			PRINT んっ、
		SIF P == 5
			PRINT ん……
		SIF P == 6
			PRINT ふあ、
		SIF P == 7
			PRINT あぁっ、
		SIF P == 8
			PRINT ……
		SIF P == 9
			PRINT くぅっ、
	REND
ELSEIF B > 1
	REPEAT A
		P = RAND:10
		SIF P == 0
			PRINT ああッ、
		SIF P == 1
			PRINT んぅっ、
		SIF P == 2
			PRINT は、
		SIF P == 3
			PRINT き、きもち、
		SIF P == 4
			PRINT いいっ、
		SIF P == 5
			PRINT んっ！　
		SIF P == 6
			PRINT いっ、
		SIF P == 7
			PRINT いいの、
		SIF P == 8
			PRINT っくうん、
		SIF P == 9
			PRINT あっあ、
	REND
ENDIF
;セリフの締め
IF B < 2
	P = RAND:10
	SIF P == 0
		PRINT ……
	SIF P == 1
		PRINT ん……
	SIF P == 2
		PRINT っ！
	SIF P == 3
		PRINT ふ……
	SIF P == 4
		PRINT は……
	SIF P == 5
		PRINT んん
	SIF P == 6
		PRINT んふ……
	SIF P == 7
		PRINT やあ……
	SIF P == 8
		PRINT ぁ……
	SIF P == 9
		PRINT んぅ……？
ELSE
	P = RAND:10
	SIF P == 0
		PRINT くぅッ!
	SIF P == 1
		PRINT い、いいのっ！
	SIF P == 2
		PRINT あああっ！！
	SIF P == 3
		PRINT んんん……っ！！
	SIF P == 4
		PRINT っ！　くぅ……
	SIF P == 5
		PRINT ……はあ、はあ
	SIF P == 6
		PRINT っくうん！
	SIF P == 7
		PRINT い、っ！！
	SIF P == 8
		PRINT ああっあ、あ……！
	SIF P == 9
		PRINT く、くぅぅんっ！
ENDIF
PRINTW 」
A = 0
B = 0
RETURN 0

@VOICE_B
;開発されていない子

PRINT 「

;お尻判定
IF SOURCE:2 && SELECTCOM != 3
	P = RAND:3
	SIF P == 0
		PRINT そ、そっちは違っ、
	SIF P == 1
		PRINT 嘘、お尻！？　
	SIF P == 2
		PRINT い、嫌っ、
ENDIF
A -= 2
SIF A < 1
	A = 1
	REPEAT A
		P = RAND:5
		SIF P == 0
			PRINT ん……
		SIF P == 1
			PRINT く、
		SIF P == 2
			PRINT 何……
		SIF P == 3
			PRINT ふう……
		SIF P == 4 && SOURCE:6
			PRINT 痛っ……
		SIF P == 4 && SOURCE:6 == 0
			PRINT ……
	REND
;セリフの締め
	P = RAND:5
	SIF P == 0
		PRINT うう……
	SIF P == 1
		PRINT 嫌よ……
	SIF P == 2
		PRINT んぅ……？
	SIF P == 3 && A < 3
		PRINT んっ
	SIF P == 3 && A > 2
		PRINT まだ……？
	SIF P == 4 && SOURCE:6
		PRINT ん、あ、痛っ
	SIF P == 4 && SOURCE:6 == 0 && TFLAG:44 == 1
		PRINT ぬるって……
	SIF P == 4 && SOURCE:6 == 0 && TFLAG:44 == 0
		PRINT ううっ……
PRINTW 」
A = 0
B = 0
RETURN 0

@VOICE_C
;奉仕系

PRINT 「

;手でする
IF B == 0
	REPEAT A
		P = RAND:5
		SIF P == 0
			PRINT しゅ、しゅ……
		SIF P == 1
			PRINT くりゅ、
		SIF P == 2
			PRINT こう？　
		SIF P == 3
			PRINT あ、これ、
		SIF P == 4
			PRINT 気持ちいい？　
	REND
;胸でする
ELSEIF B == 1
	PRINT 胸……？　
	REPEAT A
		P = RAND:5
		SIF P == 0
			PRINT あ、
		SIF P == 1
			PRINT これ、
		SIF P == 2
			PRINT んぅ……
		SIF P == 3
			PRINT 気持ちいいの？
		SIF P == 4
			PRINT 変なの……
	REND
;口でする
ELSEIF B > 1
	PRINT ん、
	REPEAT A
		P = RAND:5
		SIF P == 0
			PRINT ちゅ、
		SIF P == 1
			PRINT ちゅぷ、
		SIF P == 2
			PRINT れろ……
		SIF P == 3
			PRINT ぢゅる、
		SIF P == 4
			PRINT んん……
	REND
ENDIF
;セリフの締め
;手
IF B == 0
	P = RAND:5
	SIF P == 0
		PRINT びくって……
	SIF P == 1
		PRINT かちかち……
	SIF P == 2
		PRINT どう……？
	SIF P == 3
		PRINT ぬるぬるしてきた……
	SIF P == 4
		PRINT ここ、いい？
;キス
ELSEIF B == 3
	P = RAND:2
	SIF P == 0
		PRINT ……ぷぁ
	SIF P == 1
		PRINT ……はふ
;胸・フェラ
ELSEIF B > 1
	P = RAND:5
	SIF P == 0
		PRINT ……はぁ……♪
	SIF P == 1
		PRINT ふふ、可愛い
	SIF P == 2
		PRINT ……ちゅっ
	SIF P == 3
		PRINT ぺろっ
	SIF P == 4
		PRINT かちかち……
ENDIF
PRINTW 」
A = 0
B = 0
RETURN 0


@VOICE_D
;苦痛系

PRINT 「

;痛いの
IF B < 2
	REPEAT A
		P = RAND:5
		SIF P == 0
			PRINT いたっ！　
		SIF P == 1
			PRINT 痛い、
		SIF P == 2
			PRINT や、やだ、
		SIF P == 3
			PRINT ぐうううッ！　
		SIF P == 4
			PRINT ひっ
	REND
;苦しいの
ELSEIF B == 3
	PRINT ん、
	REPEAT A
		P = RAND:3
		SIF P == 0
			PRINT ぐぅ、
		SIF P == 1
			PRINT うう、
		SIF P == 2
			PRINT んぐ……
	REND
ENDIF
;セリフの締め
;痛い
IF B == 1
	P = RAND:5
	SIF P == 0
		PRINT ひいぃっ！！
	SIF P == 1
		PRINT 痛ぁっ！！
	SIF P == 2
		PRINT ……酷い……
	SIF P == 3
		PRINT ぐすっ……
	SIF P == 4
		PRINT ぐっう……
;針
ELSEIF B == 2
	P = RAND:2
	SIF P == 0
		PRINT ち、血が……
	SIF P == 1
		PRINT ひぎッ、も、もう……
ENDIF
PRINTW 」
A = 0
B = 0
RETURN 0
