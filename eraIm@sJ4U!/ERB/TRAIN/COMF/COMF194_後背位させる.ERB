;-------------------------------------------------
;後背位させる
;セックス系コマンド、レベル1
;-------------------------------------------------
@COM194
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 64
	CALL COM_ABLE64
	SIF RESULT == 1
		JUMP COM64

ENDIF
;室内でないと立位に
;SIF !INROOM(CFLAG:PLAYER:現在位置)
;	TFLAG:SELECTCOM分岐 = 1
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）

LOCALS = {PLAYER}/
CSTR:10 += LOCALS
TCVAR:2 = 1
;強度
TCVAR:4 = 1800
;V経験を伴うコマンドのフラグ(処女膜再生対応)
SIF TALENT:85 && ASSIPLAY == 0 && EXP:0 == 0
	TFLAG:20 = 1
;既成事実3に相当
TFLAG:200 = 3
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 50
DOWNBASE:気力 += 220
;DOWNBASE:疲労 += 40

SOURCE:快Ｃ = 150
SOURCE:中毒 = 60
SOURCE:接触 = 70
SOURCE:情愛 = 80
SOURCE:欲情 = 50
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:反感 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && !TALENT:恋慕
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:痛み += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 2
	SOURCE:痛み += 500
	SOURCE:反感 += 1000
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(PLAYER)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
SIF TALENT:PLAYER:処女
	STAIN:PLAYER:膣内 |= 64
;奴隷の膣内⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｖ |= STAIN:Ｐ
STAIN:PLAYER:膣内 |= STAIN:Ｐ
STAIN:Ｐ |= STAIN:PLAYER:Ｖ
STAIN:Ｐ |= STAIN:PLAYER:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(39 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(39 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

;愛情経験
IF !ASSIPLAY
	EXP:愛情経験 += TALENT:恋慕 * 2
	;処女
	SIF TALENT:処女 && TALENT:恋慕
		EXP:愛情経験 += 5
ENDIF
RETURN 1

@CAN_COM194(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:PLAYER:縄
	SKIPDISP ARG
	;IF !INROOM(CFLAG:PLAYER:現在位置)
	;	PRINTL 背面立位
	;ELSE
		PRINTL 後背位させる
	;ENDIF
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		A += ABL:Ｖ感覚 * 2
		PRINT Ｖ感覚
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;快楽依存
	IF MARK:快楽依存
		SIF S
			PRINT  + 
		A += MARK:快楽依存 * 3
		PRINT 快楽依存
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定 && !TALENT:恋慕
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:男嫌い && !(TALENT:PLAYER:オトコ) && (!TALENT:恋慕 || PLAYER != MASTER)
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && !ASSIPLAY
		SIF S
			PRINT  + 
		A += 15
		PRINT 恋慕
		PRINTV '(,15,')
		S = 15
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && !ASSIPLAY
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF
	;処女
	LOCAL = MAX(10,40 - EXP:Ｖ経験)
	IF TALENT:処女
		PRINT  - 
		A -= LOCAL
		PRINT 処女
		PRINTV '(,LOCAL,')
		S = 1
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT Ｖ経験不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:Ｐ & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:Ｐ & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:Ｐ & 8
		Y += 7
	SIF STAIN:PLAYER:Ｐ & 16
		Y += 15
	Y = Y * (2 - TALENT_LV(TARGET, "汚臭耐性")) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭敏感
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(22以上で実行)
	PRINT  = 
	PRINTV A

	V = 22
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1
