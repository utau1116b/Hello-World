;-------------------------------------------------
;デート
;日常系コマンド　レベル1
;-------------------------------------------------
@COM323
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;TEQUIP:デート中 = !TEQUIP:デート中
;行為者
TCVAR:116 = PLAYER
CALL COM300_SUCCESS(90, 9)
;STR:0 = デート
;-------------------------------------------------
;特殊分岐
;-------------------------------------------------
;デートはじめ
デート終了 = 0
IF !TEQUIP:デート中
	PRINTL デート($5000を消費)

ELSEIF TEQUIP:デート中 && MONEY > 2000 && !TFLAG:うふふ
	;●友情パッチ用改変
	IF IS_友情(TARGET)
		PRINTL [0] 外出終了
	ELSE
		PRINTL [0] デート終了
	ENDIF
	;●ここまで
	PRINTL [1] 場所を変える($2000を消費)
	PRINTL [100] やっぱりやめる
	$INPUT_LOOP
	INPUT
	IF RESULT != 100 && (RESULT < 0 || RESULT > 1)
		REUSELASTLINE 入力値が不正です。　正しい値を入力して下さい
		GOTO INPUT_LOOP
	;場所替え
	ELSEIF RESULT == 1
		デート場所 = TEQUIP:デート中
	;●友情パッチ用改変
		IF IS_友情(TARGET)
			PRINT 場所を変更します。
		ELSE
			PRINT デートの場所を変更します。
		ENDIF
	;●ここまで
		GOTO デート行き先選択
	;デート終わり
	ELSEIF RESULT == 0
		TEQUIP:デート中 = 0
		TEQUIP:6 = 0
		デート終了 = 1
		RETURN 1
		;デート終了が立っていたら、STR:0に<デート終了>と入れるように
		;STR:0 = デート終了
	;戻る
	ELSEIF RESULT == 100
		RETURN -1
	ENDIF
;デート終わり
ELSE
	TEQUIP:デート中 = 0
	TEQUIP:6 = 0
	デート終了 = 1
	RETURN 1
ENDIF

$デート行き先選択
CALL デート行き先
;0が返ってきたらデート中止
IF !RESULT && !TEQUIP:デート中
	デート場所 = 0
	IF パートナーの行きたい場所
;●友情パッチ用改変
		IF IS_友情(TARGET)
			PRINTFORML %CALLNAME:TARGET%は外出が中止になり残念そうだ…
		ELSE
			PRINTFORML %CALLNAME:TARGET%はデートが中止になり残念そうだ…
		ENDIF
;●ここまで
		CFLAG:好感度 += (RAND:10 - 10)
	ENDIF
	RETURN -1
ENDIF
;パートナーに行きたいデート先があり、パートナーの行きたいデート先だった場合大成功扱い。
;違う場合成功・失敗を再判定し成功もしくは失敗扱い。確率は通常の判定より厳しめ
IF パートナーの行きたい場所
	IF TEQUIP:デート中 == パートナーの行きたい場所
		TFLAG:成功判定 = 1
		TFLAG:コマンド内容 = 2
	ELSE
		LOCAL = GET_SUCCESS_RATE()
		LOCAL = 50 + LOCAL / 10
		SIF LOCAL > 99
			LOCAL = 99
		IF RAND:100 > LOCAL
			TFLAG:成功判定 = -1
		ELSE
			TFLAG:成功判定 = 0
		ENDIF
	ENDIF
ENDIF
CALL DATE_NAME
;行き先が決まったら
PRINTFORM %CALLNAME:PLAYER%達は%PLACE_NAME(TEQUIP:デート中)%
;乗り物
IF TEQUIP:デート中 == 39
	PRINTL に乗ることにしました
;映画
ELSEIF TEQUIP:デート中 >= 50 && TEQUIP:デート中 <= 54
	PRINTL を見ることにしました
ELSE
	PRINTL へと向かいました
ENDIF
;------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF デート場所
	;行き先変更場所によっての体力消費
	DOWNBASE:0 += 15
	;恋慕or恋人による体力消費軽減（デートとウフフが長続きする、の再現だとでも脳内補完）
	SIF TALENT:85 || TALENT:153
		DOWNBASE:0 -= 7
ENDIF

DOWNBASE:1 += 220
;恋慕・恋人・親愛による気力消費軽減（デートが長続きする、の再現だとでも脳内補完）
SIF TALENT:85
	DOWNBASE:1 -= 40
SIF TALENT:153
	DOWNBASE:1 -= 50
SIF TALENT:88
	DOWNBASE:1 -= 90


;固定で獲得するソース
SOURCE:歓楽 = 250
SOURCE:接触 = 150
SOURCE:受動 = 300
SOURCE:征服 = 600
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
SELECTCASE TEQUIP:デート中
	;神社
	CASE 1
		IF MONEY < 1000
			PRINTFORML そこで%CALLNAME:PLAYER%達はお賽銭を入れようとしましたが、お金が足りませんでした
		ELSE
			MONEY -= 1000
			SELECTCASE RAND:25
				;好感度増減
				CASE 0 TO 9
					IF RAND:100 < 80
						PRINTFORMW ……何だか%CALLNAME:TARGET%と仲良くなれたような気がした
						CFLAG:2 += 30
					ELSE
						PRINTFORMW ……何だか%CALLNAME:TARGET%と仲が悪くなったような気がした
						CFLAG:2 -= 5
					ENDIF
				;ムード増減
				CASE 10 TO 21
					IF RAND:100 < 80
						PRINTFORMW ……何だかその場のムードが良くなったような気がした
						;TFLAG:37 += 35
					ELSE
						PRINTFORMW ……何だかその場のムードが悪くなったような気がした
						;TFLAG:37 -= 5
					ENDIF
				;歓楽増減
				CASEELSE
					IF RAND:100 < 80
						PRINTFORMW ……%CALLNAME:TARGET%はいつもよりこの状況を楽しんでいるようだ
						TIMES SOURCE:歓楽 , 3.00
					ELSE
						PRINTFORMW ……%CALLNAME:TARGET%はいつもよりこの状況が楽しくないようだ
						TIMES SOURCE:歓楽 , 0.70
					ENDIF
			ENDSELECT
		ENDIF
	;霧の湖
	CASE 2
		;素質発覚イベント
		;CALL FIND_BUSTSIZE
		;オムツ解除
		TEQUIP:オムツ = 0
	;花畑
	CASE 3
		SELECTCASE RAND:3
			CASE 0
				PRINTFORMW %CALLNAME:PLAYER%達は花畑へと向かいました
			CASE 1
				PRINTFORMW %CALLNAME:PLAYER%達は太陽の畑へと向かいました
			CASE 2
				PRINTFORMW %CALLNAME:PLAYER%達は無名の丘へと向かいました
		ENDSELECT
	;温泉
	CASE 6
		;オムツ解除
		TEQUIP:オムツ = 0
	;海
	CASE 34
		;素質発覚イベント
		;CALL FIND_BUSTSIZE
		TEQUIP:オムツ = 0
	;遊覧船
	CASE 39
		MONEY -= 10000
	;スポーツ観戦
	CASE 42
		MONEY -= 5000
	;映画館
	CASE 50 TO 54
		MONEY -= 映画料金()
ENDSELECT
IF TFLAG:SELECTCOM分岐 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:接触 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	LOCAL:1 = 1
ELSEIF TFLAG:SELECTCOM分岐 == 0
	LOCAL:1 = 10
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	LOCAL:1 = 25
ENDIF

;恋慕
IF TALENT:恋慕
	TIMES SOURCE:18 , 1.10
	TIMES SOURCE:9 , 1.30
	TIMES SOURCE:20 , 1.10
	TIMES SOURCE:21 , 1.20
ENDIF
;恋人
IF TALENT:恋人
	TIMES SOURCE:18 , 1.20
	TIMES SOURCE:9 , 1.50
	TIMES SOURCE:20 , 1.20
	TIMES SOURCE:21 , 1.10
ENDIF
;親愛
IF TALENT:親愛
	TIMES SOURCE:歓楽 , 1.50
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 1.50
	TIMES SOURCE:21 , 1.50
ENDIF

;●友情パッチ用改変
IF IS_友情(TARGET)
	PRINTFORML %CALLNAME:TARGET%と遠出に出発しました
ELSE
	PRINTFORML %CALLNAME:TARGET%とデートに出発しました
ENDIF
;●ここまで
MONEY -= 5000

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

SIF LOCAL:1
	EXP:デート経験 += LOCAL:1

RETURN 1
@CAN_COM323(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
SKIPDISP ARG
PRINTL デート
A = 0
S = 0
V = 25

;終了時は判定不要(他の処理で利用するためA > VになるようAを設定)
SIF TEQUIP:デート中
	RETURN 1
	;A = 999
	

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF
;恥じらい
IF TALENT:35
	A -= 5
ENDIF
;献身的
IF TALENT:63
	A += 6
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;好感度
IF CFLAG:2 < 400
	B = 0
	IF CFLAG:2 < 50
		B = 40
	ELSEIF CFLAG:2 < 150
		B = 20
	ELSEIF CFLAG:2 < 300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 500
	B = 0
	B = CFLAG:2 / 75
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:63
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;好感度
	IF CFLAG:2 < 400
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 40
		ELSEIF CFLAG:2 < 150
			B = 20
		ELSEIF CFLAG:2 < 300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 500
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 75
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(25以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
	
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1
