@デート行き先
;#DIM 費用軽減, 1
VARSET LOCAL
;TFLAG:17でデートの種類を判別　通常：0　パートナーが勝手に決定：1　パートナーの行きたいデート先：2　パートナーが行きたいデート先以外：3
TFLAG:17 = 0
VARSET TSTR
CALL DATE_NAME
CALL NEWLINE_U(1)

;相手が行きたいデート先を決定
$PARTNER_DATE_SELECT
LOCALS = %TSTR:(RAND:55)%
DEBUGPRINTFORML LOCALS = %LOCALS%
SIF !STRLENS(LOCALS)
	GOTO PARTNER_DATE_SELECT
DEBUGPRINTFORML LOCALS = %LOCALS%
CALL GET_DATE_NUMBER(LOCALS)
パートナーの行きたい場所 = RESULT
DEBUGPRINTFORML パートナーの行きたい場所 = {パートナーの行きたい場所}
;本体で設定した各キャラ好みのデート先を1/5の確率で出るように
SIF !RAND:5
	CALL PARTNER_DATE_PLACE
SIF RESULT
	パートナーの行きたい場所 = RESULT
;主導権ポイント計算
T = TARGET
CALL GET_INITIATIVE_RATE
;相手の主導権ポイントが極端に高い＆パートナーに主導権があると、たまにデートの行き先を勝手に決定される
IF R >= 50
	LOCAL = R / 10
	;確率に上限アリ
	SIF LOCAL > 15
		LOCAL = 15
ENDIF
IF LOCAL > RAND:100 && TFLAG:主導権
	;相手が握る
;●友情パッチ用改変
		IF IS_友情(TARGET)
			PRINTFORML 外出に誘うと%CALLNAME:TARGET%の行きたいところに連れて行かれた…
		ELSE
			PRINTFORML デートに誘うと%CALLNAME:TARGET%の行きたいところに連れて行かれた…
		ENDIF
;●ここまで
	;強制大成功
	TFLAG:成功判定 = 1
	TFLAG:コマンド内容 = 1
	TEQUIP:デート中 = パートナーの行きたい場所
	RETURN 1
ELSE
	IF !RAND:10 && パートナーの行きたい場所
		CALL DATE_NAME
		;1/10の確率でパートナーに行きたいデート先あり。選んだデート先が同じ場合ボーナス追加
		;パートナーに主導権あり
		SIF TFLAG:主導権
			PRINTFORML %CALLNAME:TARGET%は%PLACE_NAME(パートナーの行きたい場所)%に行きたいようだ…
		SIF !TFLAG:主導権
			PRINTFORML %CALLNAME:TARGET%は%PLACE_NAME(パートナーの行きたい場所)%に行きたそうにしている…
	ELSE
		パートナーの行きたい場所 = 0
	ENDIF
ENDIF
;++++++++++++++++++++++++++++ここまでパートナーの行きたい所関係+++++++++++++++++++++++++++++++++++++	
PRINTL どこに向かいますか？
FOR LOCAL, 0, 50
	SIF STRLENS(TSTR:LOCAL) <= 0
		CONTINUE
	;LOCAL:1 = 0
	;デート場所を変える場合
	SIF FLAG:デート場所
		費用軽減 = -3000
	;行けるかどうか
	SELECTCASE LOCAL
		;神社
		CASE 1
			LOCALS = [{LOCAL, 2}] %TSTR:LOCAL%(お賽銭1000Ｇが更に必要です)
			PRINTFORM %LOCALS, 29, LEFT%
			CALL NEWLINE_U
		;温泉
		CASE 6
			SIF !TALENT:恋人 && !(ABL:親密 > 10 && !(TALENT:MASTER:オトコ^^TALENT:オトコ))
				CONTINUE
			PRINTFORM [{LOCAL, 2}] %TSTR:LOCAL, 12, LEFT%
		;カジノ
		CASE 36
			SIF !(MONEY >= 5500 + 費用軽減)
				CONTINUE
			PRINTFORM [{LOCAL, 2}] %TSTR:LOCAL, 12, LEFT%
		;映画館
		CASE 38
			SIF !(MONEY >= 5000 + 費用軽減 + 映画料金())
				CONTINUE
			LOCALS = [{LOCAL, 2}] %TSTR:LOCAL%({映画料金()}Ｇが更に必要です)
			PRINTFORM %LOCALS, 29, LEFT%
			CALL NEWLINE_U
		;遊覧船
		CASE 39
			SIF !(MONEY >= 15000 + 費用軽減)
				CONTINUE
			LOCALS = [{LOCAL, 2}] %TSTR:LOCAL%(10000Ｇが更に必要です)
			PRINTFORM %LOCALS, 29, LEFT%
			CALL NEWLINE_U
		;スポーツ観戦
		CASE 42
			SIF !(MONEY >= 10000 + 費用軽減)
				CONTINUE
			LOCALS = [{LOCAL, 2}] %TSTR:LOCAL%(5000Ｇが更に必要です)
			PRINTFORM %LOCALS, 29, LEFT%
			CALL NEWLINE_U
		;ゲームセンター
		CASE 35
			PRINTFORM [{LOCAL, 2}] %TSTR:LOCAL, 29, LEFT%
			CALL NEWLINE_U
		CASEELSE
			PRINTFORM [{LOCAL, 2}] %TSTR:LOCAL, 12, LEFT%
	ENDSELECT
	LOCAL:LOCAL = 1
	;改行処理
	CALL NEWLINE_U
	
NEXT

PRINTL 
PRINTL [100] やっぱりやめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	SIF デート場所
		PRINTL 場所を変えるのをやめました
	SIF !デート場所
		PRINTL デートに行くのを止めました
	RETURN 0
ELSEIF TEQUIP:2 == RESULT
	REUSELASTLINE 同じ場所です
	GOTO INPUT_LOOP
ELSEIF LOCAL:RESULT
	TEQUIP:2 = RESULT
	SIF RESULT == 38
		CALL MOVIE_DATE
	RETURN 1
ELSE
	REUSELASTLINE 入力値が不正です
	GOTO INPUT_LOOP
ENDIF
RESTART
;キャラ毎のデート先は未実装(ver0.700)
@DATE_NAME
VARSET TSTR
;TSTR:0 = 人里
TSTR:1 = 神社
TSTR:2 = 霧の湖
TSTR:3 = 花畑
TSTR:4 = 肝試し
TSTR:5 = カラオケ
TSTR:6 = 温泉
TSTR:7 = 図書館
;TSTR:8 = 白玉楼
;TSTR:9 = 外界
;TSTR:10 = 彼岸
;行けるようにしたいけど
;TSTR:11 = 月の都
;TSTR:12 = 妖怪の山
;TSTR:13 = 有頂天
;TSTR:14 = 地霊殿
;TSTR:15 = 間欠泉地下センター
;TSTR:16 = 魔法の森
;TSTR:17 = 命蓮寺
;TSTR:18 = 魔界
;TSTR:19 = %SAVESTR:7%
TSTR:30 = 遊園地
TSTR:31 = 水族館
TSTR:32 = 動物園
TSTR:33 = 繁華街
TSTR:34 = 海
TSTR:35 = ゲームセンター
TSTR:36 = カジノ
TSTR:37 = 博物館
TSTR:38 = 映画館
TSTR:39 = 遊覧船
TSTR:40 = 牧場
TSTR:41 = 教会
TSTR:42 = スポーツ観戦
TSTR:43 = %SAVESTR:6%
;TSTR:44 = 幻想郷

TSTR:50 = コメディ
TSTR:51 = アクション
TSTR:52 = ホラー
TSTR:53 = 恋愛系
TSTR:54 = ポルノ

@PARTNER_DATE_PLACE
;取りあえず繁華街
RETURN 33

;FUNCTIONS中でCALL使えないの忘れていたので、DATE_NAMEを呼んでからこの関数を使う
;ARG TEQUIP:2
@PLACE_NAME(ARG)
#FUNCTIONS
RESULTS = %TSTR:ARG%
RETURNF RESULTS

@GET_DATE_NUMBER(ARGS)
VARSET LOCAL
CALL DATE_NAME
FOR LOCAL, 0, 55
	IF TSTR:LOCAL == ARGS
		LOCAL:100 = LOCAL
		BREAK
	ELSE
		CONTINUE
	ENDIF
NEXT
RETURN LOCAL:100

