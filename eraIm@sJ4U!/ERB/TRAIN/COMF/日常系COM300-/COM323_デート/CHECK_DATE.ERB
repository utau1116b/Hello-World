@CHECK_DATE
;デート終了時のキスや告白の判定
TFLAG:33 = 1

;最低でも従順5、好感度1000が必要
SIF ABL:親密 > 4 && CFLAG:好感度 > 999 && !TALENT:恋人 && !ASSIPLAY
	CALL 告白_デート

;最低でも従順4、好感度400が必要
SIF ABL:親密 > 3 && CFLAG:好感度 > 399 && TALENT:キス未経験 && !ASSIPLAY && !TALENT:153 && !IS_友情(TARGET)
	CALL ファーストキス_デート

;アクセサリプレゼント判定
;告白やキスが発生していると口上の兼ね合い的な問題で発生しない(TFLAG:33で判定）
SIF TFLAG:33
	CALL ACCESSORY_CHECK

RETURN 1

;告白判定
@告白_デート
A = 0
;目標難易度は190
V = 190
;純愛系コマンド実行値取得
A = GET_JUN_VALUE()

;好感度
A += CFLAG:2 / 50
;キス未経験
SIF TALENT:151
	A -= 10
;恋慕
SIF TALENT:85
	A += 30

;臆病
SIF TALENT:10
	A -= 10

;恥じらい
SIF TALENT:35
	A -= 10

;恥薄い
SIF TALENT:36
	A += 5

;悲観的
SIF TALENT:26
	A -= 5

;一線越えない
SIF TALENT:27
	A -= 15

;Aの値を退避
Z = A
;主導権基準値ボーナス
T = TARGET
CALL GET_INITIATIVE_RATE
;退避させた値を戻す
A = Z
;従順なキャラには＋ボーナス、強気なキャラには−ボーナス
IF R < 0
	;負の値の計算がeramakerのバグで怪しいので要注意
	R = R * -1
	A += R / 5 + 5
ELSEIF R > 0
	A -= R / 5
ENDIF

;ムードボーナス この値が本命
IF TFLAG:37 < 10
	A -= 10
ELSEIF TFLAG:37 < 30
	A -= 1
ELSEIF TFLAG:37 < 50
	A += 1
ELSEIF TFLAG:37 < 100
	A += TFLAG:37 / 6
ELSEIF TFLAG:37 < 300
	A += TFLAG:37 / 4
ELSE
	A += TFLAG:37
ENDIF

;デート経験 これも割と大きい
IF EXP:94 < 50
	A -= 5
ELSEIF EXP:94 < 100
	A += 5
ELSEIF EXP:94 < 500
	A += EXP:94 / 15
ELSE
	A += EXP:94 / 10
ENDIF

;判定成功
IF A >= V
	;この辺の描写は口上との兼ね合いが難しいけど、とりあえず暫定処置
	;口上表示
	TFLAG:600 = 26
	TALENT:153 = 1
	CALL KOJO_MESSAGE_SEND("JUN")
	SIF TALENT:153 == 0
		RETURN 0
	IF TALENT:85 == 0
;●友情パッチ用改変
		IF IS_友情(TARGET)
			TALENT:156 = 1
			PRINTFORMW %CALLNAME:TARGET%は【信頼】を得ました
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は【恋慕】を得ました
		ENDIF
		TALENT:85 = 1
		;堕とした人数＋１
		FLAG:30 += 1
;●ここまで
		;堕とした人数が5人以上で、謎の魅力が付く
		IF FLAG:30 >= 5 && TALENT:MASTER:92 == 0
			PRINTFORMW %NAME:MASTER%は【謎の魅力】を身につけた
			TALENT:MASTER:92 = 1
		ENDIF
	ENDIF
	T = TARGET
	CALL GET_LOVERS
;●友情パッチ用改変
	IF !IS_友情(TARGET)
		IF TALENT:PLAYER:151
			PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
			TALENT:PLAYER:151 = 0
		ENDIF
		IF TALENT:151
			TALENT:151 = 0
			PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
			TFLAG:38 = 1
		ENDIF
		EXP:96 += 1
		EXP:PLAYER:96 += 1
	ENDIF
;●ここまで
	
	;既成事実２に相当
	TFLAG:200 = 2
	
	TFLAG:33 = 0
ENDIF

@ファーストキス_デート
;ファーストキス判定
A = 0
;目標難易度は70
V = 70

;純愛系コマンド実行値取得
A = GET_JUN_VALUE()

;好感度
A += CFLAG:2 / 50

;恋慕
SIF TALENT:85
	A += 30

;臆病
SIF TALENT:10
	A -= 10

;恥じらい
SIF TALENT:35
	A -= 10

;恥薄い
SIF TALENT:36
	A += 5

;悲観的
SIF TALENT:26
	A -= 5

;一線越えない
SIF TALENT:27
	A -= 10

;Aの値を退避
Z = A
;主導権基準値ボーナス
T = TARGET
CALL GET_INITIATIVE_RATE
;退避させた値を戻す
A = Z
IF R < 0
	A += R / 5 + 5
ELSEIF R > 0
	A -= R / 5
ENDIF

;ムードボーナス この値が本命
IF TFLAG:37 < 10
	A -= 10
ELSEIF TFLAG:37 < 30
	A -= 1
ELSEIF TFLAG:37 < 50
	A += 1
ELSEIF TFLAG:37 < 100
	A += TFLAG:37 / 5
ELSEIF TFLAG:37 < 300
	A += TFLAG:37 / 3
ELSE
	A += TFLAG:37
ENDIF

;デート経験 これも割と大きい
IF EXP:94 < 50
	A -= 5
ELSEIF EXP:94 < 100
	A += 5
ELSEIF EXP:94 < 500
	A += EXP:94 / 15
ELSE
	A += EXP:94 / 10
ENDIF

;判定成功
IF A >= V
	;この辺の描写は口上との兼ね合いが難しいけど、とりあえず暫定処置
	;口上表示
	TFLAG:600 = 25
	CALL KOJO_MESSAGE_SEND("JUN")
	TALENT:151 = 0
	PRINTFORML %EXPNAME:96%＋１
	EXP:96 += 1
	EXP:PLAYER:96 += 1
	IF TALENT:PLAYER:151
		PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
		TALENT:PLAYER:151 = 0
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
	TFLAG:38 = 1
	;既成事実1に相当
	TFLAG:200 = 1
	
	TFLAG:33 = 0
ENDIF
