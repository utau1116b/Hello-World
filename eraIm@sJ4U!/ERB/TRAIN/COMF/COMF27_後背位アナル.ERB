;-------------------------------------------------
;後背位アナル
;セックス系コマンド、レベル1
;-------------------------------------------------
@COM27
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 64
	CALL COM_ABLE64
	SIF RESULT
		JUMP COM64
ENDIF
;室内でないと立位に
;SIF !INROOM(CFLAG:PLAYER:現在位置)
;	TFLAG:SELECTCOM分岐 = 1

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 2
;強度
TCVAR:PLAYER:4 = 2000
;既成事実3に相当
TFLAG:200 = 3
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 80
DOWNBASE:気力 += 120
;DOWNBASE:疲労 += 50

SOURCE:快Ａ = 400
SOURCE:情愛 = 100
SOURCE:痛み = 900
SOURCE:露出 = 80
SOURCE:逸脱 = 200
SOURCE:反感 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:反感 += 500

;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:Ａ
STAIN:PLAYER:Ｐ |= STAIN:腸内
STAIN:Ａ |= STAIN:PLAYER:Ｐ
STAIN:腸内 |= STAIN:PLAYER:Ｐ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 ++
;EXP:Ａ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(39 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(39 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF


;愛情経験
SIF !ASSIPLAY
	EXP:愛情経験 += TALENT:恋慕

RETURN 1

@CAN_COM27(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:19
	SKIPDISP ARG
	;IF !INROOM(CFLAG:PLAYER:現在位置)
	;	PRINTL 背面立位アナル
	;ELSE
		PRINTL 後背位アナル
	;ENDIF
	PRINTL 
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		SIF S
			PRINT  + 
		A += ABL:Ａ感覚 * 2
		PRINT Ａ感覚
		PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
		S = 1
	ENDIF
	;快楽依存
	IF MARK:快楽依存
		SIF S
			PRINT  + 
		A += MARK:快楽依存 * 3
		PRINT 快楽依存
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定 && !TALENT:淫乱
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:男嫌い && !(TALENT:PLAYER:オトコ) && (!TALENT:恋慕 || PLAYER != MASTER)
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF
	;淫乱
	IF TALENT:淫乱 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT 淫乱
		PRINTV '(,15,')
		S = 15
	ENDIF
	;娼婦
	IF TALENT:淫乱 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 20
		PRINT 娼婦
		PRINTV '(,20,')
		S = 20
	ENDIF
	;A経験
	IF EXP:Ａ経験 < EXPLV:2
		PRINT  - 
		A -= 30
		PRINT Ａ経験不足
		PRINTV '(,30,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:Ｐ & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:Ｐ & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:Ｐ & 8
		Y += 7
	SIF STAIN:PLAYER:Ｐ & 16
		Y += 15
	Y = Y * (2 - TALENT_LV(TARGET, "汚臭耐性")) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭敏感
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(32以上で実行)
	PRINT  = 
	PRINTV A

	V = 32
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1
