;-------------------------------------------------
;バイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM11
;着脱の処理
TEQUIP:バイブ = !TEQUIP:バイブ
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 30
DOWNBASE:気力 += 100

SOURCE:快Ｖ = 250
SOURCE:痛み = 50
SOURCE:露出 = 20
SOURCE:逸脱 = 40
SOURCE:反感 = 40


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:反感 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:痛み += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 2
	SOURCE:痛み += 500
	SOURCE:反感 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;挿入時はＶ経験+
EXP:Ｖ経験 ++

;異常経験
SIF TALENT:処女 == 1
	EXP:異常経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(39 + HETEROSEX(TARGET,PLAYER)) += 1
	EXP:PLAYER:(39 + HETEROSEX(TARGET,PLAYER)) += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COMバイブ(ARG)
;バイブ挿入中
PRINTL ＜バイブ挿入中＞

DOWNBASE:ARG:体力 += 10
DOWNBASE:ARG:気力 += 50

SOURCE:ARG:快Ｖ += 125
SOURCE:ARG:露出 += 10
SOURCE:ARG:逸脱 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:ARG:抑圧 || TALENT:ARG:抵抗
	SOURCE:ARG:反感 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
LOCAL = 25
LOCAL:1 = 20
IF EXP:ARG:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:ARG:Ｖ経験 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:ARG:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.70
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.40
ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.10
ELSEIF PALAM:ARG:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.10
ENDIF
SOURCE:ARG:痛み += LOCAL
SOURCE:ARG:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(10,TARGET)
	EXP:ARG:Ｖ経験 += 1

RETURN 1

