;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/撮影技能upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;撮影技能up本体
;-------------------------------------------------
@ABLUP21MAIN
DRAWLINE
IF ABL:撮影技能 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP21

PRINTFORM [0] - %PALAMNAME:7%の珠×{A}
;IF ABL:撮影技能 >= 2
	PRINTFORM 、%EXPNAME:91%{B}
;ENDIF
PRINT 以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:撮影技能 += 1

IF RESULT == 0
	JUEL:7 -= A
ENDIF

PRINTS ABLNAME:21
PRINT のレベルが
PRINTV ABL:撮影技能
PRINTW になりました。



;-------------------------------------------------
;撮影技能が上げられるかチェック
;-------------------------------------------------
@ISABLUP21
;上限越してるならリターン
IF ABL:撮影技能 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP21

;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

;習得の珠で上げる
SIF JUEL:7 < A
	I |= 1

SIF EXP:91 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;撮影技能を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP21
IF ABL:撮影技能 == 0
	A = 1
	B = 5
ELSEIF ABL:撮影技能 == 1
	A = 25
	B = 10
	SIF EXP:91 >= EXPLV:3
		A = 15
ELSEIF ABL:撮影技能 == 2
	A = 200
	B = 20
	SIF EXP:91 >= EXPLV:4
		A = 150
ELSEIF ABL:撮影技能 == 3
	A = 3000
	B = 40
	SIF EXP:91 >= EXPLV:5
		A = 1500
ELSEIF ABL:撮影技能 == 4
	A = 20000
	B = 80
	SIF EXP:91 >= EXPLV:5
		A = 10000
ELSE
	A = 20000
	B = 80
	SIF EXP:91 >= EXPLV:5
		A = 10000
	;EASYモードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:撮影技能 - 3)
		B = B * (ABL:撮影技能 - 2) / 2
	ELSE
		A = A * (ABL:撮影技能 - 3) * (ABL:撮影技能 - 3)
		B = B * (ABL:撮影技能 - 3)
	ENDIF
ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
	TIMES B , 0.75
ENDIF

;習得遅い
IF TALENT:51
	TIMES A , 1.20
	TIMES B , 1.10
ENDIF


;撮影素質で補正をかける
IF ABL:51 == 2
	TIMES A , 0.80
	TIMES B , 0.80
ELSEIF ABL:51 == 1
	TIMES A , 0.90
	TIMES B , 0.90
ENDIF



