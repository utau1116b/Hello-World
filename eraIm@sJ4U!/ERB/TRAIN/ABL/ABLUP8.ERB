;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/マゾっ気upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;マゾっ気up本体
;-------------------------------------------------
@ABLUP8MAIN
DRAWLINE
IF ABL:8 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP8

;欲望がマゾっ気＋１レベルでないといけない
PRINTFORML %ABLNAME:1%{ABL:8+1}LV以上、かつ

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[解放]なら無視できる）
IF F
	PRINTFORML %EXPNAME:50%{F}以上、かつ
ENDIF

IF B > 0
	PRINTFORM [0] - %PALAMNAME:9%の珠×{A}、%PALAMNAME:5%の珠×{B}
	IF C > 0
		PRINTFORM 、%EXPNAME:30%{C}以上
	ENDIF
	PRINT ……
	
	IF I == 0
		PRINT ＯＫ
	ELSE
		SIF I & 1
			PRINT 珠不足 
		SIF I & 2
			PRINT 経験不足 
		SIF I & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF D > 0
	PRINTFORM [1] - %PALAMNAME:9%の珠×{D}、%PALAMNAME:6%の珠×{E}
	IF C > 0
		PRINTFORM 、%EXPNAME:30%{C}以上
	ENDIF
	PRINTFORM 、%EXPNAME:2%{1}以上……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF B > 0
	PRINTFORM [2] - %PALAMNAME:10%の珠×{A}、%PALAMNAME:5%の珠×{B}……
	
	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF D > 0
	PRINTFORM [3] - %PALAMNAME:8%の珠×{D}、%PALAMNAME:4%の珠×{E}
	PRINTFORM 、%EXPNAME:2%{1}以上……
	
	IF L == 0
		PRINT ＯＫ
	ELSE
		SIF L & 1
			PRINT 珠不足 
		SIF L & 2
			PRINT 経験不足 
		SIF L & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100
	RESTART
ELSEIF B <= 0 && RESULT == 0
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF D <= 0 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 2
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF D <= 0 && RESULT == 3
	RESTART
ELSEIF L != 0 && RESULT == 3
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:8 += 1

IF RESULT == 0
	JUEL:9 -= A
	JUEL:5 -= B
ELSEIF RESULT == 1
	JUEL:9 -= D
	JUEL:6 -= E
ELSEIF RESULT == 2
	JUEL:10 -= A
	JUEL:5 -= B
ELSEIF RESULT == 3
	JUEL:8 -= D
	JUEL:4 -= E
ENDIF

PRINTS ABLNAME:8
PRINT のレベルが
PRINTV ABL:8
PRINTW になりました。



;-------------------------------------------------
;マゾっ気が上げられるかチェック
;-------------------------------------------------
@ISABLUP8
IF ABL:8 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF
;能力アップに必要なコストを計算
CALL COSTABLUP8

;条件別にＯＫかダメかを記録する
I = 0
J = 0
K = 0
L = 0
;能力アップ可能なら1にする
Z = 0

;欲望がマゾっ気＋１レベルでないといけない
IF ABL:1 < ABL:8+1
	;欲望が不足
	I |= 4
	J |= 4
	K |= 4
	L |= 4
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[解放]なら無視できる）
IF EXP:50 < F
	;異常経験が不足
	I |= 2
	J |= 2
	K |= 2
	L |= 2
ENDIF

IF B > 0
	;苦痛の珠で上げる
	SIF JUEL:9 < A
		I |= 1
	;欲情の珠で上げる
	SIF JUEL:5 < B
		I |= 1
	;苦痛快楽経験が必要な場合
	SIF EXP:30 < C
		I |= 2

	IF I == 0
		Z = 1
	ENDIF
ENDIF

IF D > 0
	;苦痛の珠で上げる
	SIF JUEL:9 < D
		J |= 1
	;屈服の珠で上げる
	SIF JUEL:6 < E
		J |= 1
	;この場合苦痛快楽経験が必要なことあり
	SIF EXP:30 < C
		J |= 2
	;この場合絶頂経験が１以上必要
	SIF EXP:2 < 1
		J |= 2
	
	IF J == 0
		Z = 1
	ENDIF
	
	;恥情の珠で上げる
	SIF JUEL:8 < D
		L |= 1
	;恭順の珠で上げる
	SIF JUEL:4 < E
		L |= 1
	;この場合絶頂経験が１以上必要
	SIF EXP:2 < 1
		L |= 2
	
	IF L == 0
		Z = 1
	ENDIF
ENDIF

IF B > 0
	;恐怖の珠で上げる
	SIF JUEL:10 < A
		K |= 1
	;欲情の珠で上げる
	SIF JUEL:5 < B
		K |= 1

	IF K == 0
		Z = 1
	ENDIF
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;マゾっ気を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP8
IF ABL:8 == 0
	A = 100
	B = 100
	C = 0
	D = 100
	E = 100
ELSEIF ABL:8 == 1
	A = 500
	B = 500
	C = 0
	D = 500
	E = 300
ELSEIF ABL:8 == 2
	A = 1200
	B = 1000
	C = 0
	D = 1500
	E = 1000
ELSEIF ABL:8 == 3
	A = 0
	B = 0
	C = 10
	D = 3000
	E = 6000
	;一線越えない
	IF TALENT:27
		TIMES C , 1.10
		TIMES D , 1.10
		TIMES E , 1.10
	ENDIF
ELSEIF ABL:8 == 4
	A = 0
	B = 0
	C = 50
	D = 5000
	E = 12000
	;一線越えない
	IF TALENT:27
		TIMES C , 1.30
		TIMES D , 1.30
		TIMES E , 1.30
	ENDIF
ELSE
	A = 0
	B = 0
	C = 50
	D = 5000
	E = 12000
	;EASYモードなら軽減
	IF ITEM:99 == 1
		C = C * (ABL:8 - 2) / 2
		D = D * (ABL:8 - 3)
		E = E * (ABL:8 - 3)
		;一線越えない
		IF TALENT:27
			TIMES C , 1.30
			TIMES D , 1.30
			TIMES E , 1.30
		ENDIF
	ELSE
		C = C * (ABL:8 - 3)
		D = D * (ABL:8 - 3) * (ABL:8 - 3)
		E = E * (ABL:8 - 3) * (ABL:8 - 3)
		;一線越えない
		IF TALENT:27
			TIMES C , 1.30
			TIMES D , 1.30
			TIMES E , 1.30
		ENDIF
	ENDIF
ENDIF

;解放
IF TALENT:33
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;倒錯的
IF TALENT:80
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
	TIMES E , 0.75
ENDIF

;淫乱化
IF TALENT:76
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
	TIMES E , 0.80
ENDIF

;マゾ
IF TALENT:79
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;サド
IF TALENT:83
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 1.50
	TIMES D , 1.50
	TIMES E , 1.50
ENDIF

;サドであればあげ難い
IF ABL:15 >= 30
	TIMES A , 7.00
	TIMES B , 7.00
	TIMES C , 7.00
	TIMES D , 7.00
	TIMES E , 7.00
ELSEIF ABL:15 >= 10
	TIMES A , 6.00
	TIMES B , 6.00
	TIMES C , 6.00
	TIMES D , 6.00
	TIMES E , 6.00
ELSEIF ABL:15 >= 5
	TIMES A , 5.00
	TIMES B , 5.00
	TIMES C , 5.00
	TIMES D , 5.00
	TIMES E , 5.00
ELSEIF ABL:15 == 4
	TIMES A , 4.00
	TIMES B , 4.00
	TIMES C , 4.00
	TIMES D , 4.00
	TIMES E , 4.00
ELSEIF ABL:15 == 3
	TIMES A , 3.00
	TIMES B , 3.00
	TIMES C , 3.00
	TIMES D , 3.00
	TIMES E , 3.00
ELSEIF ABL:15 == 2
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
	TIMES E , 2.00
ELSEIF ABL:15 == 1
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 1.50
	TIMES D , 1.50
	TIMES E , 1.50
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[解放][マゾ]なら無視できる）
IF ABL:8 >= 3 && TALENT:33 == 0 && TALENT:79 == 0
	F = ABL:8 * 3
ELSE
	F = 0
ENDIF


