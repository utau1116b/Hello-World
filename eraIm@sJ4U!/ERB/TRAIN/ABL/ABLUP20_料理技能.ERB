;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/料理技能upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;料理技能up本体
;-------------------------------------------------
@ABLUP20MAIN
DRAWLINE
IF ABL:料理技能 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP20

PRINTFORM [0] - %PALAMNAME:16%の珠×{A}
IF ABL:料理技能 >= 2
	PRINTFORM 、家事経験{B}
ENDIF
PRINT 以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTFORM [1] - %PALAMNAME:7%の珠×{C}
IF ABL:料理技能 >= 2
	PRINTFORM 、家事経験{B}
ENDIF
PRINT 以上……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
	SIF J & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:料理技能 += 1

IF RESULT == 0
	JUEL:16 -= A
ELSEIF RESULT == 1
	JUEL:7 -= C
ENDIF

PRINTS ABLNAME:20
PRINT のレベルが
PRINTV ABL:料理技能
PRINTW になりました。



;-------------------------------------------------
;料理技能が上げられるかチェック
;-------------------------------------------------
@ISABLUP20
IF ABL:料理技能 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP20

;条件別にＯＫかダメかを記録する
I = 0
J = 0

;能力アップ可能なら1にする
Z = 0

;生活の珠で上げる
SIF JUEL:16 < A
	I |= 1

;習得の珠で上げる
SIF JUEL:7 < C
	J |= 1

IF EXP:家事経験 < B
	I |= 2
	J |= 2
ENDIF

IF I == 0
	Z = 1
ENDIF

IF J == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;料理技能を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP20
IF ABL:料理技能 == 0
	A = 1
	B = 0
	C = 75
ELSEIF ABL:料理技能 == 1
	A = 50
	B = 0
	C = 500
	IF EXP:家事経験 >= EXPLV:3
		A = 20
		C = 250
	ENDIF
ELSEIF ABL:料理技能 == 2
	A = 600
	B = 25
	C = 3000
	IF EXP:家事経験 >= EXPLV:4
		A = 300
		C = 1500
	ENDIF
ELSEIF ABL:料理技能 == 3
	A = 5000
	B = 50
	C = 9000
	IF EXP:家事経験 >= EXPLV:5
		A = 2500
		C = 4500
	ENDIF
ELSEIF ABL:料理技能 == 4
	A = 20000
	B = 100
	C = 24000
	IF EXP:家事経験 >= EXPLV:5
		A = 10000
		C = 12000
	ENDIF
ELSE
	A = 20000
	B = 100
	C = 24000
	;EASYモードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:料理技能 - 3)
		B = B * (ABL:料理技能 - 2) / 2
		C = C * (ABL:料理技能 - 3)
	ELSE
		A = A * (ABL:料理技能 - 3) * (ABL:料理技能 - 3)
		B = B * (ABL:料理技能 - 3)
		C = C * (ABL:料理技能 - 3) * (ABL:料理技能 - 3)
	ENDIF
	;倍の経験を持っているなら必要数軽減
	IF EXP:家事経験 >= B * 2
		A = A / 2
		C = C / 2
	ENDIF

ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
	TIMES B , 0.75
	TIMES C , 0.80
ENDIF

;習得遅い
IF TALENT:51
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.20
ENDIF

;調合知識
IF TALENT:55
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

;恋慕
IF TALENT:85
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF
