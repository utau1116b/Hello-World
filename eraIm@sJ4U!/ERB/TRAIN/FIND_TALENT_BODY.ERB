;素質発覚イベント詰め合わせ
@FINE_VIRGIN
IF FLAG:2 == 1 && (CFLAG:60 & 1) == 0
	;V経験で発覚
	IF TFLAG:19
		SIF TFLAG:3 == 1
			PRINTFORMW どうやら%CALLNAME:TARGET%は【処女】だったようだ・・・
		CFLAG:60 |= 1
	ELSEIF SELECTCOM == 71
		SIF TALENT:0
			PRINTFORMW どうやら%CALLNAME:TARGET%は【処女】のようだ
		CFLAG:60 |= 1
	ENDIF
ENDIF
@FIND_BUSTSIZE
;コンフィグが有効でかつバストサイズが発覚していない
IF FLAG:2 == 1 && (CFLAG:60 & 32) == 0
	;ウフフ中もしくは胸愛撫、湖(夏)、海(夏)でデート
	IF TFLAG:44 || SELECTCOM == 360 || ((TEQUIP:2 == 3 || TEQUIP:2 == 14) && DAY:2 == 1)
		;無条件で発覚
		CALL FIND_BUSTSIZE2
	;肝試し中、事故で発覚
	ELSEIF TEQUIP:2 == 5
		A = 50
		;臆病
		SIF TALENT:10
			A += 25
		;気丈
		SIF TALENT:12
			A -= 10
		;プライド高い
		SIF TALENT:15
			A -= 10
		;ツンデレ
		SIF TALENT:18
			A += 10
		;自制心
		SIF TALENT:20
			A -= 10
		;感情乏しい
		SIF TALENT:22
			A -= 15
		;恋慕
		SIF TALENT:85
			A += 15
		;恋人
		SIF TALENT:153
			A += 10
		;欲情
		IF PALAM:5 >= 8000
			A += 20
		ELSEIF PALAM:5 >= 6000
			A += 15
		ELSEIF PALAM:5 >= 4000
			A += 10
		ELSEIF PALAM:5 >= 2000
			A += 5
		ELSEIF PALAM:5 >= 1000
		ENDIF
		;恐怖
		IF PALAM:10 >= 4000
			A += 20
		ELSEIF PALAM:10 >= 3000
			A += 15
		ELSEIF PALAM:10 >= 2000
			A += 10
		ELSEIF PALAM:10 >= 1000
			A += 5
		ELSEIF PALAM:10 >= 500
		ENDIF
		;最高値150
		;最低値10
		;不利要素全部持ち最高値80
		
		;確立調整のため120以上は120に
		SIF A > 120
			A = 120
		A += RAND:100
		SIF A >= 175
			CALL FIND_BUSTSIZE2
		S = 0
	ENDIF
ENDIF

@FIND_BUSTSIZE2
;肝試し中(地の文nnsmyの人にいただきました。ありがとうございます)
IF TEQUIP:2 == 5
	PRINTFORMW 暗がりを二人で歩いていると、ガサッ、と物音がした。
	;恋慕
	IF TALENT:85
		PRINTFORML %CALLNAME:TARGET%は小さく悲鳴を上げて%CALLNAME:MASTER%の腕に抱きついてきた。
		;小悪魔もしくは欲情1000以上
		SIF TALENT:87 || PALAM:5 >= 1000
			PRINTFORMW 少々わざとらしい気がしたが、悪い気分はしないので何も言わないでおいた。
	;感情乏しい、もしくは無関心
	ELSEIF TALENT:21 || TALENT:22
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の後ろに回ると%CALLNAME:MASTER%を前に押し出した。
		PRINTFORMW どうやら身代わりに差し出されたようだ…
	;気丈、プライド高い、ツンデレ
	ELSEIF TALENT:12 || TALENT:15 || TALENT:18
		;あたってない
		PRINTFORML %CALLNAME:TARGET%は思わず一歩下がり身構えた。
		PRINTFORMW その際%CALLNAME:MASTER%の抱いていた事に気づくと、なんでもないと手を離した。
	;臆病
	ELSEIF TALENT:10
		PRINTFORML %CALLNAME:TARGET%は可愛らしい悲鳴をあげ、%CALLNAME:MASTER%に抱きついてきた。
		PRINTFORMW %CALLNAME:MASTER%が腕を動かすのに不自由なくらいに%CALLNAME:TARGET%は怯えている…
		IF (CFLAG:61 & 1) == 0
			PRINTFORMW %CALLNAME:TARGET%はどうやら【臆病】なようだ。
			CFLAG:61 |= 1
		ENDIF
	;その他
	ELSE
		PRINTFORML %CALLNAME:TARGET%は可愛らしい悲鳴をあげ、%CALLNAME:MASTER%に抱きついてきた。
		PRINTFORMW やがて何もないことがわかると、少し顔を赤くして%CALLNAME:MASTER%からはなれた…
	ENDIF
	;素質分岐内にぶち込む必要が出るかもしれない
	;きょぬー
	IF TALENT:110 || TALENT:141
		;恋慕がなく、感情乏しい、もしくは無関心の場合、背中に当たる
		IF TALENT:85 == 0 && (TALENT:21 || TALENT:22)
			PRINTFORML %CALLNAME:MASTER%の背中に柔らかい物が押し当てられる。
		ELSE
			PRINTFORML %CALLNAME:MASTER%の腕に、柔らかい物に挟まれているような感触が伝わってくる。
		ENDIF
		;着痩せ
		IF TALENT:142
			PRINTFORMW 思っていた以上に、%CALLNAME:TARGET%の胸は大きいようだ…
		ELSE
			PRINTFORMW みた通りに間違いなく、%CALLNAME:TARGET%の胸は大きいようだ…
		ENDIF
	;ひんぬー
	ELSEIF TALENT:109 || TALENT:140
		PRINTFORML 何か当たっているような気もするが、気のせいかもしれない。
		PRINTFORMW わかってはいた事だが、%CALLNAME:TARGET%の胸は大分小さいようだ…
	;並乳
	ELSE
		;恋慕がなく、感情乏しい、もしくは無関心の場合、背中に当たる
		IF TALENT:85 == 0 && (TALENT:21 || TALENT:22)
			PRINTFORML %CALLNAME:MASTER%の背中に柔らかい物が押し当てられる。
		ELSE
			PRINTFORML %CALLNAME:MASTER%の腕に柔らかい感触が伝わってくる。
		ENDIF
		;着痩せ
		IF TALENT:142
			PRINTFORMW 思っていたよりも、%CALLNAME:TARGET%の胸は大きいようだ…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の胸は見た目どおり、程よい大きさのようだ…
		ENDIF
	ENDIF
;それ以外の状況
ELSE
	;着痩せを持っている
	IF TALENT:142
		PRINTFORML %CALLNAME:TARGET%は意外と胸がある……
	ENDIF
	IF TALENT:110
		PRINTFORMW %CALLNAME:TARGET%はどうやら【巨乳】のようだ
	ELSEIF TALENT:109
		PRINTFORMW %CALLNAME:TARGET%はどうやら【貧乳】のようだ
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はどうやら【並乳】のようだ
	ENDIF
ENDIF
CFLAG:60 |= 32

