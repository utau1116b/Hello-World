
;スリップダメージチェック
@CHECK_SLIP_DAMAGE
;LOCAL
;0	LOOP0
;1	LOOP1

;敵側更新
FOR LOCAL:0, 1, 11
	IF TFLAG:(LOCAL:0 + 1000) > 0
		CALL CHECK_PERSONAL_SLIP_DAMAGE(LOCAL:0)
	ENDIF
NEXT

;味方側更新
FOR LOCAL:0, 1080, 1085
	IF FLAG:(LOCAL:0) > 0
		CALL CHECK_PERSONAL_SLIP_DAMAGE(FLAG:(LOCAL:0))
	ENDIF
NEXT


;スリップダメージチェック
;ARG:0	対象キャラ
@CHECK_PERSONAL_SLIP_DAMAGE(ARG:0)
;LOCAL
;0	LOOP0
;1	LOOP1
IF CFLAG:(ARG:0):スリップ状態
	LOCAL:0 = MAXBASE:(ARG:0):HP / 20
	;ボスは軽減
	SIF CFLAG:(ARG:0):ボスフラグ
		LOCAL:0 = MAXBASE:(ARG:0):HP / 50
	SIF LOCAL:0 < 1
		LOCAL:0 = 1
	PRINTFORM %CALLNAME:(ARG:0)%の
	CALL STARMARK_Y
	PRINTFORM がスリップする！
	CALL STARMARK_Y
	PRINTFORML が%ADD_COMMA(LOCAL:0)%減少した！
	BASE:(ARG:0):HP -= LOCAL:0
ENDIF
IF CFLAG:(ARG:0):リジェネ状態 > 10
	LOCAL:0 = MAXBASE:(ARG:0):HP * (CFLAG:(ARG:0):リジェネ状態 % 10) / 100
	SIF LOCAL:0 < 1
		LOCAL:0 = 1
	;PRINTFORML %CALLNAME:(ARG:0)%が纏う緑色の光が%CALLNAME:(ARG:0)%を癒す！
	PRINTFORML リジェネ発動＞＞%CALLNAME:ARG%
	CALL STARMARK_Y
	PRINTFORML が%ADD_COMMA(LOCAL:0)%回復
	BASE:(ARG:0):HP += LOCAL:0
ENDIF
IF CFLAG:(ARG:0):ハートオートヒール状態 > 10
	LOCAL:0 = MAXBASE:(ARG:0):MP * (CFLAG:(ARG:0):ハートオートヒール状態 % 10) / 100
	SIF LOCAL:0 < 1
		LOCAL:0 = 1
;	PRINTFORML %CALLNAME:(ARG:0)%が纏う青色の光が%CALLNAME:(ARG:0)%を癒す！
	PRINTFORML ハートリカバリー＞＞%CALLNAME:ARG%
	CALL HEARTMARK_P
	PRINTFORML が%ADD_COMMA(LOCAL:0)%回復
	BASE:(ARG:0):MP += LOCAL:0
ENDIF
IF CFLAG:(ARG:0):EX状態
	LOCAL:0 = MAXBASE:(ARG:0):MP / 15
	;ボスは軽減
	IF CFLAG:(ARG:0):ボスフラグ
		LOCAL:0 = 10
	ENDIF
	SIF LOCAL:0 < 1
		LOCAL:0 = 1
	PRINTFORML %CALLNAME:(ARG:0)%のTPが消耗していく！TPが%ADD_COMMA(LOCAL:0)%減少した！
	BASE:(ARG:0):MP -= LOCAL:0
	IF BASE:(ARG:0):MP < 0
		BASE:(ARG:0):MP = 0
	ENDIF
ENDIF
IF CFLAG:(ARG:0):変身状態
	LOCAL:0 = MAXBASE:(ARG:0):MP / 33
	;ボスは軽減
	IF CFLAG:(ARG:0):ボスフラグ
		LOCAL:0 = 10
	ENDIF
	SIF LOCAL:0 < 1
		LOCAL:0 = 1
	PRINTFORML %CALLNAME:(ARG:0)%は変身を維持するためにTPを消費しているTPが%ADD_COMMA(LOCAL:0)%減少した！
	BASE:(ARG:0):MP -= LOCAL:0
	IF BASE:(ARG:0):MP < 0
		BASE:(ARG:0):MP = 0
	ENDIF
ENDIF
;継続★減少
IF CFLAG:ARG:継続★減少 >= 10
	IF CFLAG:ARG:残り待機時間 == 0 && CFLAG:ARG:現在状態 == 0 &&  CFLAG:ARG:気絶状態 == 0
		LOCAL = CFLAG:ARG:継続★減少 % 10
		LOCAL *= 10
		BASE:ARG:HP -= LOCAL
	ENDIF
ENDIF
		
