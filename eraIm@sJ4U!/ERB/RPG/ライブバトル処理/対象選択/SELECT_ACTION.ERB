
;ターゲット選択
;ARG	対象の取り方(0:敵単体　1:敵全体　2:自分　3:味方単体　4:味方全体　5:敵味方全体　6:敵味方単体)
;ARG:1	自分が対象外かどうか(自分を対象外にする場合、自分のIDを入れておく)
;RETURN	対象(0:キャンセル　1〜5：味方　11〜20：敵　30：自分　31：味方全体　32：敵全体　33：敵味方全体)
@SELECT_TARGET_POSITION(ARG)
;LOCAL
;0		LOOP
;1〜10	利用可能フラグ
;11		TEMP
;12		TEMP

;コンフィグ次第では自動選択
IF FLAG:6710 && (ARG == 0 || ARG == 6)
	;敵単体なら完全自動
	LOCAL:11 = 11
	FOR LOCAL, 1, 11
		IF TFLAG:(LOCAL + 1000) && CFLAG:LOCAL:気絶状態 == 0
			LOCAL:11 = LOCAL + 10
			BREAK
		ENDIF
	NEXT
	RETURN LOCAL:11
ENDIF

IF FLAG:対象選択省略
	;一択省略系
	LOCAL:11 = 11
	SELECTCASE ARG
		CASE 0
			;敵単体　コンフィグ値によらず１択なら省略
			LOCAL:11 = 11
			LOCAL:12 = 0
			FOR LOCAL, 1, 11
				IF TFLAG:(LOCAL + 1000) && CFLAG:LOCAL:気絶状態 == 0
					;敵が居ればその敵を覚えて、カウントアップ
					LOCAL:11 = LOCAL + 10
					LOCAL:12 += 1
				ENDIF
			NEXT
			
			;残り1ならそれを選ぶ　違うならスルー
			IF LOCAL:12 == 1
				RETURN LOCAL:11
			ENDIF
		CASE 1
			;敵全体
			IF FLAG:対象選択省略 == 2
				RETURN 32
			ENDIF
		CASE 2
			;自分
			IF FLAG:対象選択省略 == 2 && ARG:1 == 0
				RETURN 30
			ENDIF
		CASE 3
			;味方単体　コンフィグ値によらず１択なら省略
			LOCAL:11 = 1
			LOCAL:12 = 0
			FOR LOCAL, 1, 6
				IF FLAG:(LOCAL + 1079) > 0
					;居ればカウントアップ
					LOCAL:11 = LOCAL
					LOCAL:12 += 1
				ENDIF
			NEXT
			
			;ソロならそれを選ぶ　違うならスルー
			IF LOCAL:12 == 1 && ARG:1 == 0
				RETURN LOCAL:11
			ENDIF
		CASE 4
			;味方全体
			IF FLAG:対象選択省略 == 2
				RETURN 31
			ENDIF
		CASE 5
			;敵味方全体
			IF FLAG:対象選択省略 == 2
				RETURN 33
			ENDIF
		CASE 6
			;敵味方単体
			;少なくとも敵と自分が居るので省略しない
		CASEELSE
			;エラーなので省略しない
	ENDSELECT
ENDIF


IF ARG == 2 || ARG == 3 || ARG == 4
	PRINTFORML 誰に使いますか？
ELSE
	PRINTFORML 誰を狙いますか？
ENDIF

FOR LOCAL, 1, 11
	LOCAL:LOCAL = 0
NEXT


;敵味方単体は元々の設定でそうなる場合と、特定の条件を満たした場合がある
IF (ARG == 6) || (TFLAG:対象制限解除 && (ARG == 0 || ARG == 3))
	;敵味方単体を選択
	;制限解除＋単体対象スキルの場合も、敵味方単体を選択
	FOR LOCAL, 1, 21
		;初期化
		LOCAL:LOCAL = 0
	NEXT
	
	FOR LOCAL, 1, 6
		IF FLAG:(LOCAL + 1079) > 0 && (ARG:1 == 0 || (LOCAL + 1079) != ARG:1)
			PRINTFORM [{LOCAL, 2}] %CALLNAME:(FLAG:(LOCAL + 1079)),21,LEFT% 
			
			CALL PRINT_HPMPEP(FLAG:(LOCAL + 1079), 0)
			CALL PRINT_HPMPEP(FLAG:(LOCAL + 1079), 1)
			CALL PRINT_HPMPEP(FLAG:(LOCAL + 1079), 2)
			PRINT      状態異常：
			CALL SHOW_BAD_STATUS(FLAG:(LOCAL + 1079))
			PRINTL 
			LOCAL:LOCAL = 1
		ENDIF
	NEXT
	FOR LOCAL, 11, 21
		IF TFLAG:(LOCAL - 10 + 1000) > 0 && CFLAG:(LOCAL - 10):気絶状態 == 0
			PRINTFORM [{LOCAL, 2}] %CALLNAME:(LOCAL - 10), 21, LEFT%
			IF FLAG:5 == 0
				;EASYならHP・EP表示
				;CALL PRINT_HPMPEP((LOCAL - 10), 0)
				;CALL PRINT_HPMPEP((LOCAL - 10), 2)
			ENDIF
			PRINTL 
			LOCAL:LOCAL = 1
		ENDIF
	NEXT
	PRINTFORML [ 0] キャンセル
	
	$INPUT_LOOP_6
	INPUT
	
	IF RESULT < 0 || RESULT > 20
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP_6
	ELSEIF RESULT == 0
		RETURN 0
	ELSEIF LOCAL:(RESULT) == 0
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP_6
	ELSE
		RETURN RESULT
	ENDIF
ENDIF


SELECTCASE ARG
	CASE 0
		;敵単体を選択
		FOR LOCAL, 1, 11
			IF TFLAG:(LOCAL + 1000) > 0 && CFLAG:LOCAL:気絶状態 == 0
				PRINTFORM [{LOCAL, 2}] %CALLNAME:LOCAL, 21, LEFT%
				;PRINTFORM  
				;CALL STARMARK_Y
				;PRINTFORM  {BASE:LOCAL:HP,6}/{MAXBASE:LOCAL:HP,6}
				;CALL PRINT_HPMPEP(LOCAL, 1)
				PRINTL 
			LOCAL:LOCAL = 1
			ENDIF
		NEXT
		PRINTFORML [ 0] キャンセル
		
		$INPUT_LOOP_0
		INPUT
		IF RESULT < 0 || RESULT > 10
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_0
		ELSEIF RESULT == 0
			RETURN 0
		ELSEIF LOCAL:(RESULT) == 0
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_0
		ELSE
			;本来は+10があった
			RETURN (RESULT + 10)
		ENDIF
	CASE 1
		;敵全体を選択
		PRINTFORML [ 1] 敵全体
		PRINTFORML [ 0] キャンセル
		
		$INPUT_LOOP_1
		INPUT
		
		IF RESULT < 0 || RESULT > 1
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_1
		ELSEIF RESULT == 0
			RETURN 0
		ELSE
			RETURN 32
		ENDIF
	CASE 2
		;自分を選択
		PRINTFORML [ 1] 自分
		PRINTFORML [ 0] キャンセル
		$INPUT_LOOP_2
		INPUT
		
		IF RESULT < 0 || RESULT > 1
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_1
		ELSEIF RESULT == 0
			RETURN 0
		ELSE
			RETURN 30
		ENDIF
	CASE 3
		;味方単体を選択
		FOR LOCAL, 1, 6
			IF FLAG:(LOCAL + 1079) > 0
				PRINTFORM [{LOCAL, 2}] %CALLNAME:(FLAG:(LOCAL + 1079)),21,LEFT% 
				CALL STARMARK_Y
				PRINTFORM  {BASE:(FLAG:(LOCAL + 1079)):HP,6}/{MAXBASE:(FLAG:(LOCAL + 1079)):HP,6} 
				CALL HEARTMARK_P
				PRINTFORM {BASE:(FLAG:(LOCAL + 1079)):MP,3}/{MAXBASE:(FLAG:(LOCAL + 1079)):MP,3} 
				CALL PRINT_HPMPEP(FLAG:(LOCAL + 1079), 2)
				PRINTL 
				
				PRINTL 
				PRINT      状態異常：
				CALL SHOW_BAD_STATUS(FLAG:(LOCAL + 1079))
				PRINTL 
				LOCAL:LOCAL = 1
			ENDIF
		NEXT
		PRINTFORML [ 0] キャンセル
		
		$INPUT_LOOP_3
		INPUT
		
		IF RESULT < 0 || RESULT > 5
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_3
		ELSEIF RESULT == 0
			RETURN 0
		ELSEIF LOCAL:(RESULT) == 0
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_3
		ELSE
			RETURN RESULT
		ENDIF
	CASE 4
		;味方全体を選択
		PRINTFORML [ 1] 味方全体
		PRINTFORML [ 0] キャンセル
		
		$INPUT_LOOP_4
		INPUT
		
		IF RESULT < 0 || RESULT > 1
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_4
		ELSEIF RESULT == 0
			RETURN 0
		ELSE
			RETURN 31
		ENDIF
	CASE 5
		;敵味方全体を選択
		PRINTFORML [ 1] 敵味方全体
		PRINTFORML [ 0] キャンセル
		
		$INPUT_LOOP_5
		INPUT
		
		IF RESULT < 0 || RESULT > 1
			REUSELASTLINE 入力値が不正です。再入力してください
			GOTO INPUT_LOOP_5
		ELSEIF RESULT == 0
			RETURN 0
		ELSE
			RETURN 33
		ENDIF
	CASEELSE
		PRINTFORMW エラー！不正な対象番号です(SELECT_TARGET_POSITION)
		RETURN 0
ENDSELECT

