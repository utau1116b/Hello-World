;スキル実行
;ARG:0	実行者登録番号
;ARG:1	実行スキルID
;ARG:2	実行対象者(1〜5：味方　11〜20：敵　30：自分　31：味方全体　32：敵全体　33：敵味方全体)
@DO_ACTION(ARG:0, ARG:1, ARG:2)
;!!!
;発動前の条件チェック

;COMABLEチェック
TRYCCALLFORM COMABLE_{ARG:1}(ARG:0)
CATCH
	RESULT = 0
ENDCATCH
IF RESULT == 0
	IF ARG:1 == 1
		PRINTFORML %CALLNAME:(ARG:0)%はアピールを行うことができない！
		CALL BMSG_WAIT(1)
	ELSE
		PRINTFORML %CALLNAME:(ARG:0)%は%ABLNAME:(ARG:1)%を試みたが失敗した…
		CALL BMSG_WAIT(1)
	ENDIF
	CALL SET_DEFAULT_WAIT(ARG:0)
	RETURN
ENDIF
IF RESULT < 0
	PRINTFORML %CALLNAME:(ARG:0)%の%ABLNAME:(ARG:1)%！　しかしMPが足りない！
	CALL BMSG_WAIT(1)
	CALL SET_DEFAULT_WAIT(ARG:0)
	RETURN
ENDIF

;スキル使用口上
FLAG:フェス中口上 = 7
CALL KOJO(ARG:0, ARG:1)

IF ARG:0 <= 10
	;敵が使った場合赤文字
	SETCOLOR 0xEE1010
ELSE
	;こちらが使った場合青文字
	SETCOLOR 0x10EEEE
ENDIF

TRYCCALLFORM USE_SKILL_{ARG:1}(ARG:0, ARG:2)
CATCH
	PRINTFORMW エラー！未実装のスキル(ID{ARG:1})を使用しました
ENDCATCH
