@どっとっぷTV
#DIMS 曲名, 10
VARSET LOCAL
CUSTOMDRAWLINE =
IF FLAG:リリース管理 == -1
	PRINTFORM リリースまであと
	CALL SET_COLOR("真美")
	PRINTFORML {(6 - DAY:5), 2}日
	RESETCOLOR
ELSEIF FLAG:発売期間 > 0
	PRINTFORML 発売から{FLAG:発売期間, 2}日経過
ELSE

ENDIF
DRAWLINE
PRINTFORML 
CALL セールスランキング
IF RESULT == 100
	LOCAL = 0
ELSE
	LOCAL = RESULT
ENDIF
PRINTFORML ┏━━━━━━━━━━━━━━━━━━━┓
PRINTFORML ┃{LOCAL, 3}位 %SONGNAME:(FLAG:リリース中楽曲), 32, LEFT%┃
PRINTFORML ┃￣￣￣￣￣￣￣￣￣￣￣￣￣￣￣￣￣￣￣┃
PRINTFORML ┃　ARTIST　　　　　　　　　TOTAL　SALES┃
PRINTFORML ┃　%CSTR:0:22, 22, LEFT%　%PRINT_CDセールス(), 10, LEFT%　┃
PRINTFORML ┗━━━━━━━━━━━━━━━━━━━┛
DRAWLINE

DRAWLINE
;ENDIF
PRINTFORMLC [101]曲データ確認
PRINTFORMLC [102]ハイスコア確認
PRINTFORMLC [103]売り上げ確認
PRINTL 
PRINTFORML [0]戻る
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN

ELSEIF RESULT == 101
	CALL 曲データ表示(FLAG:リリース中楽曲)
ELSEIF RESULT == 102
	CALL ハイスコア表示
ELSEIF RESULT == 103
	CALL 売り上げ表示
ELSE
	REUSELASTLINE 入力値が不正です。正しい値を入力して下さい
	GOTO INPUT_LOOP
ENDIF
RESTART

;実際に売る処理
;ARG	売り上げ
@CDセールス(ARG)
#LOCALSIZE 1
VARSET LOCAL
IF !FLAG:発売期間
	PRINTFORMW 「さあ、お楽しみのCD売り上げの発表よ」
	PRINTFORMW 「『%SONGNAME:(FLAG:リリース中楽曲)%』は……」
	PRINTFORMW ・・・・・
	PRINTFORMW ・・・
	PRINTFORMW ・
	PRINTFORMW 「%ADD_COMMA(ARG)%枚売れたわ！」
	PRINTL
ELSE
	PRINTFORM CDの売り上げとして
ENDIF
	PRINTFORML %ADD_COMMA(ARG)%Ｇの収入が発生しました
MONEY += ARG
SONG:(FLAG:リリース中楽曲):1 += ARG
PRINTL 
PRINTW 
RETURN ARG

@新曲リリース
;ABLの使い方変えたことによる暫定処置です
#DIMS 曲名, 10
DRAWLINE
PRINTFORML リリースする曲を選んでください
DRAWLINE
VARSET LOCAL
VARSET A
A:1 = 7
曲名:1 = my song
A:2 = 28
曲名:2 = SMOKY THRILL
A:3 = 45
曲名:3 = READY!!
A:4 = 106
曲名:4 = shiny smile
A:5 = 107
曲名:5 = My Best Friend
A:6 = 116
曲名:6 = ふるふるフューチャー☆
A:7 = 201
曲名:7 = GO MY WAY!!
A:8 = 207
曲名:8 = relations
A:9 = 215
曲名:9 = Kosmos, Cosmos
FOR LOCAL, 1, 10
	IF SONG:(A:LOCAL):0 <= 0
		PRINTFORM [{LOCAL, 3}]%SONGNAME:(A:LOCAL), 24, LEFT%
		CALLFORM VO_{A:LOCAL}
		LOCALS = VO +{RESULT/ 100, 2}
		CALL PRINTCOLOR(SET_COLOR_FUNC("ボーカル"), LOCALS)
		PRINTFORM  
		CALLFORM VI_{A:LOCAL}
		LOCALS = VI +{RESULT / 100, 2}
		CALL PRINTCOLOR(SET_COLOR_FUNC("ビジュアル"), LOCALS)
		PRINTFORM  
		CALLFORM DA_{A:LOCAL}
		LOCALS = DA +{RESULT/ 100, 2}
		CALL PRINTCOLOR(SET_COLOR_FUNC("ダンス"), LOCALS)
		PRINTFORM  
		CALLFORM BPM_{A:LOCAL}
		LOCALS = 　BPM：{RESULT, 3}
		CALL PRINTCOLOR(SET_COLOR_FUNC("雪歩"), LOCALS)
		PRINTFORM  
		CALLFORM 曲タイプ_{A:LOCAL}
		SELECTCASE RESULT
			CASE 1
				CALL SET_COLOR("ボーカル")
			CASE 2
				CALL SET_COLOR("ビジュアル")
			CASE 3
				CALL SET_COLOR("ダンス")
			CASEELSE
				RESETCOLOR
		ENDSELECT
		PRINTFORM ★
		RESETCOLOR
		LOCAL:LOCAL = 1
		PRINTL 
	ENDIF
NEXT
DRAWLINE
IF FLAG:楽曲について == 1
	FLAG:楽曲について = 0
	PRINTFORML 楽曲,CDセールスについてのチュートリアルを表示しますか？
	PRINTFORML [0] はい
	PRINTFORML [1] いいえ
	$INPUT_LOOP709
	INPUT
	IF RESULT == 0
		CALL チュートリアル("楽曲について")
		PRINTFORML 「CDセールスについてのチュートリアルも聞いておく？」
		PRINTL 
		PRINTFORML [0] はい
		PRINTFORML [1] いいえ
		FLAG:CDセールスについて = 0
		$INPUT_LOOP708
		INPUT
		IF RESULT == 0
			CALL チュートリアル("ＣＤリリースについて")
			RESTART
		ELSEIF RESULT == 1
			PRINTFORMW 「あら残念。　まあ、いいわ。　それじゃあリリースする楽曲を選んでね」
			RESTART
		ELSE
			REUSELASTLINE 入力値が不正です。正しい値を入力して下さい
			GOTO INPUT_LOOP708
		ENDIF
	ELSEIF RESULT == 1
		RESTART
	ELSE
		REUSELASTLINE 入力値が不正です。正しい値を入力して下さい
		GOTO INPUT_LOOP709
	ENDIF
ENDIF
IF FLAG:初回リリース > 0

ELSE
	PRINTFORML [  0]戻る
ENDIF
$INPUT_LOOP
INPUT
IF RESULT == 0 && FLAG:初回リリース == 0
	RETURN
ELSEIF MONEY < 5000 && FLAG:初回リリース == 0
	REUSELASTLINE ゲイツが足りません……
	GOTO INPUT_LOOP
ELSEIF LOCAL:RESULT == 1
	;リリースの準備
	FLAG:リリース中楽曲 = A:RESULT
	FLAG:リリース期間 = 0
	FLAG:発売期間 = 0
	;準備中
	FLAG:リリース管理 = -1
	FLAG:リリース中ハイスコア = 0
	PRINTFORM 『 %曲名:RESULT%』をリリース楽曲に選択しました
	IF FLAG:初回リリース
		PRINTFORMW （{7-DAY}日後に発売されます）
	ELSE
		PRINTFORMW （一週間後に発売されます）
	ENDIF
ELSE
	REUSELASTLINE 入力値が不正です。正しい値を入力して下さい
	GOTO INPUT_LOOP
ENDIF

@ハイスコア表示
VARSET LOCAL
DRAWLINE
FOR LOCAL, 1, 300
	SIF SONG:LOCAL:0 > 0
		PRINTFORML %SONGNAME:LOCAL% - %ADD_COMMA(SONG:LOCAL:0)%point
NEXT
DRAWLINE
PRINTFORML [0]戻る
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN
ELSE
	CALL 再入力
	GOTO INPUT_LOOP
ENDIF
RESTART

@曲データ表示(ARG)
PRINTFORM [ 0]%SONGNAME:(ARG), 24, LEFT%
TRYCALLFORM VO_{ARG}
LOCALS = VO +{RESULT/ 100, 2}
CALL PRINTCOLOR(SET_COLOR_FUNC("ボーカル"), LOCALS)
PRINTFORM  
TRYCALLFORM VI_{ARG}
LOCALS = VI +{RESULT / 100, 2}
CALL PRINTCOLOR(SET_COLOR_FUNC("ビジュアル"), LOCALS)
PRINTFORM  
TRYCALLFORM DA_{ARG}
LOCALS = DA +{RESULT/ 100, 2}
CALL PRINTCOLOR(SET_COLOR_FUNC("ダンス"), LOCALS)
PRINTFORM  
TRYCALLFORM BPM_{ARG}
LOCALS = 　BPM：{RESULT, 3}
CALL PRINTCOLOR(SET_COLOR_FUNC("雪歩"), LOCALS)
PRINTFORM  
TRYCALLFORM 曲タイプ_{ARG}
SELECTCASE RESULT
	CASE 1
		CALL SET_COLOR("ボーカル")
	CASE 2
		CALL SET_COLOR("ビジュアル")
	CASE 3
		CALL SET_COLOR("ダンス")
	CASEELSE
		RESETCOLOR
ENDSELECT
PRINTFORM ★
RESETCOLOR
PRINTL
PRINTL [0] 戻る
$INPUT_LOOP
INPUT
RETURN 0

@売り上げ表示
VARSET LOCAL
DRAWLINE
FOR LOCAL, 1, 300
	SIF SONG:LOCAL:1 > 0
		PRINTFORML %SONGNAME:LOCAL% - %ADD_COMMA(SONG:LOCAL:1)%枚
NEXT
DRAWLINE
PRINTFORML [0]戻る
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN
ELSE
	CALL 再入力
	GOTO INPUT_LOOP
ENDIF
RESTART

@楽曲売上取得(ARG)
#FUNCTION
SIF SONG:ARG:1 <= 0
	RETURNF 0
RETURNF SONG:ARG:1
@楽曲ハイスコア取得(ARG)
#FUNCTION
SIF SONG:ARG:0 <= 0
	RETURNF 0
RETURNF SONG:ARG:0
