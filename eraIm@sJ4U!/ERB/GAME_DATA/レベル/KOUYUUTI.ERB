;基本となる交友値用の関数
;好感度を経験値としてランク決定、大体好感度10万でランク99

;ARG:0 対象のキャラ登録番号
;
;B	交友値
;A	次の交友値に必要な好感度
@GIRLS_POW_EXP(ARG)
;LOCAL:0	好感度一時保存
;LOCAL:1	ループ用
;LOCAL:2	初期交友値

;初期交友値記憶
LOCAL:2 = ABL:ARG:親密

IF ABL:ARG:親密 >= 99
	ABL:ARG:親密 = 99
	RETURN
ENDIF

FOR LOCAL:1, 1, 100
	LOCAL:0 = CFLAG:ARG:2
	B = ABL:ARG:親密
	POWER A, (B + 1), 2
	IF LOCAL:0 >= A * 10
		ABL:ARG:親密 += 1
		TRYCALLFORM FRIEND_LV_UP_{ARG:0}(ABL:ARG:親密, ARG)
		IF MAXBASE:ARG:ボルテージ < 100
			MAXBASE:ARG:ボルテージ += 10
			;BASE:ARG:ボルテージ += 10
		ENDIF
	ENDIF
NEXT
;初期交友値より上がっていればメッセージ表示
IF (ABL:ARG:親密 - LOCAL:2) > 0
	PRINTFORML %CALLNAME:ARG%との仲が深まった気がする…
ENDIF
;アイドルマスター技解放報告
IF ABL:ARG:親密 == 20 && CFLAG:ARG:アイドルマスター技 == 0
	PRINTFORMW 信頼が深まったことで、%CALLNAME:(ARG)%の新たな力が解放された…
	PRINTFORMW <%CALLNAME:(ARG)%は%SONGNAME:(300 + ARG)%を修得した>
	CFLAG:ARG:アイドルマスター技 = (300 + ARG)
ENDIF
