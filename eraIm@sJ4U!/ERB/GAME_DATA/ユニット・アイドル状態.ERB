;ARG,上昇量
;ARG:1,相性補正有
@RELATIONS_UP(ARG,ARG:1)
FOR LOCAL,1080, 1085
	IF FLAG:LOCAL > 0
		FOR LOCAL:1, 1080, 1085
			SIF LOCAL:1 == LOCAL
				CONTINUE
			IF FLAG:(LOCAL:1) > 0
				relations:(FLAG:LOCAL):(FLAG:(LOCAL:1)) += ARG
				IF ARG:1
					IF RELATION:(FLAG:LOCAL):(FLAG:(LOCAL:1)) >= 120
						LOCAL:2 = 1
					ELSEIF RELATION:(FLAG:LOCAL):(FLAG:(LOCAL:1)) <= 80
						LOCAL:2 = -1
					ELSE
						LOCAL:2 = 0
					ENDIF
					relations:(FLAG:LOCAL):(FLAG:(LOCAL:1)) += LOCAL:2
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT

@親愛_UP(ARG,ARG:1)
FOR LOCAL,1080, 1085
	IF ARG:1
		IF FLAG:LOCAL != TARGET || FLAG:LOCAL != ASSI
			SIF !RAND:3
				BASE:(FLAG:LOCAL):親愛度 += ARG
		ENDIF
	ELSE
		SIF FLAG:LOCAL > 0
			BASE:(FLAG:LOCAL):親愛度  += ARG
	ENDIF
NEXT

@自信_UP(ARG)
FOR LOCAL,1080, 1085
	SIF FLAG:LOCAL > 0
		BASE:(FLAG:LOCAL):自信  += ARG
NEXT

@アイドル状態変化
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:勧誘状況 != -1
		CONTINUE
	;別れ中はスキップ
	SIF CFLAG:LOCAL:21
		CONTINUE
	SIF LOCAL == MASTER
		CONTINUE
	CFLAG:LOCAL:アイドル状態 = 0
	CALL 自信チェック(LOCAL)
	CALL ストレスチェック(LOCAL)
	SIF TALENT:LOCAL:覚醒
		CFLAG:LOCAL:アイドル状態 = [[状態:覚醒]]
	SIF TALENT:LOCAL:自信喪失
		CFLAG:LOCAL:アイドル状態 = [[状態:自信喪失]]
	SIF TALENT:LOCAL:無気力
		CFLAG:LOCAL:アイドル状態 = [[状態:無気力]]
	SIF TALENT:LOCAL:病気
		CFLAG:LOCAL:アイドル状態 = [[状態:病気]]
NEXT

;ユニットメンバーだけでok
FOR LOCAL, 1080, 1085
	SIF FLAG:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(FLAG:LOCAL):アイドル状態
		CONTINUE
	SIF CHECK_喧嘩(FLAG:LOCAL)
		CONTINUE
	SIF CHECK_有頂天(FLAG:LOCAL)
		CONTINUE
	SIF CHECK_覚醒(FLAG:LOCAL)
		CONTINUE
	SIF CHECK_かしまし(FLAG:LOCAL)
		CONTINUE
	SIF CHECK_鼻歌_キラキラ_小躍り(FLAG:LOCAL)
		CONTINUE

	CFLAG:(FLAG:LOCAL):アイドル状態 = 0
NEXT

CALL ユニット状態変化


@CHECK_喧嘩(ARG)
#FUNCTION
FOR LOCAL ,1080, 1085
	SIF FLAG:LOCAL <= 0 || FLAG:LOCAL == ARG
		CONTINUE
	SELECTCASE RELATION:ARG:(FLAG:LOCAL)
		CASE IS < 80
			LOCAL:1 = -5
		CASE IS < 100
			LOCAL:1 = -8
		CASE 100
			LOCAL:1 = -10
		CASE IS < 150
			LOCAL:1 = -15
		CASE IS < 200
			LOCAL:1 = -20
		CASEELSE
			LOCAL:1 = -50
	ENDSELECT
	IF relations:ARG:(FLAG:LOCAL) <= LOCAL:1
		CFLAG:ARG:アイドル状態 = [[状態:喧嘩]]
		CFLAG:(FLAG:LOCAL):アイドル状態 = [[状態:喧嘩]]
		RETURNF 1
	ENDIF
NEXT

RETURNF 0

@CHECK_有頂天(ARG)
#FUNCTION
#DIM キャラクター, 5
VARSET LOCAL
SIF BASE:ARG:親愛度 < 5
	RETURNF 0
FOR LOCAL, 1080, 1085
	SIF FLAG:LOCAL <= 0 || FLAG:LOCAL == ARG
		CONTINUE
	IF relations:ARG:(FLAG:LOCAL) < 0 && BASE:(FLAG:LOCAL):親愛度 < 0
		LOCAL:1 += 1
		キャラクター:(LOCAL-1080) = FLAG:LOCAL
	ENDIF
NEXT
IF LOCAL:1 >= 2
	FOR LOCAL, 0, 5
		SIF キャラクター:LOCAL
			CFLAG:(キャラクター:LOCAL):アイドル状態 = [[状態:ドン引き]]
	NEXT
	CFLAG:ARG:アイドル状態 = [[状態:有頂天]]
	RETURNF 1
ENDIF
RETURNF 0

@CHECK_覚醒(ARG)
#FUNCTION
SIF !TALENT:ARG:恋慕 || !CFLAG:ARG:アイドルマスター技 || !FLAG:仕事成功 || TALENT:ARG:覚醒
	RETURNF 0
SIF !RAND:10
	TALENT:ARG:覚醒 = 1
;美希は条件さえ満たしていれば覚醒
SIF ARG == 21
	TALENT:ARG:覚醒 = 1

SIF TALENT:ARG:覚醒
	RETURNF 1
CFLAG:ARG:アイドル状態 = [[状態:覚醒]]
RETURNF 0

@CHECK_かしまし(ARG)
#FUNCTION
FOR LOCAL, 1080, 1085
	SIF FLAG:LOCAL == ARG || FLAG:LOCAL <= 0
		CONTINUE
	SIF relations:ARG:(FLAG:LOCAL) < 10
		RETURNF 0
NEXT
RETURNF 1

@CHECK_鼻歌_キラキラ_小躍り(ARG)
#FUNCTION
FOR LOCAL, 1080, 1085
	SIF FLAG:LOCAL == ARG || FLAG:LOCAL <= 0
		CONTINUE
	SIF relations:ARG:(FLAG:LOCAL) < 5
		RETURNF 0
NEXT

MAX BASE:ARG:VO倍率, BASE:ARG:VI倍率, BASE:ARG:DA倍率
SIF RESULT < 200
	RETURNF 0
SIF RESULT == BASE:ARG:VO倍率
	CFLAG:ARG:アイドル状態 = [[状態:鼻歌]]
SIF RESULT == BASE:ARG:VI倍率
	CFLAG:ARG:アイドル状態 = [[状態:キラキラ]]
SIF RESULT == BASE:ARG:DA倍率
	CFLAG:ARG:アイドル状態 = [[状態:小躍り]]
RETURNF 1

@自信チェック(ARG)
;メンバーでない時
IF MATCH(FLAG,ARG,1080,1085)

ELSE
	SIF !CFLAG:ARG:待機中行動
		BASE:ARG:自信 --
ENDIF
SIF TALENT:ARG:自信喪失
	BASE:ARG:自信--
CALL CHECK_自信喪失(ARG)
CALL CHECK_無気力(ARG)
RETURN 0

@CHECK_無気力(ARG)
SIF BASE:ARG:自信 > -10 || !TALENT:ARG:自信喪失
	RETURN 0
SIF RAND:2
	TALENT:ARG:無気力 = 1
CFLAG:ARG:アイドル状態 = [[状態:無気力]]
SIF TALENT:ARG:無気力
	RETURN 1
	
RETURN 0

@CHECK_自信喪失(ARG)
SIF TALENT:ARG:自信喪失 || BASE:ARG:自信 > -5
	RETURN 0
TALENT:ARG:自信喪失 = 1
CFLAG:ARG:アイドル状態 = [[状態:自信喪失]]
RETURN 0

@ユニット状態変化
FLAG:ユニット状態 = 0
SIF CMATCH(CFLAG:アイドル状態,9,FLAG:1080,FLAG:1085) == メンバー数()
	FLAG:ユニット状態 = 1
SIF CMATCH(CFLAG:アイドル状態,6,FLAG:1080,FLAG:1085) == メンバー数()
	FLAG:ユニット状態 = 3
SIF CMATCH(CFLAG:アイドル状態,7,FLAG:1080,FLAG:1085) == メンバー数()
	FLAG:ユニット状態 = 4
SIF CMATCH(CFLAG:アイドル状態,8,FLAG:1080,FLAG:1085) == メンバー数()
	FLAG:ユニット状態 = 5
SIF FLAG:ユニット状態
	RETURN
FOR LOCAL, 1080, 1085
	SIF FLAG:LOCAL <= 0
		CONTINUE
	SELECTCASE CFLAG:(FLAG:LOCAL):アイドル状態
		CASE 1,2,3,4,10,11
			LOCAL:1++
	ENDSELECT
NEXT
SIF LOCAL:1 == メンバー数()
	FLAG:ユニット状態 = 2

RETURN 0

@PRINT_ユニット状態
#FUNCTIONS
SELECTCASE FLAG:ユニット状態
	CASE 0
		LOCALS = 普通
	CASE 1
		LOCALS = 大団円
	CASE 2
		LOCALS = ギスギス
	CASE 3
		LOCALS = 歌聖
	CASE 4
		LOCALS = カガヤキ
	CASE 5
		LOCALS = 踊り子
	CASE 6
		LOCALS = ドン引き
	CASEELSE
		LOCALS = エラー
ENDSELECT
RETURNF LOCALS

@PRINT_アイドル状態(ARG)
#FUNCTIONS
SELECTCASE CFLAG:ARG:アイドル状態
	CASE 0
		LOCALS = 普通
	CASE 1
		LOCALS = 有頂天
	CASE 2
		LOCALS = ドン引き
	CASE 3
		LOCALS = 自信喪失
	CASE 4
		LOCALS = 無気力
	CASE 5
		LOCALS = 喧嘩
	CASE 6
		LOCALS = 鼻歌
	CASE 7
		LOCALS = キラキラ
	CASE 8
		LOCALS = 小躍り
	CASE 9
		LOCALS = かしまし
	CASE 10
		LOCALS = 体調不良
	CASE 11
		LOCALS = 病気
	CASE 12
		LOCALS = 覚醒
	CASE 13
		LOCALS = えらます
	CASEELSE
		LOCALS = エラー
ENDSELECT
RETURNF LOCALS
