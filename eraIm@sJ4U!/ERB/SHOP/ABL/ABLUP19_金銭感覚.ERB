;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/金銭感覚upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;金銭感覚up本体
;-------------------------------------------------
@ABLUP19MAIN
DRAWLINE
IF ABL:金銭感覚 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP19

PRINTFORM [0] - %PALAMNAME:16%の珠×{A}以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
ENDIF
PRINTL 

PRINTFORM [1] - %PALAMNAME:7%の珠×{C}以上……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:金銭感覚 += 1

IF RESULT == 0
	JUEL:16 -= A
ELSEIF RESULT == 1
	JUEL:7 -= C
ENDIF

PRINTS ABLNAME:19
PRINT のレベルが
PRINTV ABL:金銭感覚
PRINTW になりました。



;-------------------------------------------------
;金銭感覚が上げられるかチェック
;-------------------------------------------------
@ISABLUP19
;上限越してるならリターン
IF ABL:金銭感覚 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP19

;条件別にＯＫかダメかを記録する
I = 0
J = 0

;能力アップ可能なら1にする
Z = 0

;生活の珠で上げる
SIF JUEL:16 < A
	I |= 1

;習得の珠で上げる
SIF JUEL:7 < C
	J |= 1

IF I == 0
	Z = 1
ENDIF

IF J == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;金銭感覚を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP19
IF ABL:金銭感覚 == 0
	A = 1
	B = 0
	C = 75
ELSEIF ABL:金銭感覚 == 1
	A = 50
	B = 0
	C = 500
ELSEIF ABL:金銭感覚 == 2
	A = 600
	C = 3000
ELSEIF ABL:金銭感覚 == 3
	A = 5000
	C = 9000
ELSEIF ABL:金銭感覚 == 4
	A = 20000
	C = 24000
ELSE
	A = 20000
	C = 24000
	;EASYモードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:金銭感覚 - 3)
		C = C * (ABL:金銭感覚 - 3)
	ELSE
		A = A * (ABL:金銭感覚 - 3) * (ABL:金銭感覚 - 3)
		C = C * (ABL:金銭感覚 - 3) * (ABL:金銭感覚 - 3)
	ENDIF
ENDIF

;自制心
IF TALENT:20
	TIMES A , 0.80
	TIMES C , 0.80
ENDIF

;恋慕
IF TALENT:85
	TIMES A , 0.80
	TIMES C , 0.80
ENDIF
