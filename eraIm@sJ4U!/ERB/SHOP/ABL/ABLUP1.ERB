;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/欲望upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;欲望up本体
;-------------------------------------------------
@ABLUP1MAIN
DRAWLINE
IF ABL:1 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP1

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF C
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF

PRINTFORM [0] - %PALAMNAME:5%の珠×{A}……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

IF B > 0
	PRINTFORM [1] - %PALAMNAME:17%の珠×{B}……
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:1 += 1

IF RESULT == 0
	JUEL:5 -= A
ELSEIF RESULT == 1
	JUEL:17 -= B
ENDIF

PRINTS ABLNAME:1
PRINT のレベルが
PRINTV ABL:1
PRINTW になりました。

CALL YOKUBO_UP_CHECK




;-------------------------------------------------
;欲望が上げられるかチェック
;-------------------------------------------------
@ISABLUP1
IF ABL:1 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF
;能力アップに必要なコストを計算
CALL COSTABLUP1

;条件別にＯＫかダメかを記録する
I = 0
J = 0
;能力アップ可能なら1にする
Z = 0

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF C && EXP:50 < 5
	;異常経験がない
	I |= 2
	J |= 2
ENDIF

;欲情の珠で上げる
SIF JUEL:5 < A
	I |= 1

IF I == 0
	Z = 1
ENDIF

IF B > 0
	;優越の珠で上げる
	SIF JUEL:17 < B
		J |= 1

	IF J == 0
		Z = 1
	ENDIF
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;親密を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP1
IF ABL:1 <= 1
	A = 5
	B = 3
	A = A * (ABL:1 + 1)
	B = B * (ABL:1 + 1)
ELSEIF ABL:1 <= 3
	A = 50
	B = 30
	A = A * (ABL:1 - 1)
	B = B * (ABL:1 - 1)
ELSEIF ABL:1 <= 5
	A = 1000
	B = 700
	A = A * (ABL:1 - 3)
	B = B * (ABL:1 - 3)
ELSEIF ABL:1 <= 8
	A = 8000
	B = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 1.10
	ENDIF
	A = A * (ABL:1 - 5)
ELSEIF ABL:1 <= 11
	A = 24000
	B = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 1.30
	ENDIF
	A = A + 8000 * (ABL:1 - 9)
ELSE
	A = 24000
	B = 0

	;EASYモードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:1 - 9)
		;一線越えない
		IF TALENT:27
			TIMES A , 1.30
		ENDIF
	ELSE
		A = A * (ABL:1 - 10) * (ABL:1 - 10)
		;一線越えない
		IF TALENT:27
			TIMES A , 1.30
		ENDIF
	ENDIF
ENDIF

;淫乱化
IF TALENT:76
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF ABL:1 == 12 && TALENT:70 == 0 && TALENT:76 == 0
	C = 1
ELSE
	C = 0
ENDIF

