;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/A感覚upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;A感覚up本体
;-------------------------------------------------
@ABLUP5MAIN
DRAWLINE
IF ABL:5 >= 99
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP5

PRINTFORM [0] - %PALAMNAME:2%の珠×{A}……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:5 += 1

IF RESULT == 0
	JUEL:2 -= A
ENDIF

PRINTS ABLNAME:5
PRINT のレベルが
PRINTV ABL:5
PRINTW になりました。



;-------------------------------------------------
;A感覚が上げられるかチェック
;-------------------------------------------------
@ISABLUP5
IF ABL:5 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF
;能力アップに必要なコストを計算
CALL COSTABLUP5

;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

;快Ａの珠で上げる
SIF JUEL:2 < A
	I |= 1

IF I == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;A感覚を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP5
IF ABL:5 == 0
	A = 1
ELSEIF ABL:5 == 1
	A = 50
ELSEIF ABL:5 == 2
	A = 600
ELSEIF ABL:5 == 3
	A = 7000
	;一線越えない
	IF TALENT:27
		TIMES A , 1.10
	ENDIF
ELSEIF ABL:5 == 4
	A = 45000
	;一線越えない
	IF TALENT:27
		TIMES A , 1.30
	ENDIF
ELSE
	A = 45000
	;EASYモードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:5 - 3)
		;一線越えない
		IF TALENT:27
			TIMES A , 1.30
		ENDIF
	ELSE
		A = A * (ABL:5 - 3) * (ABL:5 - 3)
		;一線越えない
		IF TALENT:27
			TIMES A , 1.30
		ENDIF
	ENDIF
ENDIF

;A鈍感
IF TALENT:105
	TIMES A , 1.20
ENDIF

;A敏感
IF TALENT:106
	TIMES A , 0.80
ENDIF

;淫乱化
IF TALENT:76
	TIMES A , 0.80
ENDIF

;尻穴狂い
IF TALENT:77
	TIMES A , 0.80
ENDIF
