;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/歌唱技能upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;歌唱技能up本体
;-------------------------------------------------
@ABLUP45MAIN
DRAWLINE
IF ABL:Ｖｏ技能 >= 15
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP45

PRINTFORM [0] -%PALAMNAME:105%の珠×{C}、
;IF ABL:Ｖｏ技能 >= 2
	PRINTFORM %EXPNAME:93%{B}
;ENDIF
PRINT 以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:Ｖｏ技能 += 1

IF RESULT == 0
	JUEL:105 -= C
ENDIF

PRINTS ABLNAME:45
PRINT のレベルが
PRINTV ABL:Ｖｏ技能
PRINTW になりました。



;-------------------------------------------------
;歌唱技能が上げられるかチェック
;-------------------------------------------------
@ISABLUP45
IF ABL:Ｖｏ技能 >= 99
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP45

;条件別にＯＫかダメかを記録する
I = 0

;能力アップ可能なら1にする
Z = 0

;習得の珠で上げる
;ここミスってるかも
;歌唱の珠で上げる
SIF JUEL:105 < C
	I |= 1
SIF EXP:歌唱経験 < B
	I |= 2
IF I == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;歌唱技能を上げるのに必要なコストを計算
;-------------------------------------------------
;A	習得の珠
;B	歌唱経験
;C	歌唱の珠
@COSTABLUP45
IF ABL:Ｖｏ技能 == 0
	A = 1
	B = 1
	C = 1
ELSEIF ABL:Ｖｏ技能 <= 1
	A = 10
	B = 10
	C = 10
	A = A * (ABL:Ｖｏ技能 + 1)
	B = B * (ABL:Ｖｏ技能 + 1)
	C = C * (ABL:Ｖｏ技能 + 1)
ELSEIF ABL:Ｖｏ技能 <= 3
	A = 25
	B = 25
	C = 25
	SIF EXP:歌唱経験 >= EXPLV:3
		A = 20
	SIF EXP:歌唱経験 >= EXPLV:3
		C = 20
	A = A * (ABL:Ｖｏ技能 - 1)
	B = B * (ABL:Ｖｏ技能 - 1)
	C = C * (ABL:Ｖｏ技能 - 1)
ELSEIF ABL:Ｖｏ技能 <= 5
	A = 200
	B = 40
	C = 200
	SIF EXP:歌唱経験 >= EXPLV:4
		A = 180
	SIF EXP:歌唱経験 >= EXPLV:4
		C = 180
	A = A * (ABL:Ｖｏ技能 - 3)
	B = B * (ABL:Ｖｏ技能 - 3)
	C = C * (ABL:Ｖｏ技能 - 3)
ELSEIF ABL:Ｖｏ技能 <= 8
	A = 3000
	B = 80
	C = 3000
	SIF EXP:歌唱経験 >= EXPLV:5
		A = 2500
	SIF EXP:歌唱経験 >= EXPLV:5
		C = 2500
	A = A * (ABL:Ｖｏ技能 - 5)
	C = C * (ABL:Ｖｏ技能 - 5)
ELSEIF ABL:Ｖｏ技能 <= 11
	A = 20000
	B = 100
	C = 20000
	SIF EXP:歌唱経験 >= EXPLV:5
		A = 15000
	SIF EXP:歌唱経験 >= EXPLV:5
		C = 15000
	A = A + 8000 * (ABL:Ｖｏ技能 - 9)
	C = C + 8000 * (ABL:Ｖｏ技能 - 9)
ELSE
	A = 20000
	B = 120
	C = 20000
	;初月モードなら軽減
	IF ITEM:99 == 1
		A = A * (ABL:Ｖｏ技能 - 3)
		B = B * (ABL:Ｖｏ技能 - 2) / 2
		C = C * (ABL:Ｖｏ技能 - 3)
	ELSE
		A = A * (ABL:Ｖｏ技能 - 3) * (ABL:Ｖｏ技能 - 3)
		B = B * (ABL:Ｖｏ技能 - 3)
		C = C * (ABL:Ｖｏ技能 - 3) * (ABL:Ｖｏ技能 - 3)
	ENDIF
ENDIF

IF ABL:7 == 1
	TIMES A , 0.90
	TIMES B , 1.00
	TIMES C , 0.90
ELSEIF ABL:7 == 2
	TIMES A , 0.80
	TIMES B , 0.90
	TIMES C , 0.80
ELSEIF ABL:7 == 3
	TIMES A , 0.75
	TIMES B , 0.80
	TIMES A , 0.75
ELSEIF ABL:7 == 4
	TIMES A , 0.70
	TIMES B , 0.70
	TIMES C , 0.70
ELSEIF ABL:7 >= 5
	TIMES A , 0.60
	TIMES B , 0.66
	TIMES C , 0.60
ENDIF

;臆病
IF TALENT:10
	TIMES A , 1.10
	TIMES B , 1.20
	TIMES C , 1.10
ENDIF

;目立ちたがり
IF TALENT:28
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES C , 0.90
ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
	TIMES B , 0.75
	TIMES C , 0.80
ENDIF

;習得遅い
IF TALENT:51
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.20
ENDIF


;歌上手
IF ABL:歌唱適性 >= 120
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

;歌苦手
IF ABL:歌唱適性 <= 90
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;音感
IF TALENT:音感
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF
;音痴
IF TALENT:音痴
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
