[SKIPSTART]
勧誘可能なキャラ数はＰランクが上昇するか施設建設で増加
[SKIPEND]
@CHARA_SCOUT
;SIF MATCH(CHARALIST,0,0,5) == 5
;	CALL GET_CHARALIST
DO
	LOCAL:3 = 0
	REDRAW 0
	CALL SAVELINE
	CALL SHOW_MENU_勧誘
	INPUT
	IF RESULT == 100
		BREAK
	ELSE
		LOCAL:2 = RESULT
		IF !MATCH(CHARALIST,RESULT,0,5)
			CALL RETURNLINE
			CONTINUE
		ENDIF
		DO
			CALL CHARA_MANUAL(LOCAL:2 + 89)
			DRAWLINE
			PRINTFORML このキャラクターを勧誘しますか？(%ADD_COMMA(契約金(LOCAL:2))%Ｇが必要です)
			PRINTFORML [0] はい
			PRINTFORML [1] いいえ
			PRINTFORMLC [2]外見を見る
			PRINTFORMLC [3]能力適正を見る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						IF MONEY < 契約金(LOCAL:2)
							REUSELASTLINE ゲイツが足りません……
							CONTINUE
						ELSE
							MONEY -= 契約金(LOCAL:2)
							CALL NEWCHARA_INIT(LOCAL:2)
							PRINTFORMW %CALLNAME:(LOCAL:2)%を勧誘しました！
							FOR LOCAL, 0, 5
								SIF CHARALIST:LOCAL == LOCAL:2
									CHARALIST:LOCAL = 0
							NEXT
							LOCAL:3 = 1
							BREAK
						ENDIF
					CASE 1
						LOCAL:3 = 1
						BREAK
					CASE 2
						CALL SHOW_CHARA_LOOKS(LOCAL:2)
						WAIT
						BREAK
					CASE 3
						CALL SHOW_CHARA_DUNAMIS(LOCAL:2)
						WAIT
						BREAK
				ENDSELECT
			LOOP 1
		LOOP (!LOCAL:3)
	ENDIF
	CALL RETURNLINE
LOOP 1
CALL RETURNLINE
RETURN 0

;勧誘可能なキャラを取得し、CHARALISTに入れて返す
;LOCAL;0　ループ用
;LOCAL:1　CHARALISTの配列用
;LOCAL:2　TEMP
;LOCAL:3　TEMP
@GET_CHARALIST(ARG)
#DIM LTEMP, 200
VARSET LOCAL
VARSET LOCALS
LOCALS = /

;数値エラー
SIF ARG <= 0
	RETURN
;勧誘可能なキャラがいない
SIF GET_SCOUTABLE_NUMBER() <= 0
	RETURN	
;勧誘可能なキャラがARGより少ない
SIF GET_SCOUTABLE_NUMBER() < ARG
	ARG = GET_SCOUTABLE_NUMBER()

;勧誘候補をLTEMPに入れる
FOR LOCAL, 11, (11+FLAG:キャラ数)
	IF IS_SCOUTABLE(LOCAL)
		LTEMP:(LOCAL:1) = LOCAL
		LOCAL:1++
	ENDIF
NEXT

LOCAL:1 = 0
LOCAL:3 = ARG
;ARGに指定された人数のキャラをCHARALISTに入れる
WHILE (1)

	LOCAL:2 = RAND:(GET_SCOUTABLE_NUMBER())
	
	;同じ要素が見つかった
	IF STRCOUNT(LOCALS, @"/{LTEMP:(LOCAL:2)}/") > 0
		CONTINUE
	ELSE
		CHARALIST:(LOCAL:1) = LTEMP:(LOCAL:2);見つからなかったら値を代入
		LOCAL:1++
		LOCAL:3--
		LOCALS += @"{LTEMP:(LOCAL:2)}/"
	ENDIF
	
	;指定数要素入れたらBREAK
	SIF LOCAL:3 <= 0
		BREAK
WEND

;最後にキャラ番号でソート
ARRAYSORT CHARALIST, FORWARD, 0, ARG
RETURN
;IF MATCH(CHARALIST,0,0,5) == 5
;
;ELSE
;	RETURN 0
;ENDIF
;DO
;	CHARALIST:LOCAL = 11+RAND:(FLAG:キャラ数)
;	SIF CFLAG:(CHARALIST:LOCAL):勧誘状況 == -1
;		CONTINUE
;	LOCAL++
;LOOP (CHARALIST:4 <= 0)
;番号順にソート
;FOR LOCAL, 0, 5
;	FOR LOCAL:1, 4, LOCAL, -1
;		SIF CHARALIST:(LOCAL:1 - 1) > CHARALIST:(LOCAL:1)
;			SWAP CHARALIST:(LOCAL:1 - 1), CHARALIST:(LOCAL:1)
;	NEXT
;NEXT

;勧誘可能なキャラの総数を返す
@GET_SCOUTABLE_NUMBER
#FUNCTION
VARSET LOCAL

FOR LOCAL, 11, (FLAG:キャラ数 + 11)
	SIF IS_SCOUTABLE(LOCAL)
		LOCAL:1++
NEXT
RETURNF LOCAL:1

;ARGがスカウト可能か返す
@IS_SCOUTABLE(ARG)
#FUNCTION
SIF ARG < 11
	RETURNF 0
SIF CFLAG:ARG:勧誘状況 == -1
	RETURNF 0
SIF CFLAG:ARG:パートナー選択可 < 0
	RETURNF 0
RETURNF 1

@SHOW_MENU_勧誘
PRINTL 
DRAWLINE
PRINTL ★★勧誘したいキャラを選択してください★★  ( )内は契約金
FOR LOCAL, 0, 5
	IF CHARALIST:LOCAL
		PRINTFORM [{CHARALIST:LOCAL,3}] 
		RESULT = 0
		TRYCALLFORM KOJO_EXIST_K{CHARALIST:LOCAL}
		SIF RESULT
			CALL SET_COLOR(CHARALIST:LOCAL)
		PRINTFORM %CALLNAME:(CHARALIST:LOCAL), 10, LEFT%
		RESETCOLOR
		PRINTFORML (%ADD_COMMA(契約金(CHARALIST:LOCAL)), 7%Ｇ)
	ENDIF
NEXT
PRINTL
PRINTFORML [100] 戻る
