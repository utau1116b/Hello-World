;三ヶ月分と言う事で
@EVENTTURNEND
;●友情パッチ用改変
SIF (FLAG:62 & 8192) == 0 || IS_友情(TARGET)
	RETURN 0
;●ここまで
;エンゲージリングを渡していたら、関連フラグをリセット
IF FLAG:29
	REPEAT CHARANUM
		CFLAG:COUNT:27 = 0
		CFLAG:COUNT:28 = 0
	REND
;そうでなければ、貯金フラグの処理
ELSE
	REPEAT CHARANUM
		;マスターならスキップ
		SIF COUNT == MASTER
			CONTINUE
		;既に首輪など贈ってるので無理
		IF TALENT:COUNT:180
			CFLAG:COUNT:27 = 0
			CFLAG:COUNT:28 = 0
			CONTINUE
		ENDIF
		;霧に飲まれかけていたらリセット
		IF CFLAG:COUNT:17
			CFLAG:COUNT:27 = 0
			CONTINUE
		ENDIF
		;所持金のカンスト
		;今のところ100万までしか見ないけど、1000万まで確保
		SIF CFLAG:COUNT:28 > 10000000
			CFLAG:COUNT:28 = 9999999
		;[親愛]＋[恋人]＋好感度30k＋反感0
		IF TALENT:COUNT:88 && TALENT:COUNT:153 && CFLAG:COUNT:2 >= 30000 && MARK:COUNT:3 == 0
			;目標達成
			IF CFLAG:COUNT:28 >= 1000000
				;達成してからの日にちをカウント
				;放置していると殴り込み
				CFLAG:COUNT:27 += 1
				CONTINUE
			;未達成なら貯金フラグ
			ELSE
				CFLAG:COUNT:27 = 1
			ENDIF
		;条件を満たさなければリセット
		ELSE
			CFLAG:COUNT:27 = 0
			CONTINUE
		ENDIF
	REND
ENDIF

;時間(気力)が余ったら金稼ぎ
@EARN_MONEY_TM(ARG)
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;気力0ならリターン
SIF BASE:ARG:1 < 0
	RETURN 0
;貯金フラグ立って無くても一定確率で金稼ぎ
;その際[恋人]だと確率上昇
;●友情パッチ
;IF 【変更予定：恋人】
IF TALENT:ARG:恋人
	A = 1
ELSE
	A = 0
ENDIF
SIF CFLAG:ARG:27 == 0 && RAND:3 > A
	RETURN 0

P = (ABL:ARG:2 * 2 + 15) * (RAND:5 + 8)
A = RAND:100
IF A <= 10
	P *= 60
ELSEIF A <= 40
	P *= 90
ELSEIF A <= 70
	P *= 110
ELSEIF A <= 97
	P *= 140
ELSE
	P *= 400
ENDIF

P *= BASE:ARG:1 / MAXBASE:ARG:1
CFLAG:ARG:28 += P

;回復処理の前
;使用不可：A-F
;ARG	対象
@TURNEND_EARN_MONEY_TM(ARG)
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;貯金フラグが立ってないならリターン
SIF CFLAG:ARG:27 == 0
	RETURN 0
;妖怪退治のコピー
IF BASE:ARG:HP >= 500
	EXP:ARG:95 += RAND:5 + 5
	CFLAG:ARG:2 -= RAND:10 + 6
	BASE:ARG:HP -= RAND:11 * 30 + 200
	T = ARG
	Z = ARG
	;CALL GET_BATTLE_POWER
	;COUNT = Z
	
	;回復量は1/10
	A /= 10
	B /= 10
	
	G = P / 10 * (RAND:10 + 5)
	
	;稼ぎが高くなり過ぎないように補正をかける
	;7500を超えた分は25%OFF
	SIF G >= 7500
		G = 7500 + (G - 7500) / 4 * 3
	;補正後、15000を超えていたらその分を更に50%OFF
	SIF G >= 15000
		G = 15000 + (G - 15000) / 2
	;補正後、30000を超えていたらその分を更に75%OFF
	SIF G >= 30000
		G = 30000 + (G - 30000) / 4
	;補正後、50000を超えていたらその分を更に90%OFF
	SIF G >= 50000
		G = 50000 + (G - 50000) / 10
	
	;別れ中でないなら稼ぎは八分の一
	SIF CFLAG:ARG:21 == 0
		G /= 8
	;稼ぎを所持金へ
	CFLAG:ARG:28 += G
ENDIF

;リングプレゼント
;開始時の口上の“後”に呼ばれます
;呼ぶタイミングは朝イベント後とかに動かすかも
@EVENTTRAIN
#LATER
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;SIF (TALENT:88 && 【変更予定：恋人】 && CFLAG:2 >= 30000 && MARK:3 == 0) == 0
SIF (TALENT:88 && TALENT:恋人 && CFLAG:2 >= 30000 && MARK:3 == 0) == 0
	RETURN 0
SIF CFLAG:28 < 1000000
	RETURN 0
SIF FLAG:29
	RETURN 0

DRAWLINE
;カットフラグを退避
;!;LOCAL:100 = CFLAG:99

;口上開始前の地の文をカットするか
TFLAG:13 = 95
CALL SELF_MAEKOJO
;地の文カット
IF (CFLAG:99 & 1) == 0
	PRINTFORMW %NAME:TARGET%は%NAME:MASTER%に渡したいものがあるらしい…
	PRINTFORMW %NAME:MASTER%が%NAME:TARGET%に近づくと、%NAME:TARGET%は%NAME:MASTER%の左手を引き寄せ、その薬指に指輪をはめた
ENDIF

;TFLAG:13 = 95
CALL SELF_KOJO

CALL RING_SET

;カットフラグを戻す
;!;CFLAG:99 = LOCAL:100

;リング持って殴り込み
@MORNING_RING
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;SIF (TALENT:88 && 【変更予定：恋人】 && CFLAG:2 >= 30000 && MARK:3 == 0) == 0
SIF (TALENT:88 && TALENT:恋人 && CFLAG:2 >= 30000 && MARK:3 == 0) == 0
	RETURN 0
SIF CFLAG:28 < 1000000
	RETURN 0
SIF FLAG:29
	RETURN 0
;カットフラグを退避
Z:302 = CFLAG:99
;!;LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
TFLAG:13 = 96
CALL SELF_MAEKOJO
;地の文カット
IF (CFLAG:99 & 1) == 0
	PRINTFORM 朝、%NAME:MASTER%のもとへ
	SIF CFLAG:21
		PRINT 別れていた
	PRINTFORMW %NAME:TARGET%が訪ねてきた
	PRINTFORMW %NAME:TARGET%は渡したいものが有ると言い、%NAME:MASTER%の腕を取った
	PRINTFORMW そして、%NAME:MASTER%の左手を引き寄せ、その薬指に指輪をはめた
ENDIF

;TFLAG:13 = 96
CALL SELF_KOJO

CFLAG:TARGET:21 = 0

CALL RING_SET

;カットフラグを戻す
CFLAG:99 = Z:302
;!;CFLAG:99 = LOCAL:100

;リングイベント共通部分
@RING_SET
IF (CFLAG:99 & 1) == 0
	;散々焦らした挙げ句断るなんて外道はJには居ねぇよな？
	PRINTFORMW %NAME:MASTER%が%NAME:TARGET%の気持ちを受け入れる旨を伝えると、感極まった様子で%NAME:TARGET%が抱きついてきた
	IF TALENT:MASTER:221 == 0
		PRINTFORMW %NAME:MASTER%は%NAME:TARGET%の[%TALENTNAME:221%]になった
	ENDIF
	IF TALENT:27
		PRINTFORMW その顔は今までに無く幸福に満ち、すべてを吹っ切ったように見えた
		PRINTFORMW %NAME:TARGET%は[%TALENTNAME:27%]を失った
	ENDIF
	PRINTFORMW そのまま二人は、長い間抱擁を交し合っていた…
	IF TALENT:89 == 0
		PRINTFORMW %NAME:TARGET%は[%TALENTNAME:89%]を得た
	ENDIF
ENDIF

TALENT:MASTER:221 = NO:TARGET
TALENT:27 = 0
TALENT:89 = 1
;売却不可フラグ
CFLAG:7 = 1
FLAG:29 = NO:TARGET
IF FLAG:5 == 9 || FLAG:5 == 10 || FLAG:24 > 6
	;結婚記念日フラグ
	TIME:9 = DAY:1
	TIME:10 = DAY:2
ENDIF
WAIT

;殴り込み判定
@EVENT_RING
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;判定値保存用配列の初期化
REPEAT 999
	Z:COUNT = 0
REND
;判定値取得
REPEAT CHARANUM
	;念のため添え字限界
	SIF COUNT >= 999
		BREAK
	CALL CHECK_RING
REND

;目標値は100
Y = 99
T = -1
REPEAT CHARANUM
	;念のため添え字限界
	SIF COUNT >= 999
		BREAK
	;目標値を超える範囲で、一番高い実行値を持つキャラを選ぶ
	IF Z:COUNT > Y
		Y = Z:COUNT
		T = COUNT
	ENDIF
REND

;Tが初期値のまま→目標値に達したキャラが居ない
SIF T == -1
	RETURN 0

;ターゲット待避
Y = TARGET
TARGET = T
CALL MORNING_RING
TARGET = Y

@CHECK_RING
;目標達成したか
IF CFLAG:COUNT:28 < 1000000 && CFLAG:COUNT:27
	Z:COUNT = -1
	RETURN 0
ENDIF

;目標達成からの日数
Y = CFLAG:COUNT:27 * CFLAG:COUNT:27

;親密
Y += ABL:COUNT:0 * 5

;臆病
SIF TALENT:COUNT:10
	Y -= 20
;反抗的
SIF TALENT:COUNT:11
	Y += 5
;気丈
SIF TALENT:COUNT:12
	Y -= 5
;大人しい
SIF TALENT:COUNT:14
	Y -= 20
;プライド高い
SIF TALENT:COUNT:15
	Y += CFLAG:COUNT:27 * 5
;生意気
SIF TALENT:COUNT:16
	Y += 10
;自制心
SIF TALENT:COUNT:20
	Y -= 15
;無関心
SIF TALENT:COUNT:21
	Y -= 30
;保守的
SIF TALENT:COUNT:24
	Y -= 20
;楽観的
SIF TALENT:COUNT:25
	Y -= 5
;悲観的
SIF TALENT:COUNT:26
	Y -= 10
;一線越えない
SIF TALENT:COUNT:27
	Y -= 30
;目立ちたがり
SIF TALENT:COUNT:28
	Y += 20
;抑圧
SIF TALENT:COUNT:32
	Y -= 10
;解放
SIF TALENT:COUNT:33
	Y += 5
;恥じらい
SIF TALENT:COUNT:35
	Y -= 10
;恥薄い
SIF TALENT:COUNT:36
	Y += 10
;倒錯的
SIF TALENT:COUNT:80 && TALENT:COUNT:122 == 0
	Y += 10
;男嫌い
SIF TALENT:COUNT:82 && TALENT:MASTER:122
	Y -= 10
;サド
SIF TALENT:COUNT:83
	Y += 5
;小悪魔
SIF TALENT:COUNT:87
	Y += 5
;奴隷と主人
SIF TALENT:COUNT:220 && TALENT:MASTER:219
	Y += 30

;主導権基準値
T = COUNT
CALL GET_INITIATIVE_RATE
Y += R / 5

;別れ中
SIF CFLAG:COUNT:21
	Y -= 20

Z:COUNT = Y
