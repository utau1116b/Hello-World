;キャラのステータス再計算処理
;ARG:0	計算対象	0：全キャラ再計算(ちょっと重いので注意)　1：PTメンバーのみ再計算　2：敵のみ再計算
@STATUS_RENEW(ARG:0)
VARSET LOCAL
IF ARG:0 == 0
	;全員再計算 CALL先でループがあるのでカウンタにはLOCAL:0を利用
	FOR LOCAL:0, 11, FLAG:キャラ数 + 11
		CALL STATUS_RENEW_2(LOCAL:0)
	NEXT
	FOR LOCAL:0, 1080, 1085
		CALL STATUS_RENEW_2(FLAG:(LOCAL:0))
	NEXT
ELSEIF ARG:0 == 1
	;PTメンバーのみ再計算
	FOR LOCAL:0, 1080, 1085
		IF FLAG:(LOCAL:0) >= 0
			CALL STATUS_RENEW_2(FLAG:(LOCAL:0))
		ENDIF
	NEXT
ELSE
	;敵のみ再計算
	FOR LOCAL:0, 1, 11
		IF TFLAG:(LOCAL:0 + 1000) > 0
			CALL STATUS_RENEW_2(LOCAL:0)
		ENDIF
	NEXT
ENDIF

RETURN


;キャラのステータス再計算処理(個別)
;ARG:0	対象キャラ登録番号
@STATUS_RENEW_2(ARG:0)
VARSET LOCAL
;装備情報更新
CALL EQUIP_RENEW(ARG:0)

;---------------------------
;基礎能力計算
;---------------------------
FOR LOCAL:10, 0, 7
	LOCAL:(LOCAL:10) = BASE:ARG:(LOCAL:10 + 31)
NEXT
LOCAL:13 = BASE:ARG:VO倍率
LOCAL:14 = BASE:ARG:VI倍率
LOCAL:15 = BASE:ARG:DA倍率
;味方なら装備補正反映
IF ARG:0 >= 11
	;攻撃力
	LOCAL:0 += BASE:ARG:POW総計
	;防御力
	LOCAL:1 += BASE:ARG:GUA総計
	;技術
	LOCAL:2 += BASE:ARG:DEX総計
	;速度
	LOCAL:4 += BASE:ARG:SPD総計
	;運
	LOCAL:5 += BASE:ARG:LUC総計
	;VO
	LOCAL:13 += BASE:ARG:VO総計
	;VI
	LOCAL:14 += BASE:ARG:VI総計
	;DA
	LOCAL:15 += BASE:ARG:DA総計
ENDIF
;味方なら曲補正反映
IF ARG:0 >= 11
	;VO
	TRYCALLFORM VO_{FLAG:リリース中楽曲}
	LOCAL:13 += RESULT / 100
	;VI
	TRYCALLFORM VI_{FLAG:リリース中楽曲}
	LOCAL:14 += RESULT / 100
	;DA
	TRYCALLFORM DA_{FLAG:リリース中楽曲}
	LOCAL:15 += RESULT / 100
ENDIF
FOR LOCAL:10, 0, 7
	;一時強化反映
	IF (CFLAG:ARG:(LOCAL:10 + 1132) / 1000) > 0
		;効果が残っていれば反映する
		LOCAL:(LOCAL:10) = LOCAL:(LOCAL:10) * (CFLAG:ARG:(LOCAL:10 + 1132) % 1000) / 100
	ENDIF
	
	;EX化補正
	LOCAL:(LOCAL:10) = LOCAL:(LOCAL:10) * (CFLAG:ARG:EX状態 + 100) / 100
	;変身補正
	LOCAL:(LOCAL:10) = LOCAL:(LOCAL:10) * (CFLAG:ARG:変身状態 + 100) / 100
NEXT
;レベル変化反映
;暫定的に1レベルにつき能力1＋
IF CFLAG:ARG:変化レベル
	FOR LOCAL:10, 0, 7
		LOCAL:(LOCAL:10) += ((CFLAG:ARG:変化レベル % 1000) - 100)
	NEXT
ENDIF
;加算系強化反映
SIF (CFLAG:ARG:変化加算POW / 1000) > 0
	LOCAL:0 += ((CFLAG:ARG:変化加算POW % 1000) - 100)
SIF (CFLAG:ARG:変化加算GUA / 1000) > 0
	LOCAL:1 += ((CFLAG:ARG:変化加算GUA % 1000) - 100)
SIF (CFLAG:ARG:変化加算SPD / 1000) > 0
	LOCAL:5 += ((CFLAG:ARG:変化加算SPD % 1000) - 100)

;状態異常による能力補正
;スロウ（速度半減）
IF CFLAG:ARG:スロウ状態 > 0
	LOCAL:4 /= 2
ENDIF

;吸収した攻撃力を加算
IF CFLAG:ARG:変化吸収POW
	LOCAL:0 += CFLAG:ARG:変化吸収POW
ENDIF



;以下ユニークスキル
;王者の威圧チェック
			CALL GET_MAX_ABL_PT(237)
			IF RESULT == 0
;運系補正
;不幸の星
IF ABL:ARG:不幸の星
	LOCAL:5 -= 15
	SIF LOCAL:5 <= 0
		LOCAL:5 = 1
ENDIF

;孤高の歌姫持ちで恋慕が付いていない時
IF ABL:ARG:孤高の歌姫 && TALENT:ARG:恋慕 == 0
	LOCAL:11 = 0
	FOR LOCAL:10, 1080, 1085
		IF FLAG:LOCAL
			LOCAL:11 += 1
		ENDIF
	NEXT
	IF LOCAL:11 > 1
		FOR LOCAL:20, 0, 7
			A = LOCAL:(LOCAL:20)
			TIMES A, 0.70
			LOCAL:(LOCAL:20) = A
		NEXT
	ENDIF
ENDIF
;ボス戦時、日高の血反映
IF ABL:ARG:日高の血
	IF TFLAG:イベント戦 > 4
		FOR LOCAL:10, 0, 7
			A = LOCAL:(LOCAL:10)
			TIMES A, 1.20
			LOCAL:(LOCAL:10) = A
		NEXT
	ENDIF
ENDIF
;過保護持ち
IF ABL:ARG:過保護
	LOCAL:12 = 0
	FOR LOCAL:10, 1080, 1085
		IF FLAG:(LOCAL:10) > 0 && CFLAG:(FLAG:(LOCAL:10)):気絶状態 == 0
			LOCAL:11 = BASE:(FLAG:(LOCAL:10)):HP / MAXBASE:(FLAG:(LOCAL:10)):HP + 100
			;メンバーの誰かが★15％以下で過保護発動
			IF LOCAL:11 <= 15
				LOCAL:12 = 1
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:12
		FOR LOCAL:10, 0, 7
			TIMES LOCAL:(LOCAL:10), 1.20
		NEXT
	ENDIF
ENDIF

;王者の威圧
			ENDIF
			
			;敵の時
			
			IF ARG < 11

;難易度による補正
;EASY
IF FLAG:難易度 == 0
	;POW
	TIMES LOCAL:0 , 0.7
	;GUA
	TIMES LOCAL:1 , 0.7
	;DEX
	TIMES LOCAL:2 , 0.7
	;HIT
	TIMES LOCAL:3 , 0.7
	;SPD
	TIMES LOCAL:4 , 0.7
	;LUC 
	TIMES LOCAL:5 , 0.7
	;CRI 
	TIMES LOCAL:6 , 0.7
;MISSION
ELSEIF FLAG:難易度 == 2
	;POW
	TIMES LOCAL:0 , 1.2
	;GUA
	TIMES LOCAL:1 , 1.0
	;DEX
	TIMES LOCAL:2 , 1.2
	;HIT
	TIMES LOCAL:3 , 1.2
	;SPD
	TIMES LOCAL:4 , 1.0
	;LUC 
	TIMES LOCAL:5 , 1.0
	;CRI 
	TIMES LOCAL:6 , 1.0
;HYPER
ELSEIF FLAG:難易度 == 3
	;POW
	TIMES LOCAL:0 , 2.0
	;GUA
	TIMES LOCAL:1 , 1.5
	;DEX
	TIMES LOCAL:2 , 2.0
	;HIT
	TIMES LOCAL:3 , 1.5
	;SPD
	TIMES LOCAL:4 , 1.0
	;LUC 
	TIMES LOCAL:5 , 1.5
	;CRI 
	TIMES LOCAL:6 , 1.5
;UNKNOWN
ELSEIF FLAG:難易度 == 10

;それ以外
ELSE

ENDIF

			ENDIF
;念のため下がり過ぎていたら値を1にする
FOR LOCAL:10, 0, 7
	SIF LOCAL:(LOCAL:10) <= 0
		LOCAL:(LOCAL:10) = 1
	
NEXT
;能力値の計算を反映
FOR LOCAL:10, 0, 7
	BASE:ARG:(LOCAL:10 + 71) = LOCAL:(LOCAL:10)
NEXT
;VOVIDAを反映
BASE:ARG:ベースボーカル = LOCAL:13
BASE:ARG:ベースビジュアル = LOCAL:14
BASE:ARG:ベースダンス = LOCAL:15

;計算中の値一時保存用にLOCAL:11を使用
LOCAL:11 = 0

;---------------------------
;攻撃力計算…戦闘で使う全てのアピール、スキルの威力にこの値を使用
;---------------------------
;■属性補正
;0未満…吸収
;0…無効
;40以下…微小
;60以下…半減
;100未満…防御
;100…通常
;100超過…弱点

LOCAL:11 = BASE:ARG:ベース攻撃力

IF ARG:0 < 11
	BASE:ARG:最終攻撃力 = LOCAL:11 / 1 + 1
ELSE
	BASE:ARG:最終攻撃力 = LOCAL:11 + 1
ENDIF

;---------------------------
;防御力計算
;---------------------------
LOCAL:11 = BASE:ARG:ベース防御力

IF ARG:0 < 11
	BASE:ARG:最終防御力 = LOCAL:11 / 1 + 1
ELSE
	BASE:ARG:最終防御力 = LOCAL:11 + 1
ENDIF

;---------------------------
;命中計算
;---------------------------
;敵の場合
IF ARG:0 < 11
	BASE:ARG:最終命中率 = 100 + (BASE:ARG:ベース命中率 * 3)
ELSE
	BASE:ARG:最終命中率 = 100 + (BASE:ARG:ベース命中率 * 2)
ENDIF
			;王者の威圧チェック
			CALL GET_MAX_ABL_PT(237)
IF RESULT == 0
;アンフェア
IF ABL:ARG:アンフェア
	A = BASE:ARG:最終命中率
	TIMES A , 1.50
	BASE:ARG:最終命中率 = A
ENDIF

			ENDIF
;---------------------------
;回避計算
;---------------------------
IF ARG:0 < 11
	BASE:ARG:最終回避率 = 50 + BASE:ARG:ベース素早さ / 4
ELSE
	BASE:ARG:最終回避率 = 50 + BASE:ARG:ベース素早さ / 2

ENDIF

			;王者の威圧チェック
			CALL GET_MAX_ABL_PT(237)
IF RESULT == 0
;アンフェア
IF ABL:ARG:アンフェア
	A = BASE:ARG:最終回避率
	TIMES A , 1.15
	BASE:ARG:最終回避率 = A
ENDIF
;サバイバー
IF ABL:ARG:サバイバー
	A = BASE:ARG:最終回避率
	TIMES A , 1.15
	BASE:ARG:最終回避率 = A
ENDIF
;小さい
IF ABL:ARG:小さい
	A = BASE:ARG:最終回避率
	TIMES A , 1.33
	BASE:ARG:最終回避率 = A
ENDIF
;ギャルの意地、ちびギャルの意地
IF ABL:ARG:ちびギャルの意地 || ABL:ARG:ギャルの意地
	A = BASE:ARG:最終回避率
	TIMES A , 1.10
	BASE:ARG:最終回避率 = A
ENDIF

			ENDIF

;---------------------------
;クリティカル計算
;---------------------------
;とりあえず10 + 運/10 100で100%クリティカル
BASE:ARG:ベースクリティカル = 10 + (BASE:ARG:ベース運 / 10)
;クリティカルアップ
IF CFLAG:ARG:クリティカルアップ状態
	LOCAL:0 = CFLAG:ARG:クリティカルアップ状態 % 10
	LOCAL:0 *= 10
	BASE:ARG:ベースクリティカル += LOCAL:0
ENDIF
;---------------------------
;★計算
;---------------------------

;装備補正
MAXBASE:ARG:HP = MAXBASE:ARG:HP + BASE:ARG:★総計

;敵用処理
IF CFLAG:ARG:敵★ > 0
	IF MAXBASE:ARG:HP <= 0
		MAXBASE:ARG:HP = CFLAG:ARG:敵★	
		;難易度による補正
		;EASY
		IF FLAG:難易度 == 0
			TIMES MAXBASE:ARG:HP , 0.7
		;MISSION
		ELSEIF FLAG:難易度 == 2
			TIMES MAXBASE:ARG:HP , 2.5
		;HYPER
		ELSEIF FLAG:難易度 == 3
			TIMES MAXBASE:ARG:HP , 5.0
		;UNKNOWN
		ELSEIF FLAG:難易度 == 10
		
		;それ以外
		ELSE
		
		ENDIF
	
		SIF MAXBASE:ARG:HP > 1000000
			MAXBASE:ARG:HP = 1000000
	ENDIF
ENDIF
;一時補正
IF (CFLAG:ARG:変化★ / 1000) > 0
	;効果が残っていれば反映する
	MAXBASE:ARG:HP = CFLAG:ARG:変化前最大★保存 * (CFLAG:ARG:変化★ % 1000) / 100

ENDIF

;最大オーバーチェック
IF BASE:ARG:HP > MAXBASE:ARG:HP
	BASE:ARG:HP = MAXBASE:ARG:HP
ENDIF

;---------------------------
;ハート計算
;---------------------------

;装備補正
MAXBASE:ARG:MP = MAXBASE:ARG:MP + BASE:ARG:ハート総計

;一時補正
IF (CFLAG:ARG:変化ハート / 1000) > 0
	;効果が残っていれば反映する
	MAXBASE:ARG:MP = CFLAG:ARG:変化前最大ハート保存 * (CFLAG:ARG:変化ハート % 1000) / 100
ENDIF
;敵用処理
IF CFLAG:ARG:敵ハート > 0
	MAXBASE:ARG:MP = CFLAG:ARG:敵ハート
ENDIF

;最大オーバーチェック
IF BASE:ARG:MP > MAXBASE:ARG:MP
	BASE:ARG:MP = MAXBASE:ARG:MP
ENDIF

;---------------------------
;ボルテージ計算
;---------------------------
;敵用処理
IF CFLAG:ARG:敵V > 0
	MAXBASE:ARG:ボルテージ = CFLAG:ARG:敵V
ENDIF

;最大オーバーチェック
IF BASE:ARG:ボルテージ > MAXBASE:ARG:ボルテージ
	BASE:ARG:ボルテージ = MAXBASE:ARG:ボルテージ
ENDIF

;---------------------------
;耐性計算
;---------------------------

FOR LOCAL:30, 0, 13
	LOCAL:(LOCAL:30) = BASE:ARG:(LOCAL:30 + 46)
	;ポジティブ思考反映
	IF LOCAL:30 >= 4
		;王者の威圧チェック
		CALL GET_MAX_ABL_PT(237)
		IF RESULT == 0
			IF ABL:ARG:ポジティブ思考
				LOCAL:(LOCAL:30) += 30
			;オーラ反映
			ELSEIF ABL:ARG:オーラ
				LOCAL:(LOCAL:30) += 10
			ELSE
				LOCAL:(LOCAL:30) += 0
			ENDIF
		ENDIF
	ENDIF
	;装備補正追加
	LOCAL:(LOCAL:30) += BASE:ARG:(LOCAL:30 + 146)
	
	;キャラ特性・装備で確保できる属性耐性は最大90まで
	IF LOCAL:30 <= 4
		SIF LOCAL:(LOCAL:30) > 90
			LOCAL:(LOCAL:30) = 90
	;キャラ特性・装備で確保できる異常耐性は、味方の場合100まで
	ELSE
		SIF LOCAL:(LOCAL:30) > 100 && ARG:0 > 10
			LOCAL:(LOCAL:30) = 100
	ENDIF

	;一時強化反映
	IF (CFLAG:ARG:(LOCAL:30 + 1138) / 1000) > 0
		;効果が残っていれば反映する
		LOCAL:(LOCAL:30) = LOCAL:(LOCAL:30) + (CFLAG:ARG:(LOCAL:30 +1138) % 1000) - 100
	ENDIF
	
	;一時強化反映後の属性耐性は100まで
	IF LOCAL:30 <= 4
		SIF LOCAL:(LOCAL:30) > 100
			LOCAL:(LOCAL:30) = 100
	;一時強化反映後の異常耐性は200まで
	ELSE
		SIF LOCAL:(LOCAL:30) > 200
			LOCAL:(LOCAL:30) = 200
	ENDIF
	;最終耐性に反映
	BASE:ARG:(LOCAL:30 + 96) = LOCAL:(LOCAL:30)
NEXT

RETURN
