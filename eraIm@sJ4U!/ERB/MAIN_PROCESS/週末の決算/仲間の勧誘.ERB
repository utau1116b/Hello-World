[SKIPSTART]
勧誘可能なキャラ数はＰランクが上昇するか施設建設で増加
[SKIPEND]
@仲間の勧誘
SIF MATCH(N,0,0,5) == 5
	CALL 勧誘可能キャラ取得

DO
	LOCAL:3 = 0
	REDRAW 0
	CALL SAVELINE
	CALL SHOW_MENU_勧誘
	INPUT
	IF RESULT == 100
		BREAK
	ELSE
		LOCAL:2 = RESULT
		IF !MATCH(N,RESULT,0,5)
			CALL RETURNLINE
			CONTINUE
		ENDIF
		DO
			CALL CHARA_MANUAL(LOCAL:2 + 89)
			DRAWLINE
			PRINTFORML このキャラクターを勧誘しますか？(%ADD_COMMA(契約金(LOCAL:2))%Ｇが必要です)
			PRINTFORML [0] はい
			PRINTFORML [1] いいえ
			PRINTFORMLC [2]外見を見る
			PRINTFORMLC [3]能力適正を見る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						IF MONEY < 契約金(LOCAL:2)
							REUSELASTLINE ゲイツが足りません……
							CONTINUE
						ELSE
							MONEY -= 契約金(LOCAL:2)
							CALL NEWCHARA_INIT(LOCAL:2)
							PRINTFORMW %CALLNAME:(LOCAL:2)%を勧誘しました！
							FOR LOCAL, 0, 5
								SIF N:LOCAL == LOCAL:2
									N:LOCAL = 0
							NEXT
							LOCAL:3 = 1
							BREAK
						ENDIF
					CASE 1
						LOCAL:3 = 1
						BREAK
					CASE 2
						CALL SHOW_CHARA_LOOKS(LOCAL:2)
						WAIT
						BREAK
					CASE 3
						CALL SHOW_CHARA_DUNAMIS(LOCAL:2)
						WAIT
						BREAK
				ENDSELECT
			LOOP 1
		LOOP (!LOCAL:3)
	ENDIF
	CALL RETURNLINE
LOOP 1
CALL RETURNLINE
RETURN 0

@勧誘可能キャラ取得
VARSET LOCAL
IF MATCH(N,0,0,5) == 5

ELSE
	RETURN 0
ENDIF
DO
	N:LOCAL = 11+RAND:(FLAG:キャラ数)
	SIF CFLAG:(N:LOCAL):勧誘状況 == -1
		CONTINUE
	LOCAL++
LOOP (N:4 <= 0)
;番号順にソート
FOR LOCAL, 0, 5
	FOR LOCAL:1, 4, LOCAL, -1
		SIF N:(LOCAL:1 - 1) > N:(LOCAL:1)
			SWAP N:(LOCAL:1 - 1), N:(LOCAL:1)
	NEXT
NEXT

@SHOW_MENU_勧誘
PRINTL 
DRAWLINE
PRINTL ★★勧誘したいキャラを選択してください★★  ( )内は契約金
FOR LOCAL, 0, 5
	IF N:LOCAL
		PRINTFORM [{N:LOCAL,3}] 
		RESULT = 0
		TRYCALLFORM KOJO_EXIST_K{N:LOCAL}
		SIF RESULT
			CALL SET_COLOR(N:LOCAL)
		PRINTFORM %CALLNAME:(N:LOCAL), 10, LEFT%
		RESETCOLOR
		PRINTFORML (%ADD_COMMA(契約金(N:LOCAL)), 7%Ｇ)
	ENDIF
NEXT
PRINTL
PRINTFORML [100] 戻る
	