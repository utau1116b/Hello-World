@EVENTSHOP
;オープニング非表示フラグリセット
DRAWLINE
DRAWLINE
;オートセーブ判定
TFLAG:99 = 1
;フラグ移動の互換処理
CALL SAVEDATA_CONVERT
;グローバル変数読み込み
CALL LOAD_GLOBAL_CONFIGURE
GLOBAL:801 = 0
SAVEGLOBAL

@SHOW_SHOP
REDRAW 0
CALL BGM("SHOP", "メイン")
;帰還フラグを折る
FLAG:帰還フラグ = 0
;未確認アイテム識別処理
CALL APPRAISE
;イベントがあれば呼び出し
CALL ストーリーイベント
TFLAG:100 = 0
;配列調整
CALL SHIFT_MEMBER
;オートセーブ判定を解除
TFLAG:99 = 0
;BASE:MASTER:MP = MAXBASE:MASTER:MP
FOR LOCAL:0, 1080, 1085
	SIF FLAG:LOCAL < 0
		CONTINUE
	IF CFLAG:(FLAG:(LOCAL:0)):気絶状態 == 1
		CFLAG:(FLAG:(LOCAL:0)):気絶状態 = 0
	ENDIF
	IF BASE:(FLAG:(LOCAL:0)):HP <= 0
		BASE:(FLAG:(LOCAL:0)):HP = 1
	ENDIF
NEXT
;所持金の上限設定
SIF MONEY >= 999999999999999999
	MONEY = 999999999999999999
;残り日数取得
CALL SYSTEM_GET_LIMITDAY
;年月日、曜日、天気等取得
CALL YOUBI
CALL STATUS_RENEW(1)

;画面表示
CALL UNITSTATUS
CALL SHOP_MAINMENU
CUSTOMDRAWLINE =
IF FLAG:スケジュール決定
	PRINTFORMLC [100] - スケジュール実行
ELSE
	PRINTFORMLC [100] - スケジュール選択
ENDIF
PRINTFORMLC 　
PRINTFORMLC [201] - 画面切り替え
;スキット表示
CALL SKIT
;IF SKIT
;	TRYCALLFORM SKITNAME{SKIT}
;	SETCOLOR SET_COLOR_FUNC("亜美")
;	PRINTFORMLC [150] - %TSTR%
;	RESETCOLOR
;ENDIF
PRINTL 

;コマンド表示
CUSTOMDRAWLINE =
REPEAT 100
	SIF COUNT == 100
		CONTINUE
	TFLAG:(COUNT+100) = 0
REND
VARSET LOCAL
CALL NEWLINE_U(1)
FOR LOCAL, 101, 200
	TRYCCALLFORM SHOP_COMABLE{LOCAL}
		IF RESULT > 0
			PRINTFORMLC [{LOCAL}] - %RESULTS%
			TFLAG:LOCAL = 1
		ELSEIF RESULT == -1
			SETCOLOR COLOR("light-gray")
			PRINTFORMLC [{LOCAL}] - %SAVESTR:199%
			RESETCOLOR
		ELSEIF RESULT == -2
			SETCOLOR COLOR("light-gray")
			PRINTFORMLC [{LOCAL}] - %RESULTS%
			RESETCOLOR
		ENDIF
		CALL NEWLINE_U
	CATCH
	ENDCATCH
NEXT
SIF !LINEISEMPTY()
	PRINTL 
CUSTOMDRAWLINE =
IF GETBIT(FLAG:23, 15)
	PRINTFORMLC [200] - セーブ            
	PRINTFORMLC [300] - ロード            
	PRINTFORMLC [400] - システム        
	PRINTL 
ELSE
	PRINTL [200] - セーブ            
	PRINTL [300] - ロード            
	PRINTL [400] - システム        
ENDIF
;スキットを表示
;CALL SKIT

IF TALENT:MASTER:998
;主人に[Debug]がある=debugモードである場合
	PRINTFORMLC [998] - デバッグメニュー
	PRINTL
	PRINTFORMLC [801] - デバッグ用フェス
	PRINTFORMLC [802] - デバッグ用ライブ
	PRINTFORMLC [803] - デバッグ用オーディション
ENDIF

@USERSHOP
;画面切り替え
IF RESULT == 201
	FLAG:ショップ画面管理 = !FLAG:ショップ画面管理
;スケジュール選択
ELSEIF RESULT == 100 && TARGET >= 0 && FLAG:スケジュール決定 == 0
	CALL SELECT_SCHEDULE
;スケジュール実行
ELSEIF RESULT == 100 && TARGET >= 0 && FLAG:スケジュール決定 == 1
	CALL DO_SCHEDULE
	RETURN 1
;スキット
ELSEIF RESULT == 150 && SKIT > 0
	IF FIRSTSKIT(SKIT)
		CALLFORM SKIT_SETUP_{SKIT}
		SIF RESULT:1
			FLAG:口上文字色使用 = 1
		CALLFORM SKIT{SKIT}
		;まだ実行できるスキットがあるなら配列をズラース
		;ARRAYSHIFT K, -1, 0
		FLAG:口上文字色使用 = 0
	ENDIF
;システム
ELSEIF RESULT == 400
	CALL SHOP_SYSTEM
;セーブ
ELSEIF RESULT == 200
	CALL SAVEGAME_EX
;ロード
ELSEIF RESULT == 300
	CALL LOADGAME_EX
ELSEIF RESULT == 998 && TALENT:MASTER:998
	PRINTL デバッグメニューを呼び出します
	CALL DEBUG_MENU_SHOP
;デバッグ用フェス
ELSEIF RESULT == 801 && TALENT:MASTER:998
	CALL ステージ前準備(1200)
;デバッグ用ライブ
ELSEIF RESULT == 802 && TALENT:MASTER:998
	CALL ステージ前準備(1115)
;デバッグ用オーディション
ELSEIF RESULT == 803 && TALENT:MASTER:998
	CALL ステージ前準備(1000)
;パートナー交代
ELSEIF RESULT == 130
	CALL CHANGE_TARGET
;Rename作成
ELSEIF RESULT == 1234
	CALL RENAME
;キャラメイク
ELSEIF RESULT == 765
	CALL MAKE_CSV
ELSE

ENDIF
;その他ショップコマンド
SIF TFLAG:RESULT > 0
	TRYCALLFORM SHOP_COM{RESULT}

;---------------------------------------------------------
;パートナー交代
;---------------------------------------------------------
@CHANGE_TARGET
DRAWLINE
LOCAL = 0
$START
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND
;Mに交代前のパートナー番号格納
M = TARGET
IF TARGET >= 0
	PRINT 現在
	PRINTFORML %NAME:TARGET%をパートナーとしています
ENDIF
PRINTL 今回は誰をパートナーにしますか？
;変数Vは出産育児関連で使用
V = 1
CALL LIFE_LIST(0, LOCAL)
V = 0
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTL [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTL [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTL [300] キャラリスト簡易表示 OFF
ELSE
	PRINTL [300] キャラリスト簡易表示 ON
ENDIF
PRINTL [400] パートナーなしにする
PRINTL [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		GOTO START
	ELSE
		U = 0
		GOTO START
	ENDIF
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ELSE
		FLAG:23 |= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ENDIF
ELSEIF RESULT == 400
	PRINTFORMW パートナーなしにしました
	TARGET = -1
	FLAG:リーダー = -1
	RESTART
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO START
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;臨月、育児中のキャラも選べない
ELSEIF FLAG:62 & 1 && (MARK:RESULT:90 == 1)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;ELSEIF RESULT == ASSI
	;GOTO INPUT_LOOP
;勧誘してないキャラは選べない
ELSEIF CFLAG:RESULT:勧誘状況 != -1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF RESULT == MASTER
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF


IF TARGET != RESULT
	;一応誰に代わるのかはTで得られるようにしておく(LOCALでも記録)
	T = RESULT
	LOCAL = RESULT
	TFLAG:600 = 22
	CALL KOJO_MESSAGE_SEND("JUN")
	;RESULTを戻しておく
	RESULT = T
	RESULT = LOCAL
ENDIF

IF RESULT != 0
	;誰から代わるかはTで得られるようにしておく
	T = TARGET
	TARGET = RESULT
ENDIF
;相性だけで助手になっていた場合、未設定に戻しておく
IF ASSI >= 0
	SIF CFLAG:ASSI:1 < 2
		ASSI = -1
ENDIF
SIF TARGET == ASSI
	ASSI = -1

;妖怪退治はさせない
SIF CFLAG:TARGET:13 == 6
	CFLAG:TARGET:13 = 0

;休憩（非表示）にはさせない
SIF CFLAG:TARGET:16 == 1
	CFLAG:TARGET:16 = 0

PRINTFORMW %CALLNAME:TARGET%をパートナーにしました
IF T != TARGET
	;誰から代わってきたかはT
	TFLAG:600 = 60
	CALL KOJO_MESSAGE_SEND("JUN")
	RESULT = TARGET
	;もしも交代前がメンバーならメンバーとユニットでの位置を入れ替え
	IF TARGET == FLAG:リーダー
		FLAG:リーダー = TARGET
	ELSEIF TARGET == FLAG:メンバー2
		FLAG:リーダー = TARGET
		FLAG:メンバー2 = M
	ELSEIF TARGET == FLAG:メンバー3
		FLAG:リーダー = TARGET
		FLAG:メンバー3 = M
	ELSEIF TARGET == FLAG:メンバー4
		FLAG:リーダー = TARGET
		FLAG:メンバー4 = M
	ELSEIF TARGET == FLAG:メンバー5
		FLAG:リーダー = TARGET
		FLAG:メンバー5 = M
	ELSE
		FLAG:リーダー = TARGET
	ENDIF
	
ENDIF
;---------------------------------------------------------
;ARG:0	変更後パートナー
;ARG:1	変更前パートナー
;---------------------------------------------------------
;パートナー交代
;---------------------------------------------------------
@CHANGE_TARGET2(ARG:0, ARG:1)

;臨月、育児中のキャラも選べない
TARGET = ARG:0
;相性だけで助手になっていた場合、未設定に戻しておく
IF ASSI >= 0
	SIF CFLAG:ASSI:1 < 2
		ASSI = -1
ENDIF
SIF TARGET == ASSI
	ASSI = -1

;助手交代
@SELECT_ASSI
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

PRINT 現在
IF ASSI >= 0
	PRINTFORML [{ASSI}]%NAME:ASSI%が助手
ELSE
	PRINTFORML 助手はいません
ENDIF
PRINTL 誰を助手にしますか？(現在の助手は☆)

PRINTFORML   [ 0]助手なし
REPEAT CHARANUM
	TFLAG:COUNT = 0
	;0（主人公）とASSI以外は排除
	SIF COUNT == 0
		CONTINUE
	SIF COUNT == MASTER
		CONTINUE
	;対象は選べない
	SIF COUNT == TARGET
		CONTINUE
	;勧誘済みでないと排除
	SIF CFLAG:COUNT:勧誘状況 != -1
		CONTINUE
	;預かり中のキャラは非表示だと選べない
	SIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れ中のキャラは選べない
	SIF CFLAG:COUNT:21
		CONTINUE
	;臨月、育児中のキャラも選べない
	SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1)
		CONTINUE
	;霧に飲まれかけているキャラは選べない
	SIF CFLAG:COUNT:17 == 1
		CONTINUE
	IF TARGET >= 0
		R = NO:TARGET
		SIF (CFLAG:COUNT:1 < 2 && RELATION:COUNT:R < 120)
			CONTINUE
	ELSE
		SIF CFLAG:COUNT:1 < 2
			CONTINUE
	ENDIF
	IF COUNT == ASSI
		PRINT ☆
	ELSE
		PRINT 　
	ENDIF
	PRINTFORM [{COUNT,2}]%NAME:COUNT,16,LEFT% 
	;CALL ARRANGE_CHARANAME
	IF COUNT == ASSI
		PRINT 　　　　　
	ELSEIF ISASSI:COUNT == 1
		PRINT [元助手]　
	ELSE
		PRINT [未経験]　
	ENDIF
	IF ABL:COUNT:2 < 10
		PRINTFORM %ABLNAME:2%Lv{ABL:COUNT:2}  
	ELSE
		PRINTFORM %ABLNAME:2%Lv{ABL:COUNT:2} 
	ENDIF
	IF ABL:COUNT:9 < 10
		PRINTFORM %ABLNAME:9%Lv{ABL:COUNT:9}  
	ELSE
		PRINTFORM %ABLNAME:9%Lv{ABL:COUNT:9} 
	ENDIF
	IF ABL:COUNT:12 < 10
		PRINTFORM %ABLNAME:12%Lv{ABL:COUNT:12}  
	ELSE
		PRINTFORM %ABLNAME:12%Lv{ABL:COUNT:12} 
	ENDIF
	SIF TALENT:COUNT:81
		PRINT 両刀
	PRINTFORML 
REND
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF


$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		RESTART
	ELSE
		U = 0
		RESTART
	ENDIF
ELSEIF RESULT == 0
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけている、別れているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1 || CFLAG:RESULT:21
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	IF TARGET >= 0
		R = NO:TARGET
		IF RELATION:RESULT:R < 120
			PRINTFORML 正しい値を入力してください
			GOTO INPUT_LOOP
		ENDIF
	ELSE
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP
	ENDIF
ENDIF

ASSI = RESULT

IF TARGET != RESULT
	;変更後の助手番号はASSIで見れる
	TFLAG:600 = 23
	CALL KOJO_MESSAGE_SEND("JUN")
ENDIF

;妖怪退治はさせない
SIF CFLAG:ASSI:13 == 6
	CFLAG:ASSI:13 = 0

;休憩（非表示）にはさせない
SIF CFLAG:ASSI:16 == 1
	CFLAG:ASSI:16 = 0

CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1

PRINTFORMW %CALLNAME:ASSI%を助手にしました

@LIFE_LIST(ARG, ARG:1)
;FLAG:23 & 16384 ONでキャラリスト簡易表示
W = 0
DRAWLINE
PRINT 体力
SIF BASE:MASTER:0 < 0
	BASE:MASTER:0 = 0
;CALL PRINT_BAR_0, BASE:MASTER:0, MAXBASE:MASTER:0, 32
CALL COMMON_SHOW_LIFEBAR1, MASTER, 1
PRINTFORM ({BASE:MASTER:0}/{MAXBASE:MASTER:0})
PRINTFORML  [0]%NAME:MASTER%
DRAWLINE
IF FLAG:23 & 16384
	REPEAT CHARANUM
	;PRINTFORM {COUNT}
		;主人公は排除
		SIF COUNT == 0
			CONTINUE
		SIF COUNT == MASTER
			CONTINUE
		;勧誘済みでなければ排除
		SIF CFLAG:COUNT:勧誘状況 != -1
			CONTINUE
		;預かり中のキャラは非表示なら排除
		SIF CFLAG:COUNT:16 == 1 && U == 0
			CONTINUE
		;別れ中のキャラは排除
		SIF CFLAG:COUNT:21
			CONTINUE
		;臨月、育児中のキャラは排除
		SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1) && V
			CONTINUE
		;2列で表示
		PRINTFORM [{COUNT,2}]
		;パートナー（とついでに助手）が分かりやすいように
		IF COUNT == TARGET
			PRINT ☆ 
		ELSEIF COUNT == ASSI
			PRINT ★ 
		ELSEIF FLAG:5 == 10 && CFLAG:COUNT:17
			PRINT × 
		ELSE
			PRINT 　 
		ENDIF
		PRINTFORM %NAME:COUNT,16,LEFT%
		SIF ARG
			CALL ISABLUP_ALL
		IF TALENT:COUNT:89
			PRINT [※相愛※] 
		ELSEIF TALENT:COUNT:88
			PRINT [※親愛※] 
		;●友情パッチ用追加
		;友情パッチ適用キャラならこれで恋人より親友、恋慕より信頼が優先されるはず
		ELSEIF TALENT:COUNT:157
			PRINT [※親友※] 
		;●ここまで
		ELSEIF TALENT:COUNT:153
			PRINT [※恋人※] 
;●友情パッチ用追加
		ELSEIF TALENT:COUNT:156 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			PRINT [※信頼※] 
;●ここまで
		;恋慕についてはインビシブルモードかどうかで表示するかどうかを変える
		ELSEIF TALENT:COUNT:85 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			PRINT [※恋慕※] 
		ELSE
			PRINT 　　　　　 
		ENDIF
		W += 1
		IF W == 2
			W = 0
			PRINTL 
		ENDIF
	REND
	PRINTL 
ELSE
	REPEAT CHARANUM
		;主人公は排除
		SIF COUNT == 0
			CONTINUE
		;勧誘済みでなければ排除
		SIF CFLAG:COUNT:勧誘状況 != -1
			CONTINUE
		;預かり中のキャラは非表示なら排除
		SIF CFLAG:COUNT:16 == 1 && U == 0
			CONTINUE
		;別れ中のキャラは排除
		SIF CFLAG:COUNT:21
			CONTINUE
		;臨月、育児中のキャラは排除
		SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1) && V
			CONTINUE
		PRINT 体力
		SIF BASE:COUNT:0 < 0
			BASE:COUNT:0 = 0
		;CALL PRINT_BAR_0, BASE:COUNT:0, MAXBASE:COUNT:0, 16
		CALL COMMON_SHOW_LIFEBAR1, COUNT, 1
		PRINTFORM ({BASE:COUNT:0,4}/{MAXBASE:COUNT:0,4})
		IF COUNT == TARGET
			PRINT  ☆
		ELSEIF COUNT == ASSI
			PRINT  ★
		ELSE
			PRINT  　
		ENDIF
		PRINTFORM [{COUNT,2}]%NAME:COUNT,16,LEFT%　
		SIF ARG
			CALL ISABLUP_ALL
		IF COUNT == ASSI
			PRINT 　　　　　
		ELSEIF ISASSI:COUNT == 1
			PRINT [●元助手]
		ELSEIF CFLAG:COUNT:1 == 2
			PRINT [○助手可]
		ENDIF
		IF TALENT:COUNT:89
			PRINT [※相愛※]
		ELSEIF TALENT:COUNT:88
			PRINT [※親愛※]
	;●友情パッチ用追加
		;友情パッチ適用キャラならこれで恋人より親友、恋慕より信頼が優先されるはず
		ELSEIF TALENT:COUNT:157
			PRINT [※親友※] 
	;●ここまで
		ELSEIF TALENT:COUNT:153
			PRINT [※恋人※]
	;●友情パッチ用追加
		ELSEIF TALENT:COUNT:156 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			PRINT [※信頼※] 
	;●ここまで
		;恋慕についてはインビシブルモードかどうかで表示するかどうかを変える
		ELSEIF TALENT:COUNT:85 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			PRINT [※恋慕※] 
		ENDIF

		PRINTL 

		T = COUNT

		;ARG:1 により表示するものを変えてみるテスト(ARG:1==0 で従来どおり）
		SELECTCASE ARG:1
			;刻印系表示
			CASE 2
				PRINTFORML     苦痛依存：{MARK:T:0, 2}  快楽依存：{MARK:T:1, 2}  既成事実：{MARK:T:2, 2}  反発感情：{MARK:T:3, 2}  
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
			;恋愛系能力表示
			CASE 1
				PRINTFORM     親密：{ABL:T:0, 2}  欲望：{ABL:T:1, 2}  技巧：{ABL:T:2, 2}  奉仕精神：{ABL:T:6, 2}  
				SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
					PRINTFORM 好感度：{CFLAG:T:2}
				PRINTFORML 
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
			;戦闘系能力表示
			CASEELSE
				Z = COUNT
				CALL GET_BATTLE_POWER
				COUNT = Z
				A = BASE:T:HP
				SIF BASE:T:HP < 0
					A = 0
				PRINTFORM        
				CALL STARMARK_Y
				PRINTFORM ：{A}/{MAXBASE:T:HP}  
				CALL HEARTMARK_P
				PRINTFORM ：{BASE:T:MP}/{MAXBASE:T:MP}  
				SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
					PRINTFORM 好感度：{CFLAG:T:2}
				PRINTFORML 
				SIF FLAG:5 == 10
					PRINTFORM     仲間になってから{CFLAG:T:18}日経過  今までに{CFLAG:T:19}回霧に飲まれかけました
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
		ENDSELECT
	REND
ENDIF
IF FLAG:5 == 10
	PRINTL 　　　　　　☆：パートナー　★：助手　×：霧に飲まれかけ　を表しています
ELSE
	PRINTL 　　　　　　☆：パートナー　★：助手　を表しています
ENDIF

;避難所（キャラ預かり所）
@AZUKARI
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

PRINTL 一覧などで表示させたくないキャラを、休憩させておく事ができます。
PRINTL 維持費などは通常通りかかります。あくまで表示しないだけなので注意してください。
CALL KYUUKEIJO

@KYUUKEIJO
DRAWLINE
PRINTL 休憩所です。休ませるor連れて行くキャラを選択してください。
PRINTL （※：休憩中を表しています）

X = 0
PRINTL  
;LOCALに表示可能なキャラ数を記録し、2人以上なら全員へ指示する選択肢表示
LOCAL = 0
REPEAT CHARANUM
	SIF COUNT == 0
		CONTINUE
	SIF COUNT == MASTER
		CONTINUE
	SIF COUNT == ASSI
		CONTINUE
	SIF COUNT == TARGET
		CONTINUE
	SIF CFLAG:COUNT:21
		CONTINUE
	;勧誘してないキャラは選べない
	SIF CFLAG:COUNT:勧誘状況 != -1
		CONTINUE
	;ユニットメンバーは選べない
	SIF COUNT == FLAG:リーダー || COUNT == FLAG:メンバー2 || COUNT == FLAG:メンバー3 || COUNT == FLAG:メンバー4 || COUNT == FLAG:メンバー5
		CONTINUE
	PRINTFORM [{COUNT,2}] 
	IF CFLAG:COUNT:16 == 1
		PRINT ※ 
	ELSE
		PRINT 　 
	ENDIF
	PRINTFORM %NAME:COUNT,16,LEFT%
	;CALL ARRANGE_CHARANAME
	X += 1
	IF X == 3
		X = 0
		PRINTL 
	ENDIF
	LOCAL += 1
REND
PRINTL  
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF LOCAL >= 2
	PRINTL [200] パートナー・助手以外を全員休ませる
	PRINTL [300] 全員連れて行く
ENDIF

$INPUTLOOPAZUKARI
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 1
ELSEIF (RESULT == 200 || RESULT == 300) && LOCAL >= 2
	REPEAT CHARANUM
		SIF COUNT == MASTER
			CONTINUE
		SIF COUNT == ASSI
			CONTINUE
		SIF COUNT == TARGET
			CONTINUE
		SIF CFLAG:COUNT:21
			CONTINUE
		IF RESULT == 200
			;非表示にする
			CFLAG:COUNT:16 = 1
		ELSEIF RESULT == 300
			;連れて行く
			CFLAG:COUNT:16 = 0
		ENDIF
	REND
	IF RESULT == 200
		PRINTFORMW 全員休ませました
	ELSEIF RESULT == 300
		PRINTFORMW 全員連れて行きます
	ENDIF
	RESTART
ELSEIF RESULT == ASSI || RESULT == MASTER || RESULT == TARGET || CFLAG:RESULT:21 == 1
	GOTO INPUTLOOPAZUKARI
ELSEIF RESULT < CHARANUM && RESULT != 0
	IF CFLAG:RESULT:16 == 1
		CFLAG:RESULT:16 = 0
		PRINTFORMW %NAME:RESULT%を連れて行きます
		RESTART
	ELSEIF CFLAG:RESULT:16 == 0
		CFLAG:RESULT:16 = 1
		PRINTFORMW %NAME:RESULT%を休ませました
		RESTART
	ENDIF
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUTLOOPAZUKARI
ELSE
	GOTO INPUTLOOPAZUKARI
ENDIF

RETURN 1

;ステータス表示
@SHOW_CHARADATA
LOCAL = 0
$START
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND
V = 0
CALL LIFE_LIST(0, LOCAL)
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTFORML [300] キャラリスト簡易表示 OFF
ELSE
	PRINTFORML [300] キャラリスト簡易表示 ON
ENDIF
PRINTFORML [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		GOTO START
	ELSE
		U = 0
		GOTO START
	ENDIF
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ELSE
		FLAG:23 |= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ENDIF
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO START
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF

;現在のTARGET一時記憶
O = TARGET

TARGET = RESULT
;新表示方式用にWにも入れておく
W = TARGET
PRINTSL NAME:TARGET
SIF TARGET != MASTER
	CALL SHOW_BATTLE_INFO
L = NO:TARGET
CALL GET_STRAINNAME
;新型表示
DRAWLINE
PRINTFORML ■ %NAME:TARGET%のステータス
SIF FLAG:23 & 524288
	PRINTFORML   種族:%STR:203%
PRINTFORML   体力:({BASE:0,4}/{MAXBASE:0,4})  気力:({BASE:1,4}/{MAXBASE:1,4})
PRINTL ･･･････････････････････････････････････････････････････････････････････････････
PRINTL □ 珠
CALL NEW_PRINT_JUEL
CALL SHOW_INFO
TARGET = O

GOTO START

@SHOW_BATTLE_INFO
;戦闘用の情報を表示
T = TARGET
CALL GET_BATTLE_POWER
PRINTFORM 戦闘能力：{P}  魔力：{BASE:MP}
SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
	PRINTFORM   好感度：{CFLAG:2}
PRINTFORML 
PRINT 耐力
A = BASE:HP
SIF BASE:HP < 0
	A = 0
CALL PRINT_BAR_4, A, MAXBASE:HP, 32
PRINTFORML ({BASE:HP}/{MAXBASE:HP})

RETURN 1

;奴隷売却の処理
@CHARA_SALE
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

DRAWLINE
PRINTV DAY+1
PRINT 日目
IF !TIME && !TIME:12
	PRINTL 朝
ELSEIF !TIME
	PRINTL  昼
ELSE
	PRINTL  夜
ENDIF
IF TARGET >= 0
	CALL SET_COLOR(TARGET)
	PRINTFORM パートナー %NAME:TARGET%
	RESETCOLOR
ENDIF
IF ASSI >= 0
	PRINTFORM （助手：
	CALL SET_COLOR(ASSI)
	PRINTFORM %NAME:ASSI%
	RESETCOLOR
	PRINTFORM ）
ENDIF
PRINTFORML (残り%ADD_COMMA(MONEY)%Ｇ)
DRAWLINE
DRAWLINE
PRINTL 誰を解雇しますか？
PRINTL 
;別れられるキャラを記録する変数初期化
REPEAT CHARANUM
	LOCAL:COUNT = 0
REND
REPEAT CHARANUM
	TFLAG:COUNT = 0
	IF COUNT == 0
		CONTINUE
	ELSEIF COUNT == MASTER
		CONTINUE
	;預かり中のキャラは非表示だと選べない
	ELSEIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れ中のキャラは選べない
	ELSEIF CFLAG:COUNT:21
		CONTINUE
	;勧誘していないキャラは選べない
	ELSEIF CFLAG:COUNT:勧誘状況 != -1
		CONTINUE
	;ユニットメンバーは選べない
	ELSEIF COUNT == FLAG:リーダー || COUNT == FLAG:メンバー2 || COUNT == FLAG:メンバー3 || COUNT == FLAG:メンバー4 || COUNT == FLAG:メンバー5
		CONTINUE
	;妊娠中、育児中のキャラは選べない(主人の子供の場合不可)
	ELSEIF (TALENT:COUNT:134 || TALENT:COUNT:135) && CFLAG:COUNT:111 == NO:MASTER
		CONTINUE
	ELSEIF TALENT:COUNT:150 == 0 && TALENT:COUNT:89 == 0 && CFLAG:COUNT:7 == 0 && TALENT:COUNT:181 == 0
		PRINTFORM [{COUNT}] %NAME:COUNT% 
		IF ASSI == COUNT
			PRINT (助手)
		ELSEIF ISASSI:COUNT == 1
			PRINT (元助手)
		ELSEIF CFLAG:COUNT:1 == 2
			PRINT (助手可)
		ENDIF
		CALL ARRANGE_CHARANAME
		T = COUNT
		Z = COUNT
		CALL GET_BATTLE_POWER
		COUNT = Z
		A = BASE:T:HP
		SIF BASE:T:HP < 0
			A = 0
		SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
			PRINTFORM 好感度：{CFLAG:T:2}
		PRINTFORML 
		
		;別れられるキャラを記録
		LOCAL:COUNT = 1
	ENDIF
REND
DRAWLINE
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
PRINTFORML [300] 解雇できるキャラ全員を解雇する

$INPUT_LOOP_CHARA_SALE
INPUT

IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		RESTART
	ELSE
		U = 0
		RESTART
	ENDIF
ELSEIF RESULT == 300
	PRINTFORML 解雇できるキャラ全員を解雇します。よろしいですか？
	PRINTFORML ※現在のパートナー、初期パートナー、ユニットメンバー、[相愛]キャラとは別れられません。
	PRINTFORML 
	PRINTFORML [0] はい（再開時に能力を引き継ぎます）
	PRINTFORML [1] はい（再開時に能力を引き継ぎません）
	PRINTFORML [2] いいえ
	
	$INPUT_LOOP_CHARA_SALE_ALL
	INPUT
	IF RESULT < 0 || RESULT > 2
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_2
	ELSEIF RESULT == 2
		RESTART
	ENDIF
	
	;口上表示等は省略
	LOCAL:1 = 0
	REPEAT CHARANUM
		;別れられないキャラは除く
		SIF LOCAL:COUNT != 1
			CONTINUE
		;現在のパートナーは除く
		SIF COUNT == TARGET
			CONTINUE
		;助手の場合助手なしに
		SIF COUNT == ASSI
			ASSI = -1
		;別れ状態にする
		CFLAG:COUNT:勧誘状況 = 3
		;待機中動作をリセット
		CFLAG:COUNT:13 = 0
		
		;装備をはずす
		IF EQUIP:COUNT:装着中服ID > 0
			ITEM:(EQUIP:COUNT:装着中服ID) ++
			EQUIP:COUNT:装着中服ID = 0
		ENDIF
		IF EQUIP:COUNT:HEAD > 0
			ITEM:(EQUIP:COUNT:HEAD) ++
			EQUIP:COUNT:HEAD = 0
		ENDIF
		IF EQUIP:COUNT:BODY > 0
			ITEM:(EQUIP:COUNT:BODY) ++
			EQUIP:COUNT:BODY = 0
		ENDIF
		IF EQUIP:COUNT:HAND > 0
			ITEM:(EQUIP:COUNT:HAND) ++
			EQUIP:COUNT:HAND = 0
		ENDIF
		IF EQUIP:COUNT:LEG > 0
			ITEM:(EQUIP:COUNT:LEG) ++
			EQUIP:COUNT:LEG = 0
		ENDIF
		
		IF RESULT == 0
			;一時的に別れる（CFLAG:21立てるだけ）
			CFLAG:COUNT:21 = 1
		ELSE
			;完全に別れる（キャラデータリセット）
			FOR TEMP, 0, VARSIZE("BASE")
				MAXBASE:COUNT:TEMP = CSVBASE(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("ABL")
				ABL:COUNT:TEMP = CSVABL(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("TALENT")
				TALENT:COUNT:TEMP = CSVTALENT(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("MARK")
				MARK:COUNT:TEMP = CSVMARK(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("EXP")
				EXP:COUNT:TEMP = CSVEXP(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("JUEL")
				JUEL:COUNT:TEMP = CSVJUEL(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("EQUIP")
				EQUIP:COUNT:TEMP = CSVEQUIP(COUNT, TEMP)
			NEXT
			FOR TEMP, 0, VARSIZE("CFLAG")
				CFLAG:COUNT:TEMP = CSVCFLAG(COUNT, TEMP)
			NEXT
		ENDIF
	REND
	PRINTFORMW 別れられるキャラ全員と別れました
	
ELSEIF LOCAL:RESULT != 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_CHARA_SALE
ELSE
	;変数保管
	LOCAL:0 = RESULT
	LOCAL:1 = 0

	PRINTFORML %CALLNAME:(LOCAL:0)%と別れます。よろしいですか？
	PRINTFORML [0] はい（再会時に能力を引き継ぎます）
	PRINTFORML [1] はい（再会時に能力を引き継ぎません）
	PRINTFORML [2] いいえ

	$INPUT_LOOP_2
	INPUT
	IF RESULT < 0 || RESULT > 2
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_2
	ELSEIF RESULT == 0
		LOCAL:1 = 0
	ELSEIF RESULT == 1
		LOCAL:1 = 1
	ELSEIF RESULT == 2
		RESTART
	ENDIF

	;変数を戻す
	RESULT = LOCAL:0

	;対象の情報を取得
	IF RESULT == TARGET || TARGET == -1
		LOCAL:0 = -1
	ELSE
		LOCAL:0 = NO:TARGET
	ENDIF

	;助手の情報を取得
	LOCAL:2 = -1
	SIF ASSI >= 0
		LOCAL:2 = NO:ASSI
	
	;売却処理
	TARGET = RESULT
	TFLAG:600 = 24
	CALL KOJO_MESSAGE_SEND("JUN")
	;別れる拒否パッチより、別れる口上内でCFLAG:7に1を代入していた場合別れるを拒否
	IF CFLAG:7 == 1
		CFLAG:7 = 0
		RESTART
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%と別れました
	;COUNTにTARGETを格納
	COUNT = TARGET
	;別れ状態にする
	CFLAG:勧誘状況 = 3
	;待機中動作をリセット
	CFLAG:13 = 0
	;装備をはずす
	IF EQUIP:COUNT:装着中服ID > 0
		ITEM:(EQUIP:COUNT:装着中服ID) ++
		EQUIP:COUNT:装着中服ID = 0
	ENDIF
	IF EQUIP:COUNT:HEAD > 0
		ITEM:(EQUIP:COUNT:HEAD) ++
		EQUIP:COUNT:HEAD = 0
	ENDIF
	IF EQUIP:COUNT:BODY > 0
		ITEM:(EQUIP:COUNT:BODY) ++
		EQUIP:COUNT:BODY = 0
	ENDIF
	IF EQUIP:COUNT:HAND > 0
		ITEM:(EQUIP:COUNT:HAND) ++
		EQUIP:COUNT:HAND = 0
	ENDIF
	IF EQUIP:COUNT:LEG > 0
		ITEM:(EQUIP:COUNT:LEG) ++
		EQUIP:COUNT:LEG = 0
	ENDIF
	
	IF LOCAL:1 == 0
		;一時的に別れる（CFLAG:21立てるだけ）
		CFLAG:21 = 1
	ELSE
		;完全に別れる（キャラデータリセット）
		FOR TEMP, 0, VARSIZE("BASE")
			MAXBASE:COUNT:TEMP = CSVBASE(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("ABL")
			ABL:COUNT:TEMP = CSVABL(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("TALENT")
			TALENT:COUNT:TEMP = CSVTALENT(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("MARK")
			MARK:COUNT:TEMP = CSVMARK(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("EXP")
			EXP:COUNT:TEMP = CSVEXP(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("JUEL")
			JUEL:COUNT:TEMP = CSVJUEL(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("EQUIP")
			EQUIP:COUNT:TEMP = CSVEQUIP(COUNT, TEMP)
		NEXT
		FOR TEMP, 0, VARSIZE("CFLAG")
			CFLAG:COUNT:TEMP = CSVCFLAG(COUNT, TEMP)
		NEXT
	ENDIF
	;対象、助手を戻す
	TARGET = -1
	ASSI = -1
	REPEAT CHARANUM
		SIF NO:COUNT == LOCAL:0
			TARGET = COUNT
		SIF NO:COUNT == LOCAL:2
			ASSI = COUNT
	REND

	RESTART
ENDIF



;能力アップ対象選択
@ABL_UP_SELECT
LOCAL = 0
$INPUT_LOOP2
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND
PRINTL 誰の能力を上げますか？
V = 0
CALL LIFE_LIST(1, LOCAL)
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTFORML [300] キャラリスト簡易表示 OFF
ELSE
	PRINTFORML [300] キャラリスト簡易表示 ON
ENDIF
PRINTFORML [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 0 || RESULT == MASTER
	PRINTFORML %CALLNAME:MASTER%の能力は上げられません
	GOTO INPUT_LOOP
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		GOTO INPUT_LOOP2
	ELSE
		U = 0
		GOTO INPUT_LOOP2
	ENDIF
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO INPUT_LOOP2
	ELSE
		FLAG:23 |= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO INPUT_LOOP2
	ENDIF
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO INPUT_LOOP2
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;ELSEIF RESULT == ASSI
	;GOTO INPUT_LOOP
;勧誘済みでないキャラは選べない
ELSEIF CFLAG:RESULT:勧誘状況 != -1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF

;現在のターゲットを記憶
Y = TARGET
TARGET = RESULT
CALL ABL_MAN
;復元
TARGET = Y
GOTO INPUT_LOOP2

@ISABLUP_ALL
VARSET LOCAL, 0
LOCAL:100 = COUNT
LOCAL:101 = TARGET
TARGET = COUNT

CALL ABL_SORT
REPEAT (A:0 - 3)
	;CALL ABL_SORTで取得したX:COUNT（並べ直したABL）をLOCALに代入
	LOCAL = X:COUNT
	;オトコ、女でそれぞれ不要なABLをスルー
	SIF LOCAL == 13 && TALENT:122 == 0
		CONTINUE
	SIF (LOCAL == 4 || LOCAL == 9 || LOCAL == 12) && TALENT:122
		CONTINUE
	;ISABLUPのRESULTが1なら半角スペース1個と*、以外なら半角スペース2個
	TRYCALLFORM ISABLUP{LOCAL}
	IF RESULT == 1
		PRINT  * 
		LOCAL:102 = 1
		BREAK
	ENDIF
REND
SIF LOCAL:102 == 0
	PRINT    
COUNT = LOCAL:100
TARGET = LOCAL:101



;@SALEITEM_CHECK,@EVENTBUY,@BUY_PLURAL,@USE_ITEMはSHOP_ITEM.ERBに移動

@EARN_MONEY
;選択肢を表示して、お金を稼ぐ
I = 0
J = 0
K = 0
IF TARGET >= 0
	SIF BASE:MASTER:HP > 300 && BASE:TARGET:0 > 200 && (ABL:0 >= 5 || CFLAG:2 >= 500 || TALENT:153)
		I = 1
	SIF BASE:MASTER:HP > 400 && BASE:TARGET:0 > 500 && ABL:料理技能 >= 5 && (ABL:0 >= 10 || CFLAG:2 >= 1000 || TALENT:153)
		J = 1
	SIF ABL:6 >= 5 && ABL:料理技能 >= 5 && (ABL:0 >= 10 || CFLAG:2 >= 1000 || TALENT:153)
		K = 1
ENDIF

;選択メニュー名を素質で変更用処理
IF FLAG:23 & 1048576 && TARGET >= 0 && TALENT:89
	TSTR:2 = 愛妻弁当販売で稼ぐ
	TSTR:3 = 愛妻弁当を頂く(※お金は稼げません！)
ELSEIF FLAG:23 & 1048576 && TARGET >= 0 && TALENT:153
	TSTR:2 = 愛情弁当販売で稼ぐ
	TSTR:3 = 愛情弁当を頂く(※お金は稼げません！)
ELSE
	TSTR:2 = 弁当販売で稼ぐ
	TSTR:3 = 弁当を作って食べる(※お金は稼げません！)
ENDIF

PRINTFORML どうしますか？
PRINTFORML [0] PVのお仕事
SIF I == 1
	PRINTFORML [1] 地方ラジオのお仕事
SIF J == 1
	PRINTFORML [2] %TSTR:2%
SIF K == 1
	PRINTFORML [3] %TSTR:3%
PRINTFORML [4] 地方TVのお仕事
SIF I == 1
	PRINTFORML [5] 地獄の特訓
PRINTFORML [100] やっぱりやめる

$INPUT_LOOP_MO
INPUT
IF RESULT == 0
	A = RAND:100
	TFLAG:17 = 0
	IF A <= 10
		TFLAG:17 = 1
	ELSEIF A <= 40
		TFLAG:17 = 2
	ELSEIF A <= 70
		TFLAG:17 = 3
	ELSEIF A <= 96
		TFLAG:17 = 4
	ELSE
		TFLAG:17 = 5
	ENDIF
	IF RAND:20 <= ABL:MASTER:90
		TFLAG:17 = 6
	ENDIF
	TFLAG:600 = 16
	CALL KOJO_MESSAGE_SEND("JUN")
	BASE:MASTER:HP -= 500

	;弁当作り判定
	IF TFLAG:17 == 6
		PRINTFORML %CALLNAME:MASTER%は料理を作って……
		PRINTFORML [0] 売ることにした
		PRINTFORML [1] 皆で食べることにした

		$INPUT_MAKE
		INPUT

		IF RESULT < 0 || RESULT > 1
			GOTO INPUT_MAKE
		ELSEIF RESULT == 0
			;CALL LUNCH_SELF_SALE
		ELSEIF RESULT == 1
			;CALL LUNCH_SELF_EAT
		ENDIF
		RETURN 1
	ENDIF

	P = (ABL:MASTER:2 + 10) * (RAND:11 + 15)
	PRINTFORM %CALLNAME:MASTER%は
	IF TFLAG:17 == 1
		P *= 50
		PRINT 内職をして
	ELSEIF TFLAG:17 == 2
		P *= 85
		PRINT 人里でアルバイトをして
	ELSEIF TFLAG:17 == 3
		P *= 100
		SIF TALENT:MASTER:209
			P *= 10
		PRINT 畑仕事を手伝って
	ELSEIF TFLAG:17 == 4
		P *= 120
		PRINT 妖怪退治をして
	ELSE
		P *= 250
		PRINT ギャンブルで成功して
	ENDIF

	SIF ABL:MASTER:86 == 1
		TIMES P , 1.20
	SIF ABL:MASTER:86 == 2
		TIMES P , 1.50
	SIF ABL:MASTER:86 == 3
		TIMES P , 2.00

	PRINTFORMW ${P}の収入を得た
	MONEY += P
	;素材を得る(COMF309.ERB)
	;CALL EARN_SOZAI_1
	RETURN 1
ELSEIF RESULT == 1 && I == 1
	A = RAND:100
	TFLAG:17 = 0
	IF A <= 10
		TFLAG:17 = 1
	ELSEIF A <= 40
		TFLAG:17 = 2
	ELSEIF A <= 70
		TFLAG:17 = 3
	ELSEIF A <= 97
		TFLAG:17 = 4
	ELSE
		TFLAG:17 = 5
	ENDIF
	TFLAG:600 = 17
	CALL KOJO_MESSAGE_SEND("JUN")
	BASE:MASTER:HP -= 300
	BASE:TARGET:0 -= 200
	;変数Bはアクセサリイベント関連で使用
	B = 0
	SIF CFLAG:MASTER:24 == NO:TARGET
		B = 1
	P = (ABL:MASTER:2 * 2 + ABL:TARGET:2 + ABL:TARGET:6 * 2 + B + 15) * (RAND:5 + 8)
	PRINTFORM %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人で
	IF TFLAG:17 == 1
		P *= 60
		PRINT 香霖堂でアルバイトをして
	ELSEIF TFLAG:17 == 2
		P *= 90
		PRINT 内職をして
	ELSEIF TFLAG:17 == 3
		P *= 110
		PRINT 人里でアルバイトをして
	ELSEIF TFLAG:17 == 4
		P *= 140
		PRINT 畑仕事を手伝って
		SIF TALENT:MASTER:209
			P *= 10
	ELSE
		P *= 400
		PRINT 埋蔵金を掘り当てて
	ENDIF

	SIF ABL:MASTER:86 == 1
		TIMES P , 1.20
	SIF ABL:MASTER:86 == 2
		TIMES P , 1.50
	SIF ABL:MASTER:86 == 3
		TIMES P , 2.00

	PRINTFORML ${P}の収入を得た
	MONEY += P
	;素材を得る(COMF309.ERB)
	;CALL EARN_SOZAI_2
	A = RAND:41 + 30 + B
	CFLAG:2 += A
	PRINTL  
	PRINTFORMW 好感度が {A} 増えた
ELSEIF RESULT == 2 && J == 1
	;CALL LUNCH_SALE
ELSEIF RESULT == 3 && K == 1
	;CALL LUNCH_EAT
ELSEIF RESULT == 4
	;CALL TREASURE_MASTER
ELSEIF RESULT == 5 && I == 1
	;CALL TREASURE_TWO
ELSEIF RESULT == 100
	RETURN 0
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_MO
ENDIF

RETURN 1

;@CHANGE_ACTION、@SHOP_ACT_LIST、@ACTION_LISTはSHOP_COM_ACT.ERBに移動

@ARRANGE_CHARANAME
;STRLENS
;文字列の長さを測定し、RESULT:0に代入
;長さはSHIFT-JISでのバイト数です。つまり全角文字を2文字と数えます。 
STRLENS NAME:COUNT
IF RESULT:0 == 2
	PRINT  　　　　　　　
ELSEIF RESULT:0 == 4
	PRINT  　　　　　　
ELSEIF RESULT:0 == 6
	PRINT  　　　　　
ELSEIF RESULT:0 == 8
	PRINT  　　　　
ELSEIF RESULT:0 == 10
	PRINT  　　　
ELSEIF RESULT:0 == 12
	PRINT  　　
ELSEIF RESULT:0 == 14
	PRINT  　
ELSE
	PRINT  
ENDIF

;-------------------------------------------------
;強くてニューゲーム
;-------------------------------------------------
@NEWGAME_SELECT
PRINTFORML 1日目、Stage1に戻って{FLAG:26 + 1}周目を開始します。よろしいですか？
PRINTL [0] はい
PRINTL [1] いいえ

$INPUT_LOOP_NEW
INPUT

IF RESULT == 1
	RETURN 1
ELSEIF RESULT != 0 && RESULT != 1
	PRINTL 正しい値を入力してください
	GOTO INPUT_LOOP_NEW
ENDIF

TRYCALL ADD_REIMUSAMA
TRYCALL ADD_SANAESAMA
TRYCALL ADD_EXRUMIA
PRINTL それでは1日目、Stage1に戻ります。
;日付初期化
DAY = 0
TIME = 0
;Stage1に戻る
FLAG:24 = 1
;ボスをランダムに
FLAG:25 = 0
;周回数加算
FLAG:26 += 1
;Stage進度リセット
FLAG:27 = 0

VARSET LOCALS, ""
LOCALS:0 = EASY
LOCALS:1 = NORMAL
LOCALS:2 = HARD
LOCALS:3 = LUNATIC
LOCALS:4 = S-LUNATIC
LOCALS:11 = I-EASY
LOCALS:12 = I-NORMAL
LOCALS:13 = I-HARD
LOCALS:14 = I-LUNATIC
LOCALS:15 = I-S-LUNATIC

;モードを変更する処理を追加
IF FLAG:5 >= 0 && FLAG:5 <= 4
	PRINTFORMW 難易度を変更できますがどうしますか？　現在は%LOCALS:(FLAG:3)%モードです
	PRINTL ※現在と同じ難易度を選択すると変更されません
	
	PRINTL [ 0]EASY   （Stage数*15日期限）
	PRINTL [ 1]NORMAL （Stage数*10日期限）
	PRINTL [ 2]HARD   （Stage数* 8日期限）
	PRINTL [ 3]LUNATIC（Stage数* 5日期限）
	PRINTL 
	PRINTL [20]INVISIBLE　※ステータスが見えない特殊モードです
	PRINTL [100] 変更しない
	
	$INPUT_LOOP_MODE
	INPUT
	
	IF RESULT == 100 || RESULT == FLAG:3
		PRINTW 変更しませんでした
	ELSEIF RESULT >= 0 && RESULT <= 4
		;攻略ルート
		FLAG:3 = RESULT
		;難易度
		FLAG:5 = RESULT
		;インビジブルモードOFF
		FLAG:2 = 0
		PRINTFORMW %LOCALS:(FLAG:3)%モードに変更しました
	ELSEIF RESULT == 20
		PRINTL ★★INVISIBLEモードの難易度を選択してください★★
		PRINTL [11]I-EASY   （Stage数*15日期限）
		PRINTL [12]I-NORMAL （Stage数*10日期限）
		PRINTL [13]I-HARD   （Stage数* 8日期限）
		PRINTL [14]I-LUNATIC（Stage数* 5日期限）
		PRINTL 
		PRINTL [100] 変更しない
		
		$INPUT_LOOP_MODE_2
		INPUT
		IF RESULT == 100 || RESULT == FLAG:3
			PRINTW 変更しませんでした
		ELSEIF RESULT >= 11 && RESULT <= 15
			;攻略ルート
			FLAG:3 = RESULT
			;I-LUNATICとI-S-LUNAは主人公組（I-LUNATICは神霊廟組にしたかったけれど未実装なので……）
			IF RESULT == 14 || RESULT == 15
				FLAG:3 = RESULT - 11
			ENDIF
			;難易度
			FLAG:5 = RESULT - 11
			;インビジブルモードON
			FLAG:2 = 1
			PRINTFORMW %LOCALS:(FLAG:3)%モードに変更しました
		ENDIF
	ELSE
		PRINTL 正しい値を入力してください
		GOTO INPUT_LOOP_MODE_2
	ENDIF
ENDIF
;攻略ルート選択(デフォルトでは選択不可能。選択可能にしたい場合はSYSTEM_DEBUG.ERBの該当関数参照。)
;CALL EDIT_GAME_ROOT
;エンゲージリングを渡している場合
REPEAT CHARANUM
	IF TALENT:COUNT:89
		PRINTFORML 現在、%CALLNAME:COUNT%が婚約者となっていますが
		PRINTFORML 婚約を破棄しますか？
		PRINTL [0] はい
		PRINTL [1] いいえ
		$INPUT_LOOP_ENGAGEMENT
		INPUT
		SIF RESULT != 0 && RESULT != 1
			GOTO INPUT_LOOP_ENGAGEMENT
		IF RESULT == 0
			PRINTFORMW %CALLNAME:COUNT%との婚約を破棄しました
			;相愛の喪失
			TALENT:COUNT:89 = 0
			;別れる不可フラグを落とす
			CFLAG:COUNT:7 = 0
			;結婚記念日の初期化
			TIME:9 = 0
			TIME:10 = 0
			;指輪は渡していない事にする
			FLAG:29 = 0
		ENDIF
	ENDIF
REND

PRINTL 
PRINTL オープニングストーリーを見ますか？
PRINTL [0] はい
PRINTL [1] いいえ
$INPUT_LOOP_OP
INPUT
IF RESULT < 0 || RESULT > 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_OP
ELSEIF RESULT == 0
	TFLAG:600 = 28
	CALL KOJO_MESSAGE_SEND("JUN")
ENDIF
RETURN 1

;-------------------------------------------------
;ボス選択
;-------------------------------------------------
@BOSS_CHOICE
$INPUT_LOOP_BOSS
CSVCALLNAME 1, 0
TSTR:1 = %RESULTS%
CSVCALLNAME 2, 0
TSTR:2 = %RESULTS%
PRINTFORML 戦いたいボスのキャラ番号を入力してください。(例：%TSTR:1%→1 %TSTR:2%→2)
PRINTFORML 基本的にチートなので、変な入力をされても動作は保証しません。
PRINTFORML (キャラが存在しない番号はやめてね)
PRINTL  
PRINTFORML それでは入力をどうぞ。
IF FLAG:25
	T = FLAG:25
	CALL GET_CHARA_NAME
	PRINTFORML 現在出現予定のボス：%STR:N%
ELSE
	PRINTFORML 現在出現予定のボス：なし
ENDIF

FOR LOCAL, 1, 200
	T = LOCAL
	EXISTCSV T,0
	IF RESULT == 1
		CALL GET_CHARA_NAME
		RESULT = 0
		;口上があるキャラは強調表示
		TRYCALLFORM KOJO_EXIST_K{LOCAL}
		SIF RESULT == 1
			SETCOLOR 0xFFFFF
		PRINTFORMLC [{T,3}] %STR:N%
		RESETCOLOR
	ENDIF
NEXT
PRINTL 
PRINTFORML [ -2] 現在出現予定のボス番号をキャンセル
PRINTL [1000][-1] 戻る

INPUT
IF RESULT == 0
	PRINTFORMW あなたとは戦えません。残念でした
	GOTO INPUT_LOOP_BOSS
ELSEIF RESULT == -2
	PRINTFORMW 現在出現予定のボス番号をキャンセルしました。次のボスはランダムで選ばれます
	FLAG:25 = 0
ELSEIF RESULT == -1 || RESULT == 1000
	PRINTFORMW キャンセルしました
	RETURN 1
ELSE
	;CSVが存在しない番号（存在すればRESULT = 1）は選択できない
	T = RESULT
	EXISTCSV T,0
	IF RESULT == 0
		PRINTFORML 止めてくださいってば。チートのバグ取りとかしなくて良いですって
		GOTO INPUT_LOOP_BOSS
	ENDIF
	CSVCALLNAME T, 0
	PRINTFORMW %RESULTS%をボスに設定しました。既に仲間にいる場合は、幻影との勝負になります
	FLAG:25 = T
ENDIF
RETURN 1
